
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2018 Job and Esther Technologies Oy
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class:

import sympathy
import capex.web

var authServerUrl static as string

func isDummy static as bool
{
	return String.equals("yes", Environment.getVariable("AUTH_SERVER_DUMMY"))
}

func setAuthServerUrl(url as string) static:
	authServerUrl = url

func getAuthServerUrl static as string
{
	if authServerUrl == null:
		authServerUrl = Environment.getVariable("AUTH_SERVER_URL")
	return authServerUrl
}

func getApiClient static as JSONAPIClient
{
	var url = assert getAuthServerUrl()
	var v = new JSONAPIClient()
	v.setApiUrl(url)
	return v
}

func sessionidToUserid(sessionid as string, callback as function<void,string>) static
{
	if isDummy() {
		callback(sessionid)
		return
	}
	// FIXME: Here we could and should implement a caching mechanism for the sessionid->userid mapping,
	// even if short term, 15/30 seconds or so...
	var api = assert getApiClient():
		callback(null)
	var params = new DynamicMap()
	params.set("sessionid", sessionid)
	api.postWithStatus("checkSession", params, func(v as DynamicMap, error as Error) {
		assert error == null && v:
			callback(null)
		callback(v.getString("userid"))
	})
}

func handleAuthenticatedRequest(req as HTTPServerRequest, handler as function<void,string,DynamicMap>) static
{
	assert handler && req:
		req.sendJSONError(ServerError.internalError())
	var params = assert req.getBodyJSONMap():
		req.sendJSONError(ServerError.invalidRequest())
	var sessionid = assert params.getString("sessionid"):
		req.sendJSONError(ServerError.invalidRequest())
	params.remove("sessionid")
	sessionidToUserid(sessionid, func(userid as string) {
		assert String.isNotEmpty(userid):
			req.sendJSONError(ServerError.invalidSession())
		handler(userid, params)
	})
}
