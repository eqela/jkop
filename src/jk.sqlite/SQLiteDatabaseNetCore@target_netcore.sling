
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2018 Job and Esther Technologies Oy
 * Copyright (c) 2018-2019 Eqela Oy
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class is SQLiteDatabase:

IFDEF "target_netcore"
{
	depend "nuget:Microsoft.Data.SQLite@2.1.*"
}

class Statement is SQLStatement
{
	var pcounter = 0
	prop sqlString as string
	var isStoredProcedure = false

	func setIsStoredProcedure(v as bool) as SQLStatement
	{
		isStoredProcedure = v
		return this
	}

	func getIsStoredProcedure as bool:
		return isStoredProcedure

	lang "cs" {{{
		public Microsoft.Data.Sqlite.SqliteCommand command;

		public bool create(Microsoft.Data.Sqlite.SqliteConnection db, string sql) {
			if(db == null || sql == null) {
				return(false);
			}
			var cmd = db.CreateCommand();
			if(cmd == null) {
				return(false);
			}
			cmd.CommandText = SQLAdoNet.convertSQLString(sql);
			command = cmd;
			sqlString = sql;
			return(true);
		}
	}}}

	func addParamString(val as string) as SQLStatement
	{
		var vv #used = val
		lang "cs" {{{
			if(vv == null) {
				vv = "";
			}
			if(command != null) {
				var name = "@p" + (pcounter+1);
				var param = command.Parameters.Add(name, Microsoft.Data.Sqlite.SqliteType.Text);
				if(param != null) {
					pcounter ++;
					param.Value = vv;
				}
			}
		}}}
		return this
	}

	func addParamInteger(val as int) as SQLStatement
	{
		lang "cs" {{{
			if(command != null) {
				var param = command.Parameters.Add("@p" + (pcounter+1), Microsoft.Data.Sqlite.SqliteType.Integer);
				if(param != null) {
					pcounter ++;
					param.Value = val;
				}
			}
		}}}
		return this
	}

	func addParamLongInteger(val as long) as SQLStatement
	{
		lang "cs" {{{
			if(command != null) {
				var param = command.Parameters.Add("@p" + (pcounter+1), Microsoft.Data.Sqlite.SqliteType.Integer);
				if(param != null) {
					pcounter ++;
					param.Value = val;
				}
			}
		}}}
		return this
	}

	func addParamDouble(val as double) as SQLStatement
	{
		lang "cs" {{{
			if(command != null) {
				var param = command.Parameters.Add("@p" + (pcounter+1), Microsoft.Data.Sqlite.SqliteType.Real);
				if(param != null) {
					pcounter ++;
					param.Value = val;
				}
			}
		}}}
		return this
	}

	func addParamBlob(val as buffer) as SQLStatement
	{
		if val == null:
			return this
		lang "cs" {{{
			if(command != null) {
				var param = command.Parameters.Add("@p" + (pcounter+1), Microsoft.Data.Sqlite.SqliteType.Blob);
				if(param != null) {
					pcounter ++;
					param.Value = val;
				}
			}
		}}}
		return this
	}

	func setParamString(name as string, val as string) as SQLStatement:
		return addParamString(val)

	func setParamInteger(name as string, val as int) as SQLStatement:
		return addParamInteger(val)

	func setParamLongInteger(name as string, val as long) as SQLStatement:
		return addParamLongInteger(val)

	func setParamDouble(name as string, val as double) as SQLStatement:
		return addParamDouble(val)

	func setParamBlob(name as string, val as buffer) as SQLStatement:
		return addParamBlob(val)

	func resetStatement
	{
		lang "cs" {{{
			if(command != null) {
				command.Parameters.Clear();
			}
		}}}
		pcounter = 0
	}

	func getError as string:
		return null

	func close:
		nop
}

lang "cs" {{{
	Microsoft.Data.Sqlite.SqliteConnection db;
}}}

func initialize(file as File, openCreate as bool) override as bool
{
	assert file
	if openCreate == false && file.isFile() == false {
		Log.error(getLogger(), "Database does not exist: " .. file.getPath())
		return false
	}
	var nativepath = assert file.getPath():
		Log.error(getLogger(), "Database file '" .. file.getPath() .. "' is not on a native filesystem. Cannot open.")
	var v = false
	var error as string = null
	lang "cs" {{{
		var db = new Microsoft.Data.Sqlite.SqliteConnection("Data Source=" + nativepath);
		try {
			db.Open();
			v = true;
		}
		catch(System.Exception e) {
			error = e.ToString();
			v = false;
		}
		this.db = db;
	}}}
	if v {
		Log.debug(getLogger(), "Opened Sqlite database: " .. file.getPath())
	}
	else {
		Log.error(getLogger(), "Failed to open database file: " .. error)
	}
	return v
}

func close override
{
	lang "cs" {{{
		if(db != null) {
			db.Close();
			db = null;
		}
	}}}
}

func prepare(sql as string) override as SQLStatement
{
	var v = new Statement()
	var ss #used = sql
	lang "cs" {{{
		if(v.create(db, ss) == false) {
			return(null);
		}
	}}}
	return v
}

func execute(stmt as SQLStatement) override as bool
{
	assert stmt
	var v = true
	var error as string = null
	lang "cs" {{{
		var cmd = ((Statement)stmt).command;
		if(cmd == null) {
			return(false);
		}
		try {
			cmd.ExecuteNonQuery();
		}
		catch(System.Exception e) {
			v = false;
			error = e.ToString();
		}
	}}}
	if error {
		var lg = getLogger()
		Log.error(lg, "Error for SQL statement: `" .. (stmt as! Statement).getSqlString() .. "'")
		Log.error(lg, "SQLite execute error: " .. error)
	}
	return v
}

func executeUpdateDelete(stmt as SQLStatement) override as int
{
	var v = 0
	if stmt {
		var error as string = null
		lang "cs" {{{
			var cmd = ((Statement)stmt).command;
			if(cmd == null) {
				return(0);
			}
			try {
				v = cmd.ExecuteNonQuery();
			}
			catch(System.Exception e) {
				v = 0;
				error = e.ToString();
			}
		}}}
		if error {
			var lg = getLogger()
			Log.error(lg, "Error for SQL statement: `" .. (stmt as! Statement).getSqlString() .. "'")
			Log.error(lg, "SQLite execute error: " .. error)
		}
	}
	return v
}

class ResultSet is SQLResultSetIterator
{
	var reader public as !"Microsoft.Data.Sqlite.SqliteDataReader"
	var hasNextRecord as bool

	dtor:
		close()

	func close override
	{
		if reader {
			reader.Close()
			reader = null
		}
	}

	func nextValues(values as vector<object>) override as bool
	{
		assert values
		assert step()
		Vector.clear(values)
		var cols = getColumnCount()
		var n as int
		for(n = 0; n < cols; n++) {
			var o = getColumnObject(n)
			if o == null:
				o = ""
			values += o
		}
		return true
	}

	func step override as bool
	{
		assert reader
		if hasNextRecord {
			hasNextRecord = false
			return true
		}
		lang "cs" {{{
			if(reader.Read() == false) {
				reader.Close();
				reader = null;
				return(false);
			}
		}}}
		return true
	}

	func getColumnCount override as int
	{
		var v = 0
		lang "cs" {{{
			if(reader != null) {
				v = reader.FieldCount;
			}
		}}}
		return v
	}

	func getColumnNames override as vector<string>
	{
		var v = new vector<string>
		var n as int
		var c = getColumnCount()
		for(n = 0; n < c; n++) {
			var nn = getColumnName(n)
			if nn == null:
				nn = ""
			v += nn
		}
		return v
	}

	func getColumnName(n as int) override as string
	{
		var v as string = null
		lang "cs" {{{
			if(reader != null) {
				v = reader.GetName(n);
			}
			if(v == null) {
				v = "";
			}
		}}}
		return v
	}

	func getColumnObject(n as int) override as object
	{
		var v as string = null
		lang "cs" {{{
			if(reader.IsDBNull(n)) {
				return(null);
			}
			if(reader.GetFieldType(n) == typeof(byte[])) {
				var b = (byte[])reader.GetValue(n);
				return(jk.lang.Buffer.asObject(b));
			}
			var o = reader.GetValue(n);
			if(o == null) {
				return(null);
			}
			v = o.ToString();
			if(v == null) {
				return(null);
			}
		}}}
		return v
	}

	func getColumnInt(n as int) override as int
	{
		var v = 0
		lang "cs" {{{
			if(reader != null) {
				v = reader.GetInt32(n);
			}
		}}}
		return v
	}

	func getColumnLong(n as int) override as long
	{
		var v as long = 0
		lang "cs" {{{
			if(reader != null) {
				v = reader.GetInt64(n);
			}
		}}}
		return v
	}

	func getColumnDouble(n as int) override as double
	{
		var v as double = 0
		lang "cs" {{{
			if(reader != null) {
				v = reader.GetDouble(n);
			}
		}}}
		return v
	}

	func getColumnBuffer(n as int) override as buffer
	{
		var v as buffer = null
		lang "cs" {{{
			if(reader != null) {
				v = (byte[])reader.GetValue(n);
			}
		}}}
		return v
	}

	func next override as DynamicMap
	{
		assert step()
		var names = getColumnNames()
		if names == null || Vector.getSize(names) < 1:
			return null
		var v = new DynamicMap()
		var n as int
		for(n = 0; n < Vector.getSize(names); n++):
			v.set(Vector.get(names, n), getColumnObject(n))
		return v
	}

	func hasNext override as bool
	{
		if hasNextRecord:
			return true
		var v = step()
		if v {
			hasNextRecord = true
			return true
		}
		return false
	}
}

func query(stmt as SQLStatement) override as SQLResultSetIterator
{
	assert stmt:
		Log.debug(getLogger(), "SQLite query: null statement")
	var v as ResultSet = null
	var error as string = null
	lang "cs" {{{
		var cmd = ((Statement)stmt).command;
		if(cmd == null) {
	}}}
	Log.debug(getLogger(), "SQLite query: null command")
	lang "cs" {{{
		return(null);
		}
		Microsoft.Data.Sqlite.SqliteDataReader rdr = null;
		try {
			rdr = cmd.ExecuteReader();
		}
		catch(System.Exception e) {
			error = e.ToString();
		}
		if(rdr != null) {
			v = new ResultSet();
			v.reader = rdr;
		}
	}}}
	if error {
		var lg = getLogger()
		Log.error(lg, "Error for SQL statement: `" .. (stmt as! Statement).getSqlString() .. "'")
		Log.error(lg, "SQLite query error: " .. error)
	}
	return v
}

func close(callback as function) override
{
	close()
	if callback:
		callback()
}

func execute(stmt as SQLStatement, callback as function<void, bool>) override
{
	if callback:
		callback(execute(stmt))
}

func executeUpdateDelete(stmt as SQLStatement, callback as function<void, int>) override
{
	if callback:
		callback(executeUpdateDelete(stmt))
}

func query(stmt as SQLStatement, callback as function<void, SQLResultSetIterator>) override
{
	if callback:
		callback(query(stmt))
}

func querySingleRow(stmt as SQLStatement, callback as function<void, DynamicMap>) override
{
	if callback:
		callback(querySingleRowSync(stmt))
}

func tableExists(table as string, callback as function<void, bool>) override
{
	if callback:
		callback(tableExists(table))
}

func getPrimaryKeyColumnName(table as string) override as string
{
	var itr = assert query(prepare("PRAGMA TABLE_INFO(" .. table .. ");"))
	loop {
		var v = itr.next()
		if not v:
			break
		if v.getInteger("pk") == 1:
			return v.getString("name")
	}
	return null
}
