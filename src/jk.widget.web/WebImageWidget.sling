
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2018 Job and Esther Technologies Oy
 * Copyright (c) 2018-2019 Eqela Oy
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.web
import jk.log

class is AsynchronousImageWidget #widget:

func forPlaceholderImage(context as GuiApplicationContext, image as Image) static as this
{
	var v = new this(context)
	v.setWidgetPlaceholderImage(image)
	return v
}

pvar widgetUseOverlay = false

ctor(context as GuiApplicationContext):
	base(context)

func setWidgetImageResource(resName as string)
{
	var img = onStartLoading(false)
	if img:
		img.setWidgetImageResource(resName)
	onEndLoading()
}

func setWidgetImage(image as Image)
{
	var img = onStartLoading(false)
	if img:
		img.setWidgetImage(image)
	onEndLoading()
}

func setWidgetImageUrl(url as string, callback as function<void,Error> = null):
	setWidgetImageUrl(url, null, null, callback)

func setWidgetImageUrl(url as string, headers as KeyValueList<string,string>, body as buffer, callback as function<void,Error>) virtual
{
	Log.debug(context, VALUE "__THIS__" .. ": Start loading image: `" .. url .."'")
	var img final = onStartLoading(widgetUseOverlay)
	IFDEF "target_html" {
		if not headers {
			var path #used = url
			var imgo = new ImageForBrowser()
			var image as object
			lang "js" {{{
				image = new Image();
				image.src = path;
			}}}
			imgo.image = image
			img.setWidgetImage(imgo)
			return
		}
	}
	doQueryImage(context, url, headers, body, func(imgo as Image) {
		onEndLoading()
		img.setWidgetImage(imgo)
	}, callback)
}

func doQueryImage(context as GuiApplicationContext, url as string, headers as KeyValueList<string,string>, body as buffer, callback as function<void, Image>, errorCallback as function<void, Error>) static
{
	var client = assert NativeWebClient.instance() {
		Log.error(context, "Failed to create web client.")
		if errorCallback:
			errorCallback(Error.forCode("noWebClient"))
	}
	var uu final = url
	var cb final = callback
	var ecb final = errorCallback
	var contextf final = context
	client.query("GET", url, headers, body, func(rcode as string, rheaders as KeyValueList<string,string>, rbody as buffer) {
		if rbody == null || Buffer.getSize(rbody) < 1 {
			Log.error(contextf, VALUE "__THIS__" .. ": FAILED loading image: `" .. uu .."'")
			if ecb:
				ecb(Error.forCode("failedToDownload"))
			return
		}
		var mimeType as string
		var hdrv as vector<KeyValuePair<string,string>> #force = rheaders.asVector()
		foreach hdr in hdrv {
			if String.equalsIgnoreCase(hdr.key, "content-type") {
				var vv = hdr.value
				if vv {
					var sc = String.getIndexOfCharacter(vv, ';')
					if sc < 0 {
						mimeType = vv
					}
					else {
						mimeType = String.getEndOfString(vv, sc)
					}
				}
			}
		}
		contextf.getImageForBuffer(rbody, mimeType, func(imgo as Image) {
			if not imgo {
				Log.error(contextf, VALUE "__THIS__" .. ": Failed to create image from the returned data")
				if ecb:
					ecb(Error.forCode("failedToCreateImage"))
				return
			}
			cb(imgo)
		})
	})
}
