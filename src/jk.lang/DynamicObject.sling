
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2022 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class:

class StringIteratorWrapper is Iterator<dynamic>
{
	pvar iterator as CharacterIterator

	func next as dynamic
	{
		if not iterator:
			return null
		var c = iterator.getNextChar()
		if Character.isEOF(c) {
			iterator = null
			return null
		}
		IFDEF "target_objc" {
			return lang "objc" dynamic {{{ [NSNumber numberWithUnsignedInt:c] }}}
		}
		ELSE {
			return c
		}
	}

	func hasNext as bool
	{
		assert iterator
		return not iterator.hasEnded()
	}
}

class BufferIteratorWrapper is Iterator<dynamic>
{
	pvar buffer as buffer
	pvar size as long
	var position = 0

	func next as dynamic
	{
		if not buffer:
			return null
		if position >= size:
			return null
		var v = buffer[position]
		position++
		IFDEF "target_objc" {
			return lang "objc" dynamic {{{ [NSNumber numberWithUnsignedInt:v] }}}
		}
		ELSE {
			return v
		}
	}

	func hasNext as bool
	{
		assert buffer
		assert position < size - 1
		return true
	}
}

class ArrayIteratorWrapper is Iterator<dynamic>
{
	pvar array as array
	pvar size as long
	var position = 0

	func next as dynamic
	{
		if not array:
			return null
		if position >= size:
			return null
		var v = array[position]
		position++
		return v
	}

	func hasNext as bool
	{
		assert array
		assert position < size - 1
		return true
	}
}

class VectorIteratorWrapper is Iterator<dynamic>
{
	pvar vector as vector
	pvar size as long
	var position = 0

	func next as dynamic
	{
		if not vector:
			return null
		if position >= size:
			return null
		var v = vector[position]
		position++
		return v
	}

	func hasNext as bool
	{
		assert vector
		assert position < size - 1
		return true
	}
}

func iterate(value as dynamic) static as Iterator<dynamic>
{
	if value is string {
		var v = new StringIteratorWrapper()
		v.setIterator(String.iterate(value as! string))
		return v
	}
	if value is buffer {
		var v = new BufferIteratorWrapper()
		v.setBuffer(value as! buffer)
		v.setSize(sizeof value as! buffer)
		return v
	}
	if value is array {
		var v = new ArrayIteratorWrapper()
		v.setArray(value as! array)
		v.setSize(sizeof value as! array)
		return v
	}
	if value is vector {
		var v = new VectorIteratorWrapper()
		v.setVector(value as! vector)
		v.setSize(sizeof value as! vector)
		return v
	}
	if value is map:
		return iterate(Map.getValues(value as! map))
	return null
}

func iteratePropertyNames(object as dyno) static as Iterator<string>:
	return Vector.iterate(getPropertyNames(object))

func getPropertyNames(object as dyno) static as vector<string>
{
	assert object
	IFDEF "target_js" {
		var v = new vector<string>
		lang "js" {{{
			for(var name in object) {
				v.append(name);
			}
		}}}
		return v
	}
	ELSE {
		ERROR "Not implemented."
		return null
	}
}

func getPropertyValue(object as dyno, key as string) static as dynamic
{
	assert object
	assert key
	IFDEF "target_js" {
		return lang "js" dynamic {{{ object[key] }}}
	}
	ELSE {
		ERROR "Not implemented."
		return null
	}
}
