
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2017 Job and Esther Technologies, Inc.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class is WidgetWithValue #widget:

func forKeyValueList(context as GuiApplicationContext, options as KeyValueList<string, string>) static as this
{
	var v  = new this(context)
	v.setWidgetItemsAsKeyValueList(options)
	return(v)
}

func forKeyValueStrings(context as GuiApplicationContext, options as array) static as this
{
	var v = new this(context)
	v.setWidgetItemsAsKeyValueStrings(options)
	return(v)
}

func forKeyValueStrings(context as GuiApplicationContext, options as vector<string>) static as this
{
	var v = new this(context)
	v.setWidgetItemsAsKeyValueStrings(options)
	return(v)
}

func forStringList(context as GuiApplicationContext, options as array) static as this
{
	var v = new this(context)
	v.setWidgetItemsAsStringList(options)
	return(v)
}

func forStringList(context as GuiApplicationContext, options as vector<string>) static as this
{
	var v = new this(context)
	v.setWidgetItemsAsStringList(options)
	return(v)
}

func forStringList(context as GuiApplicationContext, options as DynamicVector) static as this
{
	var v = new this(context)
	v.setWidgetItemsAsStringList(options)
	return(v)
}

IFDEF("target_android")
{
	basetype !"android.widget.Spinner" type extends
	basetype !"android.widget.AdapterView.OnItemSelectedListener" type implements
	basetype Widget type isAlso

	var ignoreInitialSelectEvent private = true
	var selectedWidgetIndex = -1
	var adapter as !"android.widget.ArrayAdapter<java.lang.String>"

	func requestLayout
	{
		Widget.onChanged(this)
		base.requestLayout()
	}

	class MyAdapter is !"android.widget.ArrayAdapter<java.lang.String>"
	{
		ctor(ctx as !"android.content.Context", resId as int, obj as !"java.util.List<java.lang.String>")
		{
			base(ctx, resId, obj)
			widgetContext = ctx
		}

		var widgetContext as !"android.content.Context"
		var tv as !"android.widget.TextView"
		var queueSize as double
		var queueFontFamily as string
		var queueFontResource as string
		var queueTextColor as Color

		func getView(pos as int, view as !"android.view.View", viewGroup as !"android.view.ViewGroup") as !"android.view.View"
		{
			lang "java" {{{
				tv = (android.widget.TextView)super.getView(pos, view, viewGroup);
			}}}
			tv.setPadding(0, 0, 0, 0)
			updateFont()
			updateColor()
			return tv
		}

		func setWidgetFontSize(sz as double)
		{
			queueSize = sz
			updateFont()
		}

		func setWidgetFontFamily(ff as string)
		{
			queueFontFamily = ff
			updateFont()
		}

		func setWidgetFontResource(res as string)
		{
			queueFontResource = res
			updateFont()
		}

		func setWidgetTextColor(color as Color)
		{
			queueTextColor = color
			updateColor()
		}

		func updateFont
		{
			if tv {
				lang "java" {{{
					tv.setTextSize(android.util.TypedValue.COMPLEX_UNIT_PX, (float)queueSize);
					if(queueFontResource != null) {
						tv.setTypeface(android.graphics.Typeface.createFromAsset(widgetContext.getAssets(), queueFontResource), android.graphics.Typeface.NORMAL);
					}
					else {
						tv.setTypeface(android.graphics.Typeface.create(queueFontFamily, android.graphics.Typeface.NORMAL));
					}
				}}}
			}
		}

		func updateColor
		{
			if tv {
				lang "java" {{{
					if(queueTextColor != null) {
						tv.setTextColor(queueTextColor.toARGBInt32());
					}
				}}}
			}
		}
	}

	lang "java" {{{
		public void onItemSelected(android.widget.AdapterView<?> parent, android.view.View view, int position, long id) {
			selectedWidgetIndex = position;
			android.widget.TextView sview = (android.widget.TextView)parent.getChildAt(0);
			if(sview != null) {
				cave.Color textColor = getActualWidgetTextColor();
				sview.setTextColor(textColor.toARGBInt32());
				sview.setEllipsize(android.text.TextUtils.TruncateAt.END);
				sview.setSingleLine();
			}
			if(ignoreInitialSelectEvent) {
				ignoreInitialSelectEvent = false;
				return;
			}
			onWidgetSelectionChanged();
		}

		public void onNothingSelected(android.widget.AdapterView<?> parent) {
			selectedWidgetIndex = -1;
			onWidgetSelectionChanged();
		}
	}}}
}

IFDEF("target_iosoc")
{
	basetype !"UITextField" type extends
	basetype !"UIPickerViewDelegate" type implements
	basetype Widget type isAlso

	var pickerView as !"UIPickerView"

	lang "objc" {{{
		- (NSInteger)numberOfComponentsInPickerView:(UIPickerView *)pickerView {
			return 1;
		}

		- (NSInteger)pickerView:(UIPickerView *)pickerView numberOfRowsInComponent:(NSInteger)component {
			return([self getWidgetItemCount]);
		}

		- (NSString *)pickerView:(UIPickerView *)pickerView titleForRow:(NSInteger)row forComponent:(NSInteger)component {
			return([self getWidgetTextForIndex:row]);
		}

		- (void)pickerView:(UIPickerView *)pickerView didSelectRow:(NSInteger)row inComponent:(NSInteger)component {
			[self onWidgetSelectionChanged];
		}

		- (CGRect)caretRectForPosition:(UITextPosition *)position {
			return CGRectZero;
		}

		- (CGRect)textRectForBounds:(CGRect)bounds {
			return CGRectInset(bounds, widgetPadding, widgetPadding);
		}

		- (CGRect)editingRectForBounds:(CGRect)bounds {
			return CGRectInset(bounds, widgetPadding, widgetPadding);
		}
	}}}
}

IFDEF("target_html")
{
	basetype Widget

	func createElement override as object
	{
		var v = HTMLDOM.createElement("select")
		var myObject #used = this
		lang "js" {{{
			v.onchange = function() {
				myObject.onWidgetSelectionChanged();
			};
		}}}
		return(v)
	}
}

IFDEF "target_uwp"
{
	basetype !"Windows.UI.Xaml.Controls.ComboBox" type extends
	basetype Widget type isAlso
}

var widgetContext as GuiApplicationContext
var widgetItems as KeyValueList<string, string>
var widgetValueChangeHandler as function
var widgetBackgroundColor as Color
var widgetTextColor as Color
var widgetPadding as int
var widgetFontFamily as string
var widgetFontResource as string
var widgetFontSize as double

ctor(context as GuiApplicationContext)
{
	IFDEF("target_android") {
		base((context as! cave.GuiApplicationContextForAndroid).getActivityContext(), lang "java" int {{{ android.widget.Spinner.MODE_DROPDOWN }}})
	}
	widgetContext = context
	setWidgetStyle(VALUE "__THIS__")
	IFDEF("target_android") {
		lang "java" {{{
			setOnItemSelectedListener(this);
		}}}
	}
	IFDEF("target_iosoc") {
		lang "objc" {{{
			UIPickerView *picker = [[UIPickerView alloc] init];
			picker.delegate = self;
			picker.dataSource = self;
			picker.showsSelectionIndicator = YES;
			self.inputView = picker;
			[self setPlaceholder:@"(select)"];
			UIToolbar *toolBar= [[UIToolbar alloc] initWithFrame:CGRectMake(0,0,320,44)];
			[toolBar setBarStyle:UIBarStyleDefault];
			UIBarButtonItem *flexButton = [[UIBarButtonItem alloc] initWithBarButtonSystemItem:UIBarButtonSystemItemFlexibleSpace target:self action:nil];
			UIBarButtonItem *barButtonDone = [[UIBarButtonItem alloc] initWithTitle:@"Done" style:UIBarButtonItemStyleBordered target:self action:@selector(onPickerDone)];
			toolBar.items = @[flexButton, barButtonDone];
			toolBar.userInteractionEnabled = YES;
			self.inputAccessoryView = toolBar;
			pickerView = picker;
		}}}
	}
	updateWidgetAppearance()
}

func setWidgetStyle(style as string) as this
{
	widgetFontFamily = widgetContext.getStyleString(style, "fontFamily")
	if String.isEmpty(widgetFontFamily):
		widgetFontFamily = "Arial"
	widgetFontSize = widgetContext.getStylePixels(style, "fontSize")
	if widgetFontSize < 1.0:
		widgetFontSize = widgetContext.getHeightValue("3mm")
	widgetFontResource = widgetContext.getStyleString(style, "fontResource")
	widgetTextColor = widgetContext.getStyleColor(style, "textColor")
	widgetBackgroundColor = widgetContext.getStyleColor(style, "backgroundColor")
	widgetPadding = widgetContext.getStylePixels(style, "padding")
	updateWidgetFont()
	updateWidgetAppearance()
	return this
}

func setWidgetFontFamily(family as string) as this
{
	widgetFontFamily = family
	updateWidgetFont()
	Widget.onChanged(this)
	return(this)
}

func setWidgetFontResource(res as string) as this
{
	widgetFontResource = res
	updateWidgetFont()
	Widget.onChanged(this)
	return(this)
}

func setWidgetFontSize(fontSize as double #notLiteral) as this
{
	widgetFontSize = fontSize
	updateWidgetFont()
	Widget.onChanged(this)
	return(this)
}

func updateWidgetFont private
{
	IFDEF("target_html") {
		HTMLDOM.setFontFamily(element, widgetFontFamily)
		HTMLDOM.setStyle(element, "font-size", String.forDouble(widgetFontSize) .. "px")
	}
	ELSE IFDEF("target_iosoc") {
		lang "objc" {{{
			[self setMinimumFontSize:widgetFontSize];
		}}}
	}
	ELSE IFDEF("target_android") {
		if adapter {
			(adapter as! MyAdapter).setWidgetFontSize(widgetFontSize)
			(adapter as! MyAdapter).setWidgetFontFamily(widgetFontFamily)
			(adapter as! MyAdapter).setWidgetFontResource(widgetFontResource)
			(adapter as! MyAdapter).setWidgetTextColor(widgetTextColor)
		}
	}
	ELSE {
		ERROR("Not implemented")
	}
}

func setWidgetPadding(value as int #notLiteral) as this
{
	widgetPadding = value
	updateWidgetAppearance()
	return(this)
}

func getWidgetPadding as int
{
	return(widgetPadding)
}

func setWidgetTextColor(color as Color) as this
{
	widgetTextColor = color
	updateWidgetAppearance()
	return(this)
}

func getWidgetTextColor as Color
{
	return(widgetTextColor)
}

func setWidgetBackgroundColor(color as Color) as this
{
	widgetBackgroundColor = color
	updateWidgetAppearance()
	return(this)
}

func getWidgetBackgroundColor as Color
{
	return(widgetBackgroundColor)
}

func getActualWidgetTextColor as Color
{
	var textColor = widgetTextColor
	if(textColor == null) {
		if(widgetBackgroundColor != null) {
			if(widgetBackgroundColor.isLightColor()) {
				textColor = Color.black()
			}
			else {
				textColor = Color.white()
			}
		}
		else {
			textColor = Color.black()
		}
	}
	return(textColor)
}

func updateWidgetAppearance private
{
	var textColor #used = getActualWidgetTextColor()
	IFDEF("target_android") {
		lang "java" {{{
			if(widgetBackgroundColor != null) {
				setBackgroundColor(widgetBackgroundColor.toARGBInt32());
			}
			else {
				setBackgroundColor(0);
			}
			setPadding(widgetPadding, widgetPadding, widgetPadding, widgetPadding);
		}}}
	}
	ELSE IFDEF("target_iosoc") {
		var bgc #used = widgetBackgroundColor
		lang "objc" {{{
			if(bgc != nil) {
				self.backgroundColor = [bgc toUIColor];
			}
			else {
				self.backgroundColor = nil;
			}
			self.textColor = [textColor toUIColor];
			// Padding is used automatically: See above
		}}}
	}
	ELSE IFDEF("target_html") {
		if(widgetBackgroundColor != null || widgetTextColor != null || widgetPadding > 0) {
			if(widgetBackgroundColor == null) {
				HTMLDOM.setStyle(element, "background-color", "rgba(0,0,0,0)")
			}
			else {
				HTMLDOM.setStyle(element, "background-color", HTMLDOM.colorToRGBA(widgetBackgroundColor))
			}
			HTMLDOM.setStyle(element, "color", HTMLDOM.colorToRGBA(textColor))
			HTMLDOM.setStyle(element, "padding", String.forInteger(widgetPadding) .. "px ")
			HTMLDOM.setStyle(element, "appearance", "none")
			HTMLDOM.setStyle(element, "-webkit-appearance", "none")
			HTMLDOM.setStyle(element, "-moz-appearance", "none")
			HTMLDOM.setStyle(element, "border", "0px")
			HTMLDOM.setStyle(element, "border-radius", "0px")
		}
		else {
			HTMLDOM.removeStyle(element, "appearance")
			HTMLDOM.removeStyle(element, "-webkit-appearance")
			HTMLDOM.removeStyle(element, "-moz-appearance")
			HTMLDOM.removeStyle(element, "border")
			HTMLDOM.removeStyle(element, "border-radius")
			HTMLDOM.removeStyle(element, "color")
			HTMLDOM.removeStyle(element, "padding")
		}
	}
	ELSE {
		ERROR("Not implemented")
	}
	Widget.onChanged(this)
}

IFDEF("target_iosoc")
{
	func onPickerDone
	{
		var index #used = getSelectedWidgetIndex()
		var text #used = lang "objc" string {{{ [self getWidgetTextForIndex:index] }}}
		lang "objc" {{{
			[self setText:text];
			[self resignFirstResponder];
		}}}
	}
}

func setWidgetItemsAsKeyValueList(items as KeyValueList<string, string>)
{
	var selectedIndex = getSelectedWidgetIndex()
	widgetItems = items
	IFDEF("target_android") {
		var list = new vector<string>
		if(widgetItems != null) {
			var it as Iterator<KeyValuePair<string, string>> #force = widgetItems.iterate()
			while(it != null) {
				var item = it.next()
				if(item == null) {
					break
				}
				list += item.value
			}
		}
		adapter = new MyAdapter((widgetContext as! cave.GuiApplicationContextForAndroid).getAndroidActivityContext(), lang "java" int {{{ android.R.layout.simple_spinner_item }}}, list)
		adapter.setDropDownViewResource(lang "java" int {{{ android.R.layout.simple_spinner_dropdown_item }}})
		ignoreInitialSelectEvent = true
		base.setAdapter(adapter)
		updateWidgetFont()
	}
	ELSE IFDEF("target_html") {
		HTMLDOM.setInnerHTML(element, "")
		if(widgetItems != null) {
			var it as Iterator<KeyValuePair<string, string>> #force = widgetItems.iterate()
			while(it != null) {
				var item = it.next()
				if(item == null) {
					break
				}
				var option = HTMLDOM.createElement("option")
				HTMLDOM.setInnerHTML(option, item.value)
				HTMLDOM.setAttribute(option, "value", item.key)
				HTMLDOM.appendChild(element, option)
			}
		}
	}
	ELSE IFDEF("target_iosoc") {
		lang "objc" {{{
			[pickerView reloadAllComponents];
		}}}
	}
	ELSE {
		ERROR("Not implemented.")
	}
	setSelectedWidgetIndex(selectedIndex)
}

func setWidgetItemsAsKeyValueStrings(options as vector<string>)
{
	var list = new KeyValueList<string,string>()
	if(options != null) {
		foreach(option in options) {
			var comps = String.split(option, ':', 2)
			var kk = Vector.get(comps, 0)
			var vv = Vector.get(comps, 1)
			if(vv == null) {
				vv = kk
			}
			list.add(kk, vv)
		}
	}
	setWidgetItemsAsKeyValueList(list)
}

func setWidgetItemsAsKeyValueStrings(options as array)
{
	var list = new KeyValueList<string,string>()
	if(options != null) {
		foreach(option as string in options) {
			var comps = String.split(option, ':', 2)
			var kk = Vector.get(comps, 0)
			var vv = Vector.get(comps, 1)
			if(vv == null) {
				vv = kk
			}
			list.add(kk, vv)
		}
	}
	setWidgetItemsAsKeyValueList(list)
}

func setWidgetItemsAsStringList(options as DynamicVector)
{
	var list = new KeyValueList<string,string>()
	if options {
		foreach option as string in options.toVector() {
			list.add(option, option)
		}
	}
	setWidgetItemsAsKeyValueList(list)
}

func setWidgetItemsAsStringList(options as vector<string>)
{
	var list = new KeyValueList<string,string>()
	if(options != null) {
		foreach(option in options) {
			list.add(option, option)
		}
	}
	setWidgetItemsAsKeyValueList(list)
}

func setWidgetItemsAsStringList(options as array)
{
	var list = new KeyValueList<string,string>()
	if(options != null) {
		foreach(option as string in options) {
			list.add(option, option)
		}
	}
	setWidgetItemsAsKeyValueList(list)
}

func adjustSelectedWidgetItemIndex(index as int) as int
{
	if(widgetItems == null || widgetItems.count() < 1) {
		return(-1)
	}
	if(index < 0) {
		return(0)
	}
	if(index >= widgetItems.count()) {
		return(widgetItems.count() - 1)
	}
	return(index)
}

func findIndexForWidgetValue(id as string) private as int
{
	var v = -1
	if(widgetItems != null) {
		var n = 0
		var it as Iterator<KeyValuePair<string, string>> #force = widgetItems.iterate()
		while(it != null) {
			var item = it.next()
			if(item == null) {
				break
			}
			if(item.key == id) {
				v = n
				break
			}
			n ++
		}
	}
	return(v)
}

func getWidgetItemCount private as int
{
	if(widgetItems == null) {
		return(0)
	}
	return(widgetItems.count())
}

func getWidgetTextForIndex(index as int) private as string
{
	if(widgetItems == null) {
		return(null)
	}
	return(widgetItems.getValue(index))
}

func getSelectedWidgetIndex as int
{
	IFDEF("target_iosoc") {
		return(lang "objc" int {{{ [pickerView selectedRowInComponent:0] }}})
	}
	ELSE IFDEF("target_android") {
		return(selectedWidgetIndex)
	}
	ELSE IFDEF("target_html") {
		return(lang "js" int {{{ this.element.selectedIndex }}})
	}
	ELSE {
		ERROR("Not implemented")
		return(-1)
	}
}

func setSelectedWidgetIndex(index as int)
{
	var v #used = adjustSelectedWidgetItemIndex(index)
	IFDEF("target_iosoc") {
		var txt = getWidgetTextForIndex(v)
		if(txt == null) {
			txt = ""
		}
		lang "objc" {{{
			[self setText:txt];
		}}}
		if(index >= 0 && index < getWidgetItemCount()) {
			lang "objc" {{{
				[pickerView selectRow:index inComponent:0 animated:NO];
			}}}
		}
	}
	ELSE IFDEF("target_android") {
		selectedWidgetIndex = v
		base.setSelection(v)
	}
	ELSE IFDEF("target_html") {
		lang "js" {{{
			this.element.selectedIndex = v;
		}}}
	}
	ELSE {
		ERROR("Not implemented")
	}
}

func setSelectedWidgetValue(id as string)
{
	setSelectedWidgetIndex(findIndexForWidgetValue(id))
}

func getSelectedWidgetValue as string
{
	var index = getSelectedWidgetIndex()
	if(widgetItems == null || index < 0) {
		return(null)
	}
	return(widgetItems.getKey(index))
}

func setWidgetValue(value as object)
{
	setSelectedWidgetValue(cape.String.asString(value))
}

func getWidgetValue as object
{
	return(getSelectedWidgetValue())
}

func setWidgetValueChangeHandler(handler as function)
{
	widgetValueChangeHandler = handler
}

func onWidgetSelectionChanged
{
	if(widgetValueChangeHandler != null) {
		widgetValueChangeHandler()
	}
}
