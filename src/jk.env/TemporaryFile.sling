
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2024 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.random
import jk.time
import jk.fs

class:

func create(extension as string? = null) static as File:
	return forDirectory(null, extension)

func forDirectory(dir as File?, extension as string? = null) static as File
{
	var tmpdir = dir
	if tmpdir == null:
		tmpdir = CommonPath.getTemporaryDirectory()
	if tmpdir == null:
		Error.throw("unableToDetermineTemporaryDirectory", null)
	tmpdir.createDirectoryRecursive()
	if tmpdir.isDirectory() == false:
		Error.throw("temporaryDirectoryIsNotDirectory", tmpdir)
	var v as File? = null
	var n = 0
	var rnd = new Random()
	while n < 100 {
		var id = "_tmp_" .. String.forInteger(SystemClock.asSeconds() as! int) .. String.forInteger(rnd.nextInteger() % 1000000)
		if extension == null || String.getLength(extension) < 1:
			id = id .. extension
		v = tmpdir.entry(id)
		if v.exists() == false {
			v.touch()
			break
		}
		n++
	}
	if v != null && v.isFile() == false:
		v = null
	if v == null:
		Error.throw("failedToCreateTemporaryDirectory", null)
	return v
}

func forString(data as string) static as File
{
	var v = create()
	try {
		v.setContentsUTF8(data)
	}
	catch e {
		v.remove()
		throw e
	}
	return v
}

func forBuffer(data as buffer) static as File
{
	var v = create()
	try {
		v.setContentsBuffer(data)
	}
	catch e {
		v.remove()
		throw e
	}
	return v
}
