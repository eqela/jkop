
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2018 Job and Esther Technologies Oy
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class abstract imports cape:

func encryptString(data as string, cipher as this) static as buffer
{
	if(data == null) {
		return(null)
	}
	return(encryptBuffer(String.toUTF8Buffer(data), cipher))
}

func decryptString(data as buffer, cipher as this) static as string
{
	var db = decryptBuffer(data, cipher)
	if(db == null) {
		return(null)
	}
	return(String.forUTF8Buffer(db))
}

func encryptBuffer(data as buffer, cipher as this) static as buffer
{
	if(cipher == null || data == null) {
		return(null)
	}
	var bw = new BufferWriter()
	if(bw == null) {
		return(null)
	}
	var ww = BlockCipherWriter.create(bw, cipher)
	if(ww == null) {
		return(null)
	}
	var r = ww.write(data)
	ww.close()
	if(r < Buffer.getSize(data)) {
		return(null)
	}
	return(bw.getBuffer())
}

func decryptBuffer(data as buffer, cipher as this) static as buffer
{
	if(cipher == null || data == null) {
		return(null)
	}
	var br = BufferReader.forBuffer(data)
	if(br == null) {
		return(null)
	}
	var rr = BlockCipherReader.create(br, cipher)
	if(rr == null) {
		return(null)
	}
	var db = Buffer.allocate(Buffer.getSize(data))
	if(db == null) {
		return(null)
	}
	var ll = rr.read(db)
	if(ll < 0) {
		return(null)
	}
	if(ll < Buffer.getSize(db)) {
		Buffer.allocate(ll)
	}
	return(db)
}

func getBlockSize abstract as int
func encryptBlock(src as buffer, dest as buffer) abstract
func decryptBlock(src as buffer, dest as buffer) abstract
