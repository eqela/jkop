
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2024 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class is CharacterDecoder is Duplicateable:

func forBuffer(buffer as buffer) static as this
{
	var v = new this()
	v.setBuffer(buffer)
	return v
}

var buffer as buffer?
var bufferSize as long
var currentPosition = -1

func setBuffer(buffer as buffer)
{
	resetDecoder()
	this.buffer = buffer
	this.bufferSize = Buffer.getSize(buffer)
	this.currentPosition = -1
}

func moveToPreviousByte override as bool
{
	if currentPosition < 1:
		return false
	currentPosition--
	return true
}

func moveToNextByte override as bool
{
	var n = currentPosition + 1
	if n >= bufferSize:
		return false
	currentPosition = n
	return true
}

func getCurrentByte override as int
{
	assert buffer
	return (buffer[currentPosition] & 0xFF) as! int
}

func getCurrentPosition override as int:
	return currentPosition

func setCurrentPosition(position as int) override
{
	if position < 0 {
		current = 0 as! char
		currentPosition = -1
	}
	else {
		currentPosition = position + 1
		doMoveToPreviousChar()
	}
}

func duplicate implement as object
{
	var v = new this()
	base.copyTo(v)
	v.buffer = buffer
	v.bufferSize = bufferSize
	v.currentPosition = currentPosition
	return v
}
