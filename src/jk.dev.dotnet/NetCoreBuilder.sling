
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2023 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.fs
import jk.log
import jk.env
import jk.process

class:

prop dotnetCommand as File
prop updateDependencies as bool = true
prop buildRelease as bool = true
prop targetPlatform as string

func getDotnetCommandFile as File
{
	var v = dotnetCommand
	if v:
		return v
	v = ExternalCommand.findInPath("dotnet")
	if v:
		return v
	return null
}

func getBuilderId as string:
	return "netcore"

func canBuild(dir as File) as bool
{
	assert dir
	if CSProjUtil.findNetCoreProjectFile(dir):
		return true
	return false
}

func execute(ctx as LoggingContext, sourceDirectory as File, destinationDirectory as File) as bool
{
	var dir = assert sourceDirectory:
		Log.error(ctx, "No source directory")
	assert dir.isDirectory():
		Log.error(ctx, "Source directory does not exist: `" .. String.asString(dir) .. "'")
	Log.debug(ctx, "Using source directory: `" .. String.asString(dir) .. "'")
	var dotnet = assert getDotnetCommandFile()
	if updateDependencies {
		// Dotnet/Nuget continues to be a little bit braindead with regards to
		// finding packages. It CANNOT prioritize local packages over cached versions.
		// So we force it...
		Log.status(ctx, "[dotnet] Clearing cached copies of local packages ..")
		Nupkg.clearLocalPackagesFromNugetCache(ctx, dotnet, dir)
		Log.status(ctx, "[dotnet] Updating package dependencies ..")
		var pl = ProcessLauncher.forFile(dotnet)
		pl.addToParams("restore")
		pl.setCwd(dir)
		var sb = new StringBuilder()
		assert pl.executeToStringBuilder(sb) == 0 {
			Log.error(ctx, "Failed to restore dotnet packages in: `" .. String.asString(dir) .. "'")
			if ctx.isInDebugMode():
				Log.debug(ctx, sb.toString())
		}
		Log.debug(ctx, "`dotnet restore' successfully ended.")
	}
	var rplat as string
	var isAppProject = CSProjUtil.isAppProject(dir)
	if isAppProject {
		Log.status(ctx, "[dotnet] Compiling ..")
		var pl = ProcessLauncher.forFile(dotnet)
		pl.addToParams("publish")
		if buildRelease {
			pl.addToParams("-c")
			pl.addToParams("Release")
			rplat = targetPlatform
			if rplat {
				pl.addToParams("-r")
				pl.addToParams(rplat)
			}
		}
		pl.setCwd(dir)
		var sb = new StringBuilder()
		assert pl.executeToStringBuilder(sb) == 0 {
			Log.error(ctx, "Failed to build dotnet application in: `" .. String.asString(dir) .. "'")
			if ctx.isInDebugMode():
				Log.debug(ctx, sb.toString())
		}
		Log.debug(ctx, "`dotnet build' successful")
	}
	else {
		Log.status(ctx, "[dotnet] Compiling / packaging ..")
		var pl = ProcessLauncher.forFile(dotnet)
		pl.addToParams("pack")
		if buildRelease {
			pl.addToParams("-c")
			pl.addToParams("Release")
		}
		pl.setCwd(dir)
		var sb = new StringBuilder()
		assert pl.executeToStringBuilder(sb) == 0 {
			Log.error(ctx, "Failed to build and pack dotnet library in: `" .. String.asString(dir) .. "'")
			if ctx.isInDebugMode():
				Log.debug(ctx, sb.toString())
		}
		Log.debug(ctx, "`dotnet pack' successful")
	}
	var destdir = destinationDirectory
	if destdir {
		destdir.createDirectoryRecursive()
		var builddir = dir.entry("build")
		if not builddir.isDirectory():
			builddir = dir.entry("bin").entry("Debug")
		if not builddir.isDirectory():
			builddir = dir.entry("bin").entry("Release")
		var ee = builddir.entries()
		if isAppProject {
			while ee {
				var file = ee.next()
				if not file {
					builddir = null
					break
				}
				if file.isDirectory() {
					builddir = file
					break
				}
			}
			assert builddir:
				Log.error(ctx, "Failed to find build directory in `" .. String.asString(destdir) .. "'")
			if rplat:
				builddir = builddir.entry(rplat)
			builddir = builddir.entry("publish")
			ee = builddir.entries()
		}
		var runtimes as File
		while ee {
			var file = ee.next()
			if not file:
				break
			var bn = file.getBasename()
			if String.equals(bn, "runtimes") && runtimes == null:
				runtimes = file
			var dest = destdir.entry(bn)
			Log.debug(ctx, "Copying `" .. String.asString(file) .. "' -> `" .. String.asString(dest) .. "' ..")
			file.copyFileOrDirectoryTo(dest)
		}
		if runtimes:
			copyFile(ctx, runtimes, destdir)
		var see = dir.entries()
		while see {
			var file = see.next()
			if not file:
				break
			var bn = file.getBasename()
			if String.startsWith(bn, "apidoc") && String.endsWith(bn, ".json") {
				var dest = destdir.entry(bn)
				Log.debug(ctx, "Copying `" .. String.asString(file) .. "' -> `" .. String.asString(dest) .. "' ..")
				file.copyFileTo(dest)
			}
		}
	}
	return true
}

func copyFile(ctx as LoggingContext, file as File, dest as File)
{
	assert file
	assert dest
	if file.isFile() {
		var bn = file.getBasename()
		var dt = dest.entry(bn)
		if dt.isFile():
			return
		Log.debug(ctx, "Copying `" .. String.asString(file) .. "' -> `" .. String.asString(dt) .. "' ..")
		file.copyFileTo(dt)
		return
	}
	if file.isDirectory() {
		var ee = file.entries()
		while ee {
			var ef = ee.next()
			if not ef:
				break
			copyFile(ctx, ef, dest)
		}
	}
}
