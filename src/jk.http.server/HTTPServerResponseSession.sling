
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2018 Job and Esther Technologies Oy
 * Copyright (c) 2018-2020 Eqela Oy
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.socket
import jk.time.format

class:

pvar socket as ConnectedSocket
pvar server as HTTPServerBase
pvar request as HTTPServerRequest
pvar response as HTTPServerResponse
pvar totalBytes = 0

func createHeaderBuffer private as buffer
{
	var ver as string
	var met as string
	if request {
		ver = request.getVersion()
		met = request.getMethod()
	}
	if ver == "HTTP/0.9":
		return null
	var resp = response
	if resp == null:
		resp = HTTPServerResponse.forTextString("")
	var status = resp.getStatus()
	var headers = resp.getHeaders()
	if status == null || String.getLength(status) < 1 {
		status = "200"
		resp.setStatus(status)
	}
	var fs = HTTPServerResponse.getFullStatus(status)
	var reply = new StringBuilder()
	if ver == null || String.getLength(ver) < 1 {
		reply.appendString("HTTP/1.1")
	}
	else {
		reply.appendString(ver)
	}
	reply.appendCharacter(' ')
	reply.appendString(fs)
	reply.appendCharacter('\r')
	reply.appendCharacter('\n')
	if headers {
		var it = headers.iterate()
		while it {
			var kvp = it.next()
			if kvp == null:
				break
			reply.appendString(kvp.key)
			reply.appendCharacter(':')
			reply.appendCharacter(' ')
			reply.appendString(kvp.value)
			reply.appendCharacter('\r')
			reply.appendCharacter('\n')
		}
	}
	reply.appendString("Connection: close\r\n")
	reply.appendString("Cache-Control: no-store\r\n")
	if server {
		reply.appendString("Server: ")
		reply.appendString(server.getServerName())
	}
	reply.appendCharacter('\r')
	reply.appendCharacter('\n')
	reply.appendString("Date: ")
	reply.appendString(VerboseDateTimeString.forUTCNow())
	reply.appendCharacter('\r')
	reply.appendCharacter('\n')
	reply.appendCharacter('\r')
	reply.appendCharacter('\n')
	return String.toUTF8Buffer(reply.toString())
}

func writeHeader as bool
{
	var bb = createHeaderBuffer()
	if not bb:
		return true
	return writeData(bb)
}

func writeData(data as buffer, size as int = -1) as bool
{
	assert socket
	if not data:
		return true
	var sz = size
	if sz < 0:
		sz = sizeof data
	var r = socket.write(data, sz)
	if r > 0:
		totalBytes += r
	if r < sz:
		return false
	return true
}

func end
{
	assert socket
	socket.close()
	socket = null
}
