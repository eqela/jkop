
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2018 Job and Esther Technologies Oy
 * Copyright (c) 2018-2020 Eqela Oy
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.sql
import jk.log

class is SQLDatabase abstract:

const SSL_MODE_PREFERRED = "Preferred"
const SSL_MODE_NONE = "None"
const SSL_MODE_REQUIRED = "Required"
const SSL_MODE_VERIFYCA = "VerifyCA"
const SSL_MODE_VERIFYFULL = "VerifyFull"

const PROTOCOL_SOCKET = "socket"
const PROTOCOL_UNIX = "unix"
const PROTOCOL_PIPE = "pipe"
const PROCOL_MEMORY = "memory"

pvar databaseName as string

func instance private static as this
{
	IFDEF "target_netx" {
		return new MySQLDatabaseForDotNet()
	}
	ELSE {
		return new MySQLDatabaseGeneric()
	}
}

func forConnectionStringSync(ctx as LoggingContext, connectionString as string) static as this
{
	assert String.isNotEmpty(connectionString):
		Log.error(ctx, "Empty connection string given to MySQLDatabase")
	var ss = String.split(connectionString, ':')
	var paramc = Vector.getSize(ss)
	assert ss && paramc >= 4:
		Log.error(ctx, "MySQLDatabase connection string has less than 4 fields: `" .. connectionString .. "'")
	var address = ss[0]
	var username = ss[1]
	var password = ss[2]
	var database = ss[3]
	var sslMode = SSL_MODE_NONE
	var enableUTF8 = false
	for(var n=4; n<paramc; n++) {
		var param = ss[n]
		if String.startsWith(param, "sslmode=") {
			sslMode = String.getEndOfString(param, 8)
		}
		else if param == "ssl" {
			sslMode = SSL_MODE_REQUIRED
		}
		else if String.startsWith(param, "enableutf8=") {
			enableUTF8 = Boolean.asBoolean(String.getEndOfString(param, 11))
		}
		else if param == "utf8" {
			enableUTF8 = true
		}
	}
	return connectSync(ctx, address, username, password, database, sslMode, enableUTF8)
}

func connectSync(ctx as LoggingContext, serverAddress as string, username as string, password as string, database as string, sslMode as string, enableUTF8 as bool) static as this
{
	assert String.isNotEmpty(serverAddress) && String.isNotEmpty(username) && String.isNotEmpty(password) && String.isNotEmpty(database) && String.isNotEmpty(sslMode)
	var v = assert instance()
	v.setLogger(ctx)
	assert v.initializeSync(serverAddress, username, password, database, sslMode, PROTOCOL_SOCKET, enableUTF8)
	return v
}

func forConfig(logContext as LoggingContext, config as DynamicMap) static as this
{
	assert config
	var serverAddress = assert config.getString("serverAddress")
	var username = assert config.getString("username")
	var password = assert config.getString("password")
	var database = assert config.getString("database")
	var enableUTF8 = config.getBoolean("enableUTF8", false)
	var sslMode = config.getString("sslMode")
	if String.isEmpty(sslMode):
		sslMode = SSL_MODE_NONE
	var protocol = config.getString("protocol")
	if String.isEmpty(protocol):
		protocol = PROTOCOL_SOCKET
	var v = assert instance()
	v.setLogger(logContext)
	assert v.initializeSync(serverAddress, username, password, database, sslMode, protocol, enableUTF8)
	return v
}

func getDatabaseTypeId override as string:
	return "mysql"

func initializeSync(serverAddress as string, username as string, password as string, database as string, sslMode as string, protocol as string, enableUTF8 as bool) virtual as bool
{
	logDebug("MySQLDatabase: Connecting to database host=`" .. serverAddress .. "', username=`" .. username .. "', database=`" .. database .. "', sslMode=`" .. sslMode .. "', protocol=`" .. protocol .. "'")
	assert doInitialize(serverAddress, username, password, database, sslMode, protocol, enableUTF8)
	setDatabaseName(database)
	return true
}

func doInitialize(serverAddress as string, username as string, password as string, database as string, sslMode as string, protocol as string, enableUTF8 as bool) virtual as bool:
	return true

func querySingleRowSync(stmt as SQLStatement) override as DynamicMap
{
	var it = assert querySync(stmt)
	var v as DynamicMap #force = it.next()
	it.close()
	return v
}

func tableExistsSync(table as string) override as bool
{
	assert String.isNotEmpty(table) && String.isNotEmpty(databaseName)
	var stmt = assert prepareSync("SELECT TABLE_NAME FROM information_schema.tables WHERE table_schema = ? AND table_name = ? LIMIT 1;")
	stmt.addParamString(databaseName)
	stmt.addParamString(table)
	var sr = assert querySingleRowSync(stmt)
	return String.equalsIgnoreCase(table, sr.getString("TABLE_NAME"))
}

func tableExists(table as string, callback as function<void, bool>) override
{
	assert String.isNotEmpty(table) && String.isNotEmpty(databaseName):
		callback(false)
	var stmt = assert prepareSync("SELECT TABLE_NAME FROM information_schema.tables WHERE table_schema = ? AND table_name = ? LIMIT 1;"):
		callback(false)
	stmt.addParamString(databaseName)
	stmt.addParamString(table)
	querySingleRow(stmt, func(sr as DynamicMap) {
		assert sr:
			callback(false)
		callback(String.equalsIgnoreCase(table, sr.getString("TABLE_NAME")))
	})
}

func queryAllTableNames(callback as function<void, vector>) override
{
	var stmt = assert prepareSync("SELECT TABLE_NAME FROM information_schema.tables WHERE table_schema = ?;"):
		callback(null)
	stmt.addParamString(databaseName)
	query(stmt, func(it as SQLResultSetIterator) {
		assert it {
			callback(null)
		}
		var v = new vector
		loop {
			var vv = it.next()
			if not vv:
				break
			v += vv.getString("TABLE_NAME")
		}
		callback(v)
	})
}

func queryAllTableNamesSync override as vector
{
	assert String.isNotEmpty(databaseName)
	var stmt = assert prepareSync("SELECT TABLE_NAME FROM information_schema.tables WHERE table_schema = ?;")
	stmt.addParamString(databaseName)
	var it = assert querySync(stmt)
	var v = new vector
	loop {
		var vv = it.next()
		if not vv:
			break
		v += vv.getString("TABLE_NAME")
	}
	return v
}

func columnToCreateString(cc as SQLTableColumnInfo) private as string
{
	var sb = new StringBuilder()
	var columnName = cc.getName()
	sb.appendString(columnName)
	sb.appendCharacter(' ')
	var tt = cc.getType()
	sb.appendString(prepareTypeAsString(tt, columnName))
	return sb.toString()
}

func prepareTypeAsString(tt as int, columnName as string, forDataTypeChecking as bool = false) as string
{
	if tt == SQLTableColumnInfo.TYPE_INTEGER_KEY {
		if forDataTypeChecking:
			return "INTEGER"
		return "INTEGER AUTO_INCREMENT, PRIMARY KEY (" .. columnName .. ")"
	}
	else if tt == SQLTableColumnInfo.TYPE_INTEGER {
		return "INTEGER"
	}
	else if tt == SQLTableColumnInfo.TYPE_LONG_KEY {
		if forDataTypeChecking:
			return "BIGINT"
		return "BIGINT AUTO_INCREMENT, PRIMARY KEY (" .. columnName .. ")"
	}
	else if tt == SQLTableColumnInfo.TYPE_LONG {
		return "BIGINT"
	}
	else if tt == SQLTableColumnInfo.TYPE_STRING {
		return "VARCHAR(255)"
	}
	else if tt == SQLTableColumnInfo.TYPE_STRING_KEY {
		if forDataTypeChecking:
			return "VARCHAR(255)"
		return "VARCHAR(255), PRIMARY KEY (" .. columnName .. ")"
	}
	else if tt == SQLTableColumnInfo.TYPE_TEXT {
		return "LONGTEXT"
	}
	else if tt == SQLTableColumnInfo.TYPE_BLOB {
		return "LONGBLOB"
	}
	else if tt == SQLTableColumnInfo.TYPE_DOUBLE {
		return "REAL"
	}
	Log.error(getLogger(), "Unknown column type: " .. String.forInteger(tt))
	return "UNKNOWN"
}

func prepareCreateTableStatementSync(table as string, columns as vector<SQLTableColumnInfo>) override as SQLStatement
{
	assert String.isNotEmpty(table) && columns
	var sb = new StringBuilder()
	sb.appendString("CREATE TABLE ")
	sb.appendString(table)
	sb.appendString(" (")
	var first = true
	foreach column as SQLTableColumnInfo in columns {
		if not first:
			sb.appendCharacter(',')
		sb.appendCharacter(' ')
		sb.appendString(columnToCreateString(column))
		first = false
	}
	sb.appendString(" );")
	return prepareSync(sb.toString())
}

func prepareDeleteTableStatementSync(table as string) override as SQLStatement
{
	assert String.isNotEmpty(table)
	var sb = new StringBuilder()
	sb.appendString("DROP TABLE ")
	sb.appendString(table)
	sb.appendString(";")
	return prepareSync(sb.toString())
}

func prepareCreateIndexStatementSync(table as string, column as string, unique as bool) override as SQLStatement
{
	assert String.isNotEmpty(table) && String.isNotEmpty(column)
	var unq = ""
	if unique:
		unq = "UNIQUE "
	var sql = "CREATE " .. unq .. "INDEX " .. table .. "_" .. column .. " ON " .. table .. " (" .. column .. ")"
	return prepareSync(sql)
}

func prepareCreateColumnStatementSync(table as string, column as SQLTableColumnInfo) override as SQLStatement
{
	assert String.isNotEmpty(table) && column
	var sql = "ALTER TABLE " .. table .. " ADD COLUMN " .. column.getName() .. " " .. prepareTypeAsString(column.getType(), column.getName())
	return prepareSync(sql)
}

func prepareUpdateColumnTypeStatementSync(table as string, column as SQLTableColumnInfo) override as SQLStatement
{
	assert String.isNotEmpty(table) && column
	var sql = "ALTER TABLE " .. table .. " CHANGE " .. column.getName() .. " " .. column.getName() .. " " .. prepareTypeAsString(column.getType(), column.getName(), true)
	return prepareSync(sql)
}

func getLastInsertIdSync(table as string) override as long
{
	// FIXME: Parameter table is ignored.
	var v = assert querySingleRowSync(prepareSync("SELECT LAST_INSERT_ID() AS id;"))
	return v.getLongInteger("id")
}

func getPrimaryKeyColumnNameSync(table as string) override as string
{
	var v = assert querySingleRowSync(prepareSync("SELECT COLUMN_NAME FROM information_schema.KEY_COLUMN_USAGE WHERE CONSTRAINT_NAME LIKE 'PRIMARY%' AND TABLE_NAME='" .. table .. "';"))
	return v.getString("COLUMN_NAME")
}

func getIdentityColumnNameSync(table as string) override as string
{
	var v = assert querySingleRowSync(prepareSync("SELECT COLUMN_NAME FROM information_schema.columns WHERE TABLE_NAME='" .. table .. "' AND IS_NULLABLE='NO' AND EXTRA LIKE '%auto_increment%';"))
	return v.getString("COLUMN_NAME")
}

func columnExistsSync(table as string, column as string) override as bool
{
	assert String.isNotEmpty(table) && String.isNotEmpty(databaseName) && String.isNotEmpty(column)
	var stmt = assert prepareSync("SELECT COLUMN_NAME FROM information_schema.columns WHERE table_schema = ? AND table_name = ? AND column_name = ? LIMIT 1;")
	stmt.addParamString(databaseName)
	stmt.addParamString(table)
	stmt.addParamString(column)
	var sr = assert querySingleRowSync(stmt)
	return String.equalsIgnoreCase(column, sr.getString("COLUMN_NAME"))
}

func isColumnTypeSameSync(table as string, column as SQLTableColumnInfo) override as bool
{
	assert String.isNotEmpty(table) && String.isNotEmpty(databaseName) && column
	var stmt = assert prepareSync("SELECT COLUMN_TYPE FROM information_schema.columns WHERE table_schema = ? AND table_name = ? AND column_name = ? LIMIT 1;")
	stmt.addParamString(databaseName)
	stmt.addParamString(table)
	stmt.addParamString(column.getName())
	var sr = assert querySingleRowSync(stmt)
	return String.equalsIgnoreCase(prepareTypeAsString(column.getType(), column.getName(), true), sr.getString("COLUMN_TYPE"))
}
