
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2018 Job and Esther Technologies Oy
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class:

class IconDirEntry
{
	var width public as uint8
	var height public as uint8
	var colors public as uint8
	var colorPlanes public as uint16
	var bitsPerPixel public as uint16
	var size public as int
	var offset public as int
}

func read(file as File) static as vector<RGBAImageBuffer>
{
	assert file
	assert file.hasExtension("ico")
	return read(file.getContentsBuffer())
}

func read(buffer as buffer) static as vector<RGBAImageBuffer>
{
	var data = buffer
	var size = SIZE data
	assert size > 6

	// Reading icon header

	var res1 = (data[1] << 8) | data[0] as uint16
	assert res1 == 0
	var type = (data[3] << 8) | data[2] as uint16
	assert type == 1 || type == 2
	var inum = (data[5] << 8) | data[4] as uint16
	assert size > (6 + (inum * 16))
	var n = 0
	var c = 6
	var entries as vector<RGBAImageBuffer>
	loop {
		if n >= inum:
			break

		// Reading icon dir entry

		var iw = data[c+0] as uint8
		var ih = data[c+1] as uint8
		var cc = data[c+2] as uint8
		var rs = data[c+3] as uint8
		var cp = (data[c+5] << 8) | data[c+4] as uint16
		var bp = (data[c+7] << 8) | data[c+6] as uint16
		var sz = (data[c+11] << 24) | (data[c+10] << 16) | (data[c+9] << 8) | data[c+8] as int
		var of = (data[c+15] << 24) | (data[c+14] << 16) | (data[c+13] << 8) | data[c+12] as int
		c += 16
		n ++
		if rs != 0:
			continue
		var ee = new IconDirEntry()
		ee.width = iw
		ee.height = ih
		ee.colors = cc
		ee.colorPlanes = cp
		ee.bitsPerPixel = bp
		ee.size = sz
		ee.offset = of
		var buf = processEntry(ee, data)
		if buf {
			if not entries:
				entries = new vector<RGBAImageBuffer>
			var ib = new RGBAImageBuffer()
			ib.setBuffer(buf)
			ib.setWidth(iw)
			ib.setHeight(ih)
			entries += ib
		}
	}
	return entries
}

func processEntry(entry as IconDirEntry, buffer as buffer) private static as buffer
{
	var size = entry.size
	var data = MALLOC size
	Buffer.copyFrom(data, buffer, entry.offset, 0, size)

	// Reading bitmap info header
	// FIXME: Unused headers may serve their purpose in the future. Marking it as used to prevent warnings

	assert size > 40
	var c = 0
	var hs #used = (data[c+3] << 24) | (data[c+2] << 16) | (data[c+1] << 8) | data[c+0] as int
	var hw #used = (data[c+7] << 24) | (data[c+6] << 16) | (data[c+5] << 8) | data[c+4] as int
	var hh #used = (data[c+11] << 24) | (data[c+10] << 16) | (data[c+9] << 8) | data[c+8] as int
	var hp #used = (data[c+13] << 8) | data[c+12] as uint16
	var hc #used = (data[c+19] << 24) | (data[c+18] << 16) | (data[c+17] << 8) | data[c+16] as int
	var hi #used = (data[c+23] << 24) | (data[c+22] << 16) | (data[c+21] << 8) | data[c+20] as int
	var hx #used = (data[c+27] << 24) | (data[c+26] << 16) | (data[c+25] << 8) | data[c+24] as int
	var hy #used = (data[c+31] << 24) | (data[c+30] << 16) | (data[c+29] << 8) | data[c+28] as int
	var hu #used = (data[c+35] << 24) | (data[c+34] << 16) | (data[c+33] << 8) | data[c+32] as int
	var hm #used = (data[c+39] << 24) | (data[c+38] << 16) | (data[c+37] << 8) | data[c+36] as int
	var hb = (data[c+15] << 8) | data[c+14] as uint16
	c += 40
	var srcs = size - c
	var isrc = MALLOC srcs
	Buffer.copyFrom(isrc, data, c, 0, srcs)
	var width = entry.width as int
	var height = entry.height as int
	var xorMask = getXorMask(isrc, width, height, hb)
	var andMask = getAndMask(isrc, width, height)
	var ides = MALLOC(width * height * 4)
	for(var y = 0; y < height; y++) {
		for(var x = 0; x < width; x++) {
			var z = (x + y * width) * 4
			var w = (x + (height - y - 1) * width) * 4
			Buffer.setByte(ides, w+0, Buffer.getByte(xorMask, z+0))
			Buffer.setByte(ides, w+1, Buffer.getByte(xorMask, z+1))
			Buffer.setByte(ides, w+2, Buffer.getByte(xorMask, z+2))
			if andMask {
				var a = x + y * width
				Buffer.setByte(ides, w+3, Buffer.getByte(andMask, a))
			}
		}
	}
	return ides
}

func getAndMask(data as buffer, width as int, height as int) private static as buffer
{
	var sz = SIZE data
	var iw = width
	var ih = height
	var pd = getPadding(iw/8)
	var of = sz - (iw/8 + pd) * ih
	var bf = 0x80
	var bs = 0
	var cc = 0
	var am = MALLOC(iw * ih)
	for(var y = 0; y < ih; y++) {
		for(var x = 0; x < iw; x++) {
			if bs > 7 {
				bs = 0
				of ++
			}
			var cb = data[of] as uint8
			var mm = bf >> bs
			var vv = cb & mm as uint8
			var bm = (vv != 0x00) ? 0x00 : 0xff
			Buffer.setByte(am, cc, bm)
			bs ++
			cc ++
		}
		of += pd
	}
	return am
}

func getXorMask(data as buffer, width as int, height as int, colorDepth as int) private static as buffer
{
	assert data
	var cd = colorDepth
	var iw = width
	var ih = height
	var xm = MALLOC(iw * ih * 4)
	var pd = getPadding(iw)
	var of = 0
	if cd == 24 || cd == 32 {
		var io = 0
		for(var y = 0; y < ih; y++) {
			for(var x = 0; x < iw; x++) {
				Buffer.setByte(xm, of++, data[io++] as uint8)
				Buffer.setByte(xm, of++, data[io++] as uint8)
				Buffer.setByte(xm, of++, data[io++] as uint8)
				of ++
				io ++
			}
			io += pd
		}
	}
	else if cd == 8 {
		var io = Math.pow(2.0, cd*1.0) * 4 as int
		for(var y = 0; y < ih; y++) {
			for(var x = 0; x < iw; x++) {
				var iy = data[io] as uint8
				var ix = iy * 4 as int
				Buffer.setByte(xm, of++, data[ix+0] as uint8)
				Buffer.setByte(xm, of++, data[ix+1] as uint8)
				Buffer.setByte(xm, of++, data[ix+2] as uint8)
				of ++
				io ++
			}
			io += pd
		}
	}
	else {
		; // FIXME: Support images with color depth 1 and 4
	}
	return xm
}

func getPadding(width as int) private static as int:
	return Math.abs(4-width) % 4 as int
