
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2023 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class is DoubleObject:

func forValue(value as double) static as this:
	return new this(value)

func asDouble(obj as dynamic, defval as double = 0.0) static as double #deprecated:
	return asValue(obj, defval)

func asValue(obj as dynamic, defval as double) static as double
{
	var v = asObject(obj)
	if not v:
		return defval
	return v.toDouble()
}

func asObject(obj as dynamic) static as this
{
	if not obj:
		return null
	if obj is double:
		return new this(obj as double)
	if obj is this:
		return obj as! this
	if obj is DoubleObject {
		var od = obj as! DoubleObject
		return new this(od.toDouble())
	}
	if obj is int {
		var v = obj as int
		return new this(v as double)
	}
	if obj is IntegerObject {
		var oi = obj as! IntegerObject
		return new this(oi.toInteger() as double)
	}
	if obj is long {
		var v = obj as long
		return new this(v as double)
	}
	if obj is LongIntegerObject {
		var oi = obj as! LongIntegerObject
		return new this(oi.toLong() as double)
	}
	if obj is string:
		return new this(String.toDouble(obj as! string))
	if obj is StringObject:
		return new this(String.toDouble((obj as! StringObject).toString()))
	if obj is char {
		var v = obj as char
		return new this(v as double)
	}
	if obj is CharacterObject {
		var oc = obj as! CharacterObject
		return new this(oc.toCharacter() as double)
	}
	if obj is bool {
		if obj as bool == true:
			return new this(1.0)
		return new this(0.0)
	}
	if obj is BooleanObject {
		if (obj as! BooleanObject).toBoolean():
			return new this(1.0)
		return new this(0.0)
	}
	return null
}

func longBitsToDouble(vv as long) static as double
{
	IFDEF "target_java" {
		return lang "java" double {{{ Double.longBitsToDouble(vv) }}}
	}
	ELSE IFDEF "target_cs" {
		return lang "cs" double {{{ System.BitConverter.Int64BitsToDouble(vv) }}}
	}
	ELSE IFDEF "target_js" {
		lang "js" {{{
			const buffer = new ArrayBuffer(16);
			const view = new DataView(buffer);
			view.setBigInt64(0, BigInt(vv));
		}}}
		return lang "js" double {{{ view.getFloat64(0) }}}
	}
	ELSE {
		ERROR "Not implemented"
		return 0.0
	}
}

func doubleToLongBits(vv as double) static as long
{
	IFDEF "target_java" {
		return lang "java" long {{{ Double.doubleToLongBits(vv) }}}
	}
	ELSE IFDEF "target_cs" {
		return lang "cs" long {{{ System.BitConverter.ToInt64(System.BitConverter.GetBytes(vv), 0) }}}
	}
	ELSE IFDEF "target_js" {
		lang "js" {{{
			const buffer = new ArrayBuffer(16);
			const view = new DataView(buffer);
			view.setFloat64(0, vv);
		}}}
		return lang "js" long {{{ view.getBigInt64(0) }}}
	}
	ELSE {
		ERROR "Not implemented"
		return 0
	}
}

ctor:
	nop

ctor(value as double):
	setValue(value)

pvar value as double

func add(amount as double):
	value += amount

func toDouble as double:
	return value
