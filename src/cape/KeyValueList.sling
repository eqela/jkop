
/*
 * This file is part of Jkop
 * Copyright (c) 2016 Job and Esther Technologies, Inc.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class <K,V>:

var values as vector<KeyValuePair<K,V>>

func add(key as K, val as V)
{
	if(values == null) {
		values = new vector<KeyValuePair<K,V>>
	}
	var kvp = new KeyValuePair<K,V>()
	kvp.key = key
	kvp.value = val
	values += kvp
}

func iterate as Iterator<KeyValuePair<K,V>>
{
	var v as Iterator<KeyValuePair<K,V>> = Vector.iterate(values)
	return(v)
}

func asVector as vector<KeyValuePair<K,V>>
{
	return(values)
}

func dup as this
{
	var v = new this()
	var it as Iterator<KeyValuePair<K,V>> = iterate()
	while(true) {
		var kvp = it.next()
		if(kvp == null) {
			break
		}
		v.add(kvp.key, kvp.value)
	}
	return(v)
}

func getKey(index as int) as K
{
	if(values == null) {
		return(DEFAULT(K))
	}
	var kvp = Vector.get(values, index)
	if(kvp == null) {
		return(DEFAULT(K))
	}
	return(kvp.key)
}

func getValue(index as int) as V
{
	if(values == null) {
		return(DEFAULT(V))
	}
	var kvp = Vector.get(values, index)
	if(kvp == null) {
		return(DEFAULT(V))
	}
	return(kvp.value)
}

func count as int
{
	if(values == null) {
		return(0)
	}
	return(Vector.getSize(values))
}

func clear
{
	values = null
}
