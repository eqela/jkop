
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2017 Job and Esther Technologies, Inc.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class is CustomContainerWidget imports cave:

func findTopMostLayerWidget(widget as Widget) static as this
{
	var v as this
	var pp = widget
	while(pp != null) {
		if(pp is this) {
			v = pp as! this
		}
		pp = Widget.getParent(pp)
	}
	return(v)
}

func forMargin(context as GuiApplicationContext, margin as int) static as this
{
	var v = new this(context)
	v.margin = margin
	return(v)
}

func forWidget(context as GuiApplicationContext, widget as Widget, margin as int = 0) static as this
{
	var v = new this(context)
	v.margin = margin
	v.addWidget(widget)
	return(v)
}

func forWidgets(context as GuiApplicationContext, widgets as array<Widget>, margin as int = 0) static as this
{
	var v = new this(context)
	v.margin = margin
	foreach(widget in widgets) {
		v.addWidget(widget)
	}
	return(v)
}

var margin as int

ctor(ctx as GuiApplicationContext)
{
	base(ctx)
}

func onWidgetHeightChanged(height as int) override
{
	base.onWidgetHeightChanged(height)
	var children = Widget.getChildren(this)
	if(children != null) {
		var topmost = cape.Vector.get(children, cape.Vector.getSize(children) - 1)
		if(topmost != null) {
			Widget.resizeHeight(topmost, height-margin-margin)
		}
	}
}

func computeWidgetLayout(widthConstraint as int) override
{
	var children = Widget.getChildren(this)
	if(children == null) {
		return
	}
	var childCount = cape.Vector.getSize(children)
	var wc = -1
	if(widthConstraint >= 0) {
		wc = widthConstraint - margin - margin
		if(wc < 0) {
			wc = 0
		}
	}
	var mw as int
	var mh as int
	var n = 0
	foreach(child in Widget.getChildren(this)) {
		if(n == childCount-1) {
			Widget.layout(child, wc)
			mw = Widget.getWidth(child)
			mh = Widget.getHeight(child)
			Widget.move(child, margin, margin)
		}
		else {
			var ww = Widget.getWidth(child)
			Widget.move(child, -ww, margin)
		}
		n ++
	}
	Widget.setLayoutSize(this, mw+margin+margin, mh+margin+margin)
}

func addWidget(widget as Widget) as this
{
	Widget.addChild(this, widget)
	return(this)
}

func removeAllChildren
{
	Widget.removeChildrenOf(this)
}

func getChildWidget(index as int) as Widget
{
	var children = Widget.getChildren(this)
	if(children == null) {
		return(null)
	}
	return(cape.Vector.get(children, index))
}
