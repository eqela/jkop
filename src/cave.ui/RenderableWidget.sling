
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2018 Job and Esther Technologies Oy
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class is ParentAwareWidget is ResizeAwareWidget #widget:

basetype Widget type extends

func forRenderer(ctx as GuiApplicationContext, renderer as Renderer) static as this
{
	var v = new this(ctx)
	v.setRenderer(renderer)
	return v
}

IFDEF "target_android" {

	func onDraw(canvas as !"android.graphics.Canvas")
	{
		lang "java" {{{
			super.onDraw(canvas);
		}}}
		render(RenderingContextForAndroid.forCanvas(context, canvas))
	}
}

IFDEF "target_kotlin" {

	func onDraw(canvas as !"android.graphics.Canvas") override
	{
		lang "kotlin" {{{
			super.onDraw(canvas)
		}}}
		render(RenderingContextForKotlin.forCanvas(context, canvas))
	}
}

IFDEF "target_html"
{
	var renderContext as RenderingContext

	func createElement override as object:
		return HTMLDOM.createElement("canvas")
}

var context protected as GuiApplicationContext
prop renderer as Renderer

ctor(ctx as GuiApplicationContext)
{
	IFDEF "target_android" {
		base((ctx as! cave.GuiApplicationContextForAndroid).getActivityContext())
	}
	ELSE IFDEF "target_kotlin" {
		base((ctx as! cave.GuiApplicationContextForKotlin).getActivityContext())
	}
	context = ctx
	IFDEF "target_html" {
		renderContext = RenderingContextForHTMLCanvas.forElement(element)
	}
	IFDEF "target_swift" {
		renderContext = new RenderingContextForSwift()
	}
}

func render(ctx as RenderingContext) virtual
{
	if renderer:
		renderer.render(ctx, Widget.getWidth(this), Widget.getHeight(this))
}

func redraw
{
	IFDEF "target_html" {
		var thisWidget #used = this
		lang "js" {{{
			window.requestAnimationFrame(function() {
				thisWidget.render(thisWidget.renderContext);
			});
		}}}
	}
	ELSE IFDEF "target_android" {
		lang "java" {{{
			invalidate();
		}}}
	}
	ELSE IFDEF "target_kotlin" {
		lang "kotlin" {{{
			invalidate()
		}}}
	}
	ELSE IFDEF "target_swift" {
		lang "swift" {{{
			setNeedsDisplay()
		}}}
	}
	ELSE {
		ERROR "Not implemented"
	}
}

func onWidgetResized virtual
{
	IFDEF "target_html" {
		HTMLDOM.setAttribute(element, "width", String.forInteger(Widget.getWidth(this)) .. "px")
		HTMLDOM.setAttribute(element, "height", String.forInteger(Widget.getHeight(this)) .. "px")
		redraw()
	}
}

func onWidgetAddedToParent virtual
{
}

func onWidgetRemovedFromParent virtual
{
}

func onWidgetAddingToParent
{
}
