
/*
 * This file is part of Jkop
 * Copyright (c) 2016 Job and Esther Technologies, Inc.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class is StringObject:

IFDEF("target_objc") {
	var data as !"NSMutableString*"
}
ELSE IFDEF("target_cs") {
	var builder as !"System.Text.StringBuilder"
}
ELSE IFDEF("target_java") {
	var builder as !"java.lang.StringBuilder"
}
ELSE IFDEF("target_js") {
	var builder as !"String" #used
}

ctor
{
	initialize()
}

ctor(initial as string)
{
	initialize()
	append(initial)
}

ctor(initial as this)
{
	initialize()
	if(initial != null) {
		append(initial.toString())
	}
}

func initialize
{
	IFDEF("target_objc") {
		lang "objc" {{{
			data = [[NSMutableString alloc] init];
		}}}
	}
	ELSE IFDEF("target_cs") {
		lang "cs" {{{
			builder = new System.Text.StringBuilder();
		}}}
	}
	ELSE IFDEF("target_java") {
		lang "java" {{{
			builder = new java.lang.StringBuilder();
		}}}
	}
	ELSE IFDEF("target_js") {
		lang "js" {{{
			this.builder = new String();
		}}}
	}
	ELSE {
		ERROR("Not implemented.")
	}
}

func clear
{
	IFDEF("target_objc") {
		initialize()
	}
	ELSE IFDEF("target_cs") {
		lang "cs" {{{
			builder.Clear();
		}}}
	}
	ELSE IFDEF("target_java") {
		lang "java" {{{
			builder.setLength(0);
		}}}
	}
	ELSE IFDEF("target_js") {
		initialize()
	}
	ELSE {
		ERROR("Not implemented.")
	}
}

func count as int
{
	IFDEF("target_objc") {
		return(lang "objc" int {{{ [data length] }}})
	}
	ELSE IFDEF("target_cs") {
		return(lang "cs" int {{{ builder.Length }}})
	}
	ELSE IFDEF("target_java") {
		return(lang "java" int {{{ builder.length() }}})
	}
	ELSE IFDEF("target_js") {
		return(lang "js" int {{{ this.builder.length }}})
	}
	ELSE {
		ERROR("Not implemented.")
	}
}

func append(c as int) as this
{
	return(append(String.forInteger(c)))
}

func append(c as char) as this
{
	if(c == 0) {
		return(this)
	}
	return(append(String.forCharacter(c)))
}

func append(c as double) as this
{
	return(append(String.forDouble(c)))
}

func append(c as float) as this
{
	return(append(String.forFloat(c)))
}

func append(str as string #used) as this
{
	if(str == null) {
		return(this)
	}
	IFDEF("target_objc") {
		lang "objc" {{{
			[data appendString:str];
		}}}
		return(this)
	}
	ELSE IFDEF("target_cs") {
		lang "cs" {{{
			builder.Append(str);
		}}}
		return(this)
	}
	ELSE IFDEF("target_java") {
		lang "java" {{{
			builder.append(str);
		}}}
		return(this)
	}
	ELSE IFDEF("target_js") {
		lang "js" {{{
			this.builder += str;
		}}}
		return(this)
	}
	ELSE {
		ERROR("Not implemented.")
		return(this)
	}
}

func insert(index as int, c as int) as this
{
	return(insert(index, String.forInteger(c)))
}

func insert(index as int, c as char) as this
{
	if(c == 0) {
		return(this)
	}
	return(insert(index, String.forCharacter(c)))
}

func insert(index as int, c as double) as this
{
	return(insert(index, String.forDouble(c)))
}

func insert(index as int, c as float) as this
{
	return(insert(index, String.forFloat(c)))
}

func insert(index as int, str as string) as this
{
	if(str == null) {
		return(this)
	}
	IFDEF("target_objc") {
		lang "objc" {{{
			[data insertString:str atIndex:index];
		}}}
		return(this)
	}
	ELSE IFDEF("target_cs") {
		lang "cs" {{{
			builder.Insert(index, str);
		}}}
		return(this)
	}
	ELSE IFDEF("target_java") {
		lang "java" {{{
			builder.insert(index, str);
		}}}
		return(this)
	}
	ELSE IFDEF("target_js") {
		var fp #used
		var lp #used
		lang "js" {{{
			fp = this.builder.substring(0, index);
			lp = this.builder.substring(index, this.builder.length);
			this.builder = fp + str + lp;
		}}}
		return(this)
	}
	ELSE {
		ERROR("Not implemented.")
		return(this)
	}
}

func toString as string
{
	IFDEF("target_objc") {
		return(lang "objc" string {{{ [NSString stringWithString:data] }}})
	}
	ELSE IFDEF("target_cs") {
		return(lang "cs" string {{{ builder.ToString() }}})
	}
	ELSE IFDEF("target_java") {
		return(lang "java" string {{{ builder.toString() }}})
	}
	ELSE IFDEF("target_js") {
		return(lang "js" string {{{ this.builder.toString() }}})
	}
	ELSE {
		ERROR("Not implemented.")
		return(null)
	}
}
