
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2018 Job and Esther Technologies Oy
 * Copyright (c) 2018-2019 Eqela Oy
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.server
import jk.socket.io
import jk.time
import jk.log
import jk.thread

class is NetworkServer:

pvar writeBufferSize = 1024 * 512
pvar smallBodyLimit = 32 * 1024
pvar timeoutDelay = 30
pvar maintenanceTimerDelay = 60
pvar serverName as string
pvar allowCORS = true
pvar workerThreadPool as ThreadPool
var timeoutTimer as IOManagerTimer
var maintenanceTimer as IOManagerTimer

ctor
{
	setPort(8080)
	setServerName(VALUE "sling_project_title" .. "/" .. VALUE "sling_project_version")
}

macro UPDATE_SERVER_NAME(server, title, version)
{
	server.setServerName(title .. "/" .. version)
}

func createConnectionObject override as NetworkConnection:
	return new HTTPServerConnection()

func onRefresh virtual
{
}

func onTimeoutTimer as bool
{
	var cfc final = new vector<HTTPServerConnection>
	var now final = SystemClock.asSeconds()
	forEachConnection(func(connection as NetworkConnection) {
		var httpc = connection as HTTPServerConnection
		if httpc == null:
			return
		if (httpc.getResponses() >= httpc.getRequests() || httpc.getIsWaitingForBodyReceiver()) && now - httpc.getLastActivity() >= timeoutDelay:
			cfc += httpc
	})
	foreach wsc in cfc:
		wsc.close()
	return true
}

func onMaintenanceTimer as bool
{
	onMaintenance()
	return true
}

func onMaintenance virtual
{
}

func startTimer(delay as long, handler as function<bool>) as IOManagerTimer
{
	assert ioManager
	return ioManager.startTimer(delay, handler)
}

func initialize override as bool
{
	assert base.initialize()
	if timeoutDelay < 1 {
		Log.debug(logContext, VALUE "__THIS__" .. ": Timeout timer disabled")
	}
	else {
		Log.debug(logContext, VALUE "__THIS__" .. ": Starting a timeout timer with a " .. String.forInteger(timeoutDelay) .. " second delay.")
		timeoutTimer = ioManager.startTimer((timeoutDelay as long) * 1000000, onTimeoutTimer)
		if timeoutTimer == null:
			Log.error(logContext, VALUE "__THIS__" .. ": Failed to start timeout timer")
	}
	if maintenanceTimerDelay < 1 {
		Log.debug(logContext, "Maintenance timer disabled")
	}
	else {
		Log.debug(logContext, VALUE "__THIS__" .. ": Starting a maintenance timer with a " .. String.forInteger(maintenanceTimerDelay) .. " second delay.")
		maintenanceTimer = ioManager.startTimer((maintenanceTimerDelay as long) * 1000000, onMaintenanceTimer)
		if maintenanceTimer == null:
			Log.error(logContext, VALUE "__THIS__" .. ": Failed to start maintenance timer")
	}
	Log.info(logContext, VALUE "__THIS__" .. ": initialized: `" .. getServerName() .. "'")
	return true
}

func cleanup override
{
	base.cleanup()
	if maintenanceTimer {
		maintenanceTimer.stop()
		maintenanceTimer = null
	}
	if timeoutTimer {
		timeoutTimer.stop()
		timeoutTimer = null
	}
}

func createOptionsResponse(req as HTTPServerRequest) virtual as HTTPServerResponse:
	return new HTTPServerResponse().setStatus("200").addHeader("Content-Length", "0")

func onRequest(req as HTTPServerRequest) virtual:
	req.sendResponse(HTTPServerResponse.forHTTPNotFound())

func handleIncomingRequest(req as HTTPServerRequest)
{
	assert req
	if req.getMethod() == "OPTIONS" {
		var resp = createOptionsResponse(req)
		if resp {
			req.sendResponse(resp)
			return
		}
	}
	onRequest(req)
}

func sendResponse(connection as HTTPServerConnection, req as HTTPServerRequest, resp as HTTPServerResponse)
{
	assert connection
	if allowCORS:
		resp.enableCORS(req)
	connection.sendResponse(req, resp)
}

func startResponse(connection as HTTPServerConnection, req as HTTPServerRequest, resp as HTTPServerResponse) as HTTPServerResponseSession
{
	assert connection
	if allowCORS:
		resp.enableCORS(req)
	return connection.startResponse(req, resp)
}

class WorkerThread is Runnable
{
	pvar worker as HTTPServerResponseWorker

	func run
	{
		assert worker
		worker.onStart()
		loop {
			if worker.tick() == false:
				break
		}
		worker.onEnd()
	}
}

func startResponseWorker(worker as HTTPServerResponseWorker) as bool
{
	assert worker
	worker.setContext(logContext)
	if not workerThreadPool {
		Log.error(logContext, "Trying to start a response worker, but no thread pool has been initialized")
		worker.abort()
		return false
	}
	workerThreadPool.submitTask(new WorkerThread().setWorker(worker))
	Log.debug(logContext, VALUE "__THIS__" .. ": Starting a new worker thread. We now have " .. String.forInteger(workerThreadPool.getRunningThreadsCount()) .. " thread(s).")
	return true
}

func onRequestComplete(request as HTTPServerRequest, resp as HTTPServerResponse, bytesSent as int, remoteAddress as string) virtual
{
}
