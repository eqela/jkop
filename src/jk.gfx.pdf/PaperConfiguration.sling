
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2022 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class:

class Size public
{
	var width public as double
	var height public as double

	ctor
	{
		width = 0.0
		height = 0.0
	}

	ctor(w as double, h as double)
	{
		width = w
		height = h
	}

	func getHeight as double:
		return height

	func getWidth as double:
		return width
}

func forDefault static as this:
	return forA4Portrait()

func forA4Portrait static as this
{
	var v = new this()
	v.setSize(PaperSize.forValue(PaperSize.A4))
	v.setOrientation(PaperOrientation.forValue(PaperOrientation.PORTRAIT))
	return v
}

func forA4Landscape static as this
{
	var v = new this()
	v.setSize(PaperSize.forValue(PaperSize.A4))
	v.setOrientation(PaperOrientation.forValue(PaperOrientation.LANDSCAPE))
	return v
}

pvar size as PaperSize
pvar orientation as PaperOrientation

func getSizeInches as Size
{
	var sz = getRawSizeInches()
	if PaperOrientation.matches(orientation, PaperOrientation.LANDSCAPE):
		return new Size(sz.getHeight(), sz.getWidth())
	return sz
}

func getRawSizeInches as Size
{
	if PaperSize.matches(size, PaperSize.LETTER):
		return new Size(8.5, 11.0)
	if PaperSize.matches(size, PaperSize.LEGAL):
		return new Size(8.5, 14.0)
	if PaperSize.matches(size, PaperSize.A3):
		return new Size(11.7, 16.5)
	if PaperSize.matches(size, PaperSize.A4):
		return new Size(8.27, 11.7)
	if PaperSize.matches(size, PaperSize.A5):
		return new Size(5.8, 8.3)
	if PaperSize.matches(size, PaperSize.B4):
		return new Size(9.8, 13.9)
	if PaperSize.matches(size, PaperSize.B5):
		return new Size(6.9, 9.8)
	return new Size(8.27, 11.7)
}

func getSizeDots (dpi as int) as Size
{
	var szi = getSizeInches()
	return new Size(szi.getWidth() * dpi, szi.getHeight() * dpi)
}
