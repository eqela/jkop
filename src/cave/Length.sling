
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2017 Job and Esther Technologies, Inc.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class imports cape:

func asPoints(value as string, ppi as int) static as int
{
	return(forString(value).toPoints(ppi))
}

func forString(value as string) static as this
{
	var v = new this()
	v.parse(value)
	return(v)
}

func forPoints(value as double) static as this
{
	var v = new this()
	v.setValue(value)
	v.setUnit(UNIT_POINT)
	return(v)
}

func forMilliMeters(value as double) static as this
{
	var v = new this()
	v.setValue(value)
	v.setUnit(UNIT_MILLIMETER)
	return(v)
}

func forMicroMeters(value as double) static as this
{
	var v = new this()
	v.setValue(value)
	v.setUnit(UNIT_MICROMETER)
	return(v)
}

func forNanoMeters(value as double) static as this
{
	var v = new this()
	v.setValue(value)
	v.setUnit(UNIT_NANOMETER)
	return(v)
}

func forInches(value as double) static as this
{
	var v = new this()
	v.setValue(value)
	v.setUnit(UNIT_INCH)
	return(v)
}

func forValue(value as double, unit as int) static as this
{
	var v = new this()
	v.setValue(value)
	v.setUnit(unit)
	return(v)
}

func forStringAsPoints(value as string, ppi as int) static as this
{
	var v = new this()
	v.parse(value)
	v.setValue(v.toPoints(ppi))
	v.setUnit(UNIT_POINT)
	return(v)
}

const UNIT_POINT = 0
const UNIT_MILLIMETER = 1
const UNIT_MICROMETER = 2
const UNIT_NANOMETER = 3
const UNIT_INCH = 4

prop value as double
prop unit as int

func parse(value as string) private
{
	if(value == null) {
		this.value = 0
		unit = UNIT_POINT
		return
	}
	var i = 0
	var n = 0
	var it = String.iterate(value)
	while(true) {
		var c = it.getNextChar()
		if(c < 1) {
			break
		}
		else if(c >= '0' && c <= '9') {
			i *= 10
			i += c - '0' as int
		}
		else {
			break
		}
		n++
	}
	this.value = i
	var suffix = String.subString(value, n)
	if(String.isEmpty(suffix)) {
		unit = UNIT_POINT
	}
	else if(suffix == "pt" || suffix == "px") {
		unit = UNIT_POINT
	}
	else if(suffix == "mm") {
		unit = UNIT_MILLIMETER
	}
	else if(suffix == "um") {
		unit = UNIT_MICROMETER
	}
	else if(suffix == "nm") {
		unit = UNIT_NANOMETER
	}
	else if(suffix == "in") {
		unit = UNIT_INCH
	}
	else {
		unit = UNIT_POINT
	}
}

func toPoints(ppi as int) as int
{
	if(unit == UNIT_POINT) {
		return(value as int)
	}
	if(unit == UNIT_MILLIMETER) {
		var v = value * ppi / 25
		if(value > 0 && v < 1) {
			v = 1
		}
		return(v as int)
	}
	if(unit == UNIT_MICROMETER) {
		var v = value * ppi / 25000
		if(value > 0 && v < 1) {
			v = 1
		}
		return(v as int)
	}
	if(unit == UNIT_NANOMETER) {
		var v = value * ppi / 25000000
		if(value > 0 && v < 1) {
			v = 1
		}
		return(v as int)
	}
	if(unit == UNIT_INCH) {
		var v = value * ppi
		if(value > 0 && v < 1) {
			v = 1
		}
		return(v as int)
	}
	return(0)
}
