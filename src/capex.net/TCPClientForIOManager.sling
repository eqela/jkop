
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2018 Job and Esther Technologies Oy
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class is TCPClient:

func forCurrentIOManager static as this
{
	var mgr = assert IOManager.getCurrent()
	var v = new this()
	v.setIomanager(mgr)
	return v
}

func forIOManager(mgr as IOManager) static as this
{
	var v = new this()
	v.setIomanager(mgr)
	return v
}

var tcpsocket as TCPSocket
var sslsocket as SSLSocket
var rcvbuffer as buffer
prop iomanager as IOManager

ctor
{
	rcvbuffer = MALLOC(64 * 1024)
}

func getSocket as ConnectedSocket
{
	if sslsocket:
		return sslsocket
	return tcpsocket
}

func doConnectSocket(address as string, port as int) private as TCPSocket
{
	return TCPSocket.createAndConnect(address, port)
}

func connectSSL(address as string, port as int, callback as function<void,Error>) override
{
	// FIXME: This needs to be made asynchronous. Use worker threads.
	assert tcpsocket = doConnectSocket(address, port) {
		if callback:
			callback(Error.forCode("failedToConnect"))
	}
	assert sslsocket = SSLSocket.forClient(tcpsocket, address) {
		if callback:
			callback(Error.forCode("sslHandshakeFailed"))
	}
	if callback:
		callback(null)
}

func connect(address as string, port as int, callback as function<void,Error>) override
{
	// FIXME: This needs to be made asynchronous. Use worker threads.
	assert tcpsocket = doConnectSocket(address, port) {
		if callback:
			callback(Error.forCode("failedToConnect"))
	}
	sslsocket = null
	if callback:
		callback(null)
}

func receive(callback as function<void,buffer,int>) override
{
	assert tcpsocket {
		if callback:
			callback(null, 0)
	}
	var ee final = iomanager.add(tcpsocket)
	var cc final = callback
	ee.setReadListener(func {
		ee.remove()
		var ss = getSocket()
		var bb = rcvbuffer
		var v = 0
		if ss:
			v = ss.read(bb)
		if cc:
			cc(bb, v)
	})
}

class MyBuffer
{
	prop data as buffer
	prop size as int
	prop callback as function<void,Error>
}

var toWrite = new Queue<MyBuffer>()
var writeScheduled = false

func send(data as buffer, size as int, callback as function<void,Error>) override
{
	assert tcpsocket {
		if callback:
			callback(Error.forCode("notConnected"))
	}
	var mb = new MyBuffer()
	mb.setData(data)
	mb.setSize(size)
	mb.setCallback(callback)
	toWrite.push(mb)
	scheduleWrite()
}

func scheduleWrite private
{
	if writeScheduled:
		return
	if toWrite.getSize() < 1:
		return
	var ee final = iomanager.add(tcpsocket)
	ee.setWriteListener(func {
		ee.remove()
		writeScheduled = false
		var mb = toWrite.pop()
		if mb {
			var data = mb.getData()
			var size = mb.getSize()
			var callback = mb.getCallback()
			var ss = getSocket()
			var n = 0
			if ss:
				n = ss.write(data, size)
			if callback {
				if n < size {
					callback(Error.forCode("failedToWrite"))
				}
				else {
					callback(null)
				}
			}
		}
		scheduleWrite()
	})
	writeScheduled = true
}

func disconnect(callback as function<void,Error>) override
{
	toWrite.clear()
	writeScheduled = false
	// FIXME: Needs to be made asynchronous
	if sslsocket {
		sslsocket.close()
		sslsocket = null
	}
	if tcpsocket {
		tcpsocket.close()
		tcpsocket = null
	}
	if callback:
		callback(null)
}
