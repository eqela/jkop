
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2018 Job and Esther Technologies Oy
 * Copyright (c) 2018-2019 Eqela Oy
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.io
import jk.app
import jk.log
import jk.env
import jk.process

class is ApplicationContext is LoggingContext:

func forApplicationName(name as string) static as this
{
	var v = new this()
	v.setApplicationName(name)
	return v
}

prop applicationName as string
prop enableColor = true
prop dumbMode = false
prop showInfo = true
prop showWarning = true
prop showError = true
prop showDebug = false
prop enableStatus = true
prop enableOutput = true
prop autoClearStatus = true
prop currentPrefix as string
var lastStatus = false
var lastStatusLength = 0
var errorCounter = 0
var warningCounter = 0
var infoCounter = 0
var debugCounter = 0

IFDEF "target_dotnet" {
}
ELSE {
	var stdout as PrintWriter
}

ctor
{
	IFDEF "target_dotnet" {
	}
	ELSE {
		stdout = Stdout.instance()
		var term = EnvironmentVariable.get("TERM")
		if term == null || term == "cygwin":
			enableColor = false
	}
	if String.equals("yes", EnvironmentVariable.get("CONSOLE_DEBUG")) {
		setEnableDebugMessages(true)
	}
	else if String.equals("true", EnvironmentVariable.get("EQELA_DEBUG")) {
		setEnableDebugMessages(true)
	}
}

IFDEF "target_android" {
	func getAndroidActivityContext as !"android.content.Context":
		return null
}

func isInDebugMode as bool:
	return showDebug

func setEnableDebugMessages(v as bool) as this
{
	showDebug = v
	return this
}

func useApplicationNameAsPrefix
{
	currentPrefix = applicationName
	if String.isEmpty(currentPrefix):
		currentPrefix = CurrentProcess.getProcessName()
}

func resetCounters
{
	errorCounter = 0
	warningCounter = 0
	infoCounter = 0
	debugCounter = 0
}

func getErrorCount as int:
	return errorCounter

func getWarningCount as int:
	return warningCounter

func getInfoCount as int:
	return infoCounter

func getDebugCount as int:
	return debugCounter

func logError(message as string)
{
	if showError && enableOutput:
		doPrintLine("ERROR", 91, currentPrefix, message)
	errorCounter ++
}

func logWarning(message as string)
{
	if showWarning && enableOutput:
		doPrintLine("WARNING", 95, currentPrefix, message)
	warningCounter ++
}

func logInfo(message as string)
{
	if showInfo && enableOutput:
		doPrintLine("INFO", -1, currentPrefix, message)
	infoCounter ++
}

func logDebug(message as string)
{
	if showDebug && enableOutput:
		doPrintLine("DEBUG", 96, currentPrefix, message)
	debugCounter ++
}

func logStatus(message as string)
{
	if enableOutput && enableStatus {
		if currentPrefix && message {
			doPrintStatus("[" .. currentPrefix .. "] " .. message)
		}
		else {
			doPrintStatus(message)
		}
	}
}

func getApplicationDataDirectory as File:
	return getApplicationDataDirectoryForAppId(applicationName)

func getApplicationDataDirectoryForAppId(appId as string = null) as File
{
	var applicationName = appId
	if String.isEmpty(applicationName):
		applicationName = CurrentProcess.getProcessName()
	if String.isEmpty(applicationName):
		return null
	var v as File
	IFDEF "target_dotnet4cs" {
		if OS.isWindows() {
			v = File.forPath(lang "cs" string {{{ System.Environment.GetFolderPath(System.Environment.SpecialFolder.LocalApplicationData) }}}).entry(applicationName)
		}
	}
	if v == null:
		v = File.forRelativePath("." .. applicationName, CommonPath.getHomeDirectory())
	return v
}

func doPrintLine(type as string #preserve, color as int #preserve, prefix as string #preserve, message as string #preserve) private
{
	if autoClearStatus:
		doPrintStatus(null)
	IFDEF "target_dotnet" {
		if lastStatus {
			lang "cs" {{{
				System.Console.WriteLine("");
			}}}
		}
		lang "cs" {{{
			System.Console.ResetColor();
			if(type != null || prefix != null) {
				System.Console.Write("[");
				if(enableColor && dumbMode == false) {
					if(color == 91) {
						System.Console.ForegroundColor = System.ConsoleColor.Red;
					}
					else if(color == 95) {
						System.Console.ForegroundColor = System.ConsoleColor.Magenta;
					}
					else if(color == 96) {
						System.Console.ForegroundColor = System.ConsoleColor.Cyan;
					}
				}
				if(type != null) {
					System.Console.Write(type);
					if(prefix != null) {
						System.Console.Write(":");
					}
				}
				System.Console.ResetColor();
				if(prefix != null) {
					System.Console.Write(prefix);
				}
				System.Console.Write("] ");
			}
			System.Console.WriteLine(message);
		}}}
	}
	ELSE {
		var sb = new StringBuilder()
		if lastStatus:
			sb.appendCharacter('\n')
		if type || prefix {
			sb.appendCharacter('[')
			if dumbMode == false && enableColor && color >= 0 {
				sb.appendCharacter(0x1b as char)
				sb.appendCharacter('[')
				sb.appendInteger(color)
				sb.appendCharacter('m')
			}
			if type {
				sb.appendString(type)
				if prefix:
					sb.appendCharacter(':')
			}
			if prefix:
				sb.appendString(prefix)
			if dumbMode == false && enableColor && color >= 0 {
				sb.appendCharacter(0x1b as char)
				sb.appendString("[39m")
			}
			sb.appendCharacter(']')
			sb.appendCharacter(' ')
		}
		sb.appendString(message)
		sb.appendCharacter('\n')
		if stdout:
			stdout.print(sb.toString())
	}
	lastStatus = false
}

func doPrintStatus(m as string #preserve) private
{
	if showDebug && m {
		doPrintLine(null, -1, null, m)
		return
	}
	if dumbMode {
		if m == null:
			return
		IFDEF "target_dotnet" {
			lang "cs" {{{
				System.Console.WriteLine(m);
			}}}
		}
		ELSE {
			if stdout:
				stdout.print(m .. "\n")
		}
		return
	}
	if lastStatusLength < 1 && m == null {
		return
	}
	var sb #preserve = new StringBuilder()
	sb.appendCharacter('\r')
	for(var n = 0 ; n<lastStatusLength ; n++):
		sb.appendCharacter(' ')
	sb.appendCharacter('\r')
	var rm = m
	if rm {
		var width = 0
		IFDEF "target_dotnet" {
			width = lang "cs" int {{{ System.Console.WindowWidth }}}
		}
		if width > 0 && String.getLength(rm) >= width:
			rm = String.getSubString(rm, 0, width - 4) .. " .."
		sb.appendString(rm)
	}
	IFDEF "target_dotnet" {
		lang "cs" {{{
			System.Console.Write(sb.toString());
		}}}
	}
	ELSE {
		if stdout:
			stdout.print(sb.toString())
	}
	if m {
		lastStatusLength = String.getLength(rm)
	}
	else {
		lastStatusLength = 0
	}
	if rm == null {
		lastStatus = false
	}
	else {
		lastStatus = true
	}
}
