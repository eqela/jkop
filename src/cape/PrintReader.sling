
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2018 Job and Esther Technologies Oy
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class is Reader is LineReader is Closable:

func forReader(reader as Reader) static as this
{
	assert reader
	if reader is this:
		return reader as! this
	var v = new this()
	v.setReader(reader, null)
	return v
}

func forReaderAndEncoding(reader as Reader, encoding as string) static as this
{
	assert reader
	if reader is this:
		return reader as! this
	var v = new this()
	v.setReader(reader, encoding)
	return v
}

var reader as Reader
var iterator as CharacterIteratorForReader

func setReader(reader as Reader, encoding as string)
{
	this.reader = reader
	if reader == null {
		iterator = null
	}
	else {
		iterator = CharacterIteratorForReader.forReader(reader)
		if encoding:
			iterator.setEncoding(encoding)
	}
}

func readLine as string
{
	assert iterator
	var sb = new StringBuilder()
	loop {
		var c = iterator.getNextCharacter()
		if c < 1 {
			if sb.count() < 1:
				return null
			break
		}
		if c == '\r':
			continue
		if c == '\n':
			break
		sb.appendCharacter(c)
	}
	if sb.count() < 1:
		return ""
	return sb.toString()
}

func read(buffer as buffer) as int
{
	if reader == null:
		return -1
	return reader.read(buffer)
}

func close
{
	var rc = reader as Closable
	if rc:
		rc.close()
}
