
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2022 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class is IntegerObject:

func forValue(value as int) static as this:
	return new this(value)

func asInteger(obj as dynamic, defval as int = 0) static as int #deprecated:
	return asValue(obj, defval)

func asValue(obj as dynamic, defval as int) static as int
{
	var v = asObject(obj)
	if not v:
		return defval
	return v.toInteger()
}

func asObject(obj as dynamic) static as this
{
	if not obj:
		return null
	if obj is int:
		return new this(obj as int)
	if obj is this:
		return obj as! this
	if obj is IntegerObject {
		var oi = obj as! IntegerObject
		return new this(oi.toInteger())
	}
	if obj is long {
		var v = obj as long
		return new this(v as int)
	}
	if obj is LongIntegerObject {
		var oi = obj as! LongIntegerObject
		return new this(oi.toLong() as int)
	}
	if obj is string:
		return new this(String.toInteger(obj as! string))
	if obj is StringObject:
		return new this(String.toInteger((obj as! StringObject).toString()))
	if obj is double {
		var v = obj as double
		var vv = v as int
		return new this(vv)
	}
	if obj is DoubleObject {
		var od = obj as! DoubleObject
		var vv = od.toDouble() as int
		return new this(vv)
	}
	if obj is bool {
		if obj as bool == true:
			return new this(1)
		return new this(0)
	}
	if obj is BooleanObject {
		if (obj as! BooleanObject).toBoolean():
			return new this(1)
		return new this(0)
	}
	if obj is char {
		var v = obj as char
		return new this(v as int)
	}
	if obj is CharacterObject {
		var oc = obj as! CharacterObject
		return new this(oc.toCharacter() as int)
	}
	return null
}

ctor:
	nop

ctor(value as int):
	setValue(value)

pvar value as int

func add(amount as int):
	value += amount

func toInteger as int:
	return value
