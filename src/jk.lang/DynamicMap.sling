
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2023 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class is Duplicateable is Iterateable<string>:

IFDEF "target_sushi"
{
	var nullObject static as object = "null"
}

func asDynamicMap(value as dynamic) static as this
{
	assert value
	if value is this:
		return value as! this
	if value is map:
		return forMap(value as! map)
	return null
}

func forMap(map as map) static as this
{
	var v = new this()
	if map {
		var it = Map.iterateKeys(map)
		while it {
			var key = it.next()
			if key == null:
				break
			if key is string == false:
				continue
			v.setDynamic(key as! string, Map.getValue(map, key as! string))
		}
	}
	return v
}

func forStringMap(map as map<string,string>) static as this
{
	var v = new this()
	if map {
		var it = Map.iterateKeys(map)
		while it {
			var key = it.next()
			if key == null:
				break
			v.setString(key, Map.getValue(map, key))
		}
	}
	return v
}

func forDynamicMaps(map1 as this, map2 as this) static as this
{
	var v = new this()
	v.mergeFrom(map1)
	v.mergeFrom(map2)
	return v
}

func forDynamicMap(map as this) static as this
{
	assert map
	return map.duplicateMap()
}

func forData(map as this) static as this
{
	assert map
	return map.duplicateMap()
}

var map as map<string,dynamic>

ctor:
	map = new map<string,dynamic>

func asMap as map<string,dynamic>
{
	IFDEF "target_sushi" {
		var v = new map<string,dynamic>
		var it = iterateKeys()
		while it {
			var key = it.next()
			if not key:
				break
			Map.set(v, key, getDynamic(key))
		}
		return v
	}
	ELSE {
		return map
	}
}

func asStringMap as map<string,string>:
	return toStringMap()

func toStringMap as map<string,string>
{
	var v = new map<string,string>
	var it = iterateKeys()
	while it {
		var key = it.next()
		if not key:
			break
		Map.set(v, key, getString(key))
	}
	return v
}

func toMap as map
{
	var v = new map
	var it = iterateKeys()
	while it {
		var key = it.next()
		if not key:
			break
		Map.set(v, key, getDynamic(key))
	}
	return v
}

func duplicate as object:
	return duplicateMap()

func duplicateMap as this
{
	var v = new this()
	var it = iterateKeys()
	while it {
		var key = it.next()
		if key == null:
			break
		v.setDynamic(key, getDynamic(key))
	}
	return v
}

func mergeFrom(other as this) as this
{
	if other == null:
		return this
	var it = other.iterateKeys()
	while it {
		var key = it.next()
		if key == null:
			break
		setDynamic(key, other.getDynamic(key))
	}
	return this
}

func setDynamic(key as string, value as dynamic) as this
{
	if key {
		IFDEF "target_sushi" {
			if value == null {
				map[key] = nullObject
			}
			else {
				map[key] = value
			}
		}
		ELSE {
			map[key] = value
		}
	}
	return this
}

func setObject(key as string, value as object) as this:
	return setDynamic(key, value)

func setString(key as string, value as string) as this
{
	if key {
		IFDEF "target_sushi" {
			if value == null {
				map[key] = nullObject
			}
			else {
				map[key] = value
			}
		}
		ELSE {
			map[key] = value
		}
	}
	return this
}

func setBuffer(key as string, value as buffer) as this
{
	if key != null && value == null {
		IFDEF "target_sushi" {
			map[key] = nullObject
		}
		ELSE {
			map[key] = null
		}
		return this
	}
	return setObject(key, Buffer.asObject(value))
}

func setInteger(key as string, value as int) as this:
	return setObject(key, Integer.asObject(value))

func setLong(key as string, value as long) as this #deprecated:
	return setObject(key, LongInteger.asObject(value))

func setLongInteger(key as string, value as long) as this:
	return setObject(key, LongInteger.asObject(value))

func setBoolean(key as string, value as bool) as this:
	return setObject(key, Boolean.asObject(value))

func setDouble(key as string, value as double) as this:
	return setObject(key, Double.asObject(value))

func get(key as string) as dynamic:
	return getDynamic(key)

func getDynamic(key as string) as dynamic
{
	var v = Map.getValue(map, key)
	IFDEF "target_sushi" {
		if v == nullObject:
			v = null
	}
	return v
}

func getObject(key as string) as object:
	return getDynamic(key) as object

func getString(key as string, defval as string = null) as string
{
	var v = String.asString(getDynamic(key))
	if v == null:
		return defval
	return v
}

func getInteger(key as string, defval as int = 0) as int
{
	var vv = getDynamic(key)
	if vv == null:
		return defval
	return Integer.asInteger(vv)
}

func getLongInteger(key as string, defval as long = 0) as long
{
	var vv = getDynamic(key)
	if vv == null:
		return defval
	return LongInteger.asLong(vv)
}

func getBoolean(key as string, defval as bool = false) as bool
{
	var vv = getDynamic(key)
	if vv == null:
		return defval
	return Boolean.asBoolean(vv)
}

func getDouble(key as string, defval as double = 0.0) as double
{
	var vv = getDynamic(key)
	if vv == null:
		return defval
	return Double.asDouble(vv)
}

func getBuffer(key as string) as buffer
{
	var vv = assert getDynamic(key)
	return Buffer.asBuffer(vv)
}

func getDynamicVector(key as string) as DynamicVector
{
	var vv = getDynamic(key) as DynamicVector
	if vv:
		return vv
	var v = getVector(key)
	if v:
		return DynamicVector.forVector(v)
	return null
}

func getVector(key as string) as vector
{
	var val = assert getDynamic(key)
	if val is vector:
		return val as! vector
	if val is VectorObject<dynamic> {
		var vo = val as! VectorObject<dynamic>
		return vo.toVector()
	}
	return null
}

func getVectorOfStrings(key as string) as vector<string>
{
	var vv = assert getVector(key)
	var v = new vector<string>
	foreach o in vv {
		var s = String.asString(o)
		if s:
			v += s
	}
	return v
}

func getDynamicMap(key as string) as this:
	return getDynamic(key) as this

func getKeys as vector<string>:
	return Map.getKeys(map)

func getValues as vector
{
	var v = Map.getValues(map)
	IFDEF "target_sushi" {
		var v2 = new vector
		foreach o in v {
			if o != nullObject:
				v2 += o
		}
		v = v2
	}
	return v
}

func iterate as Iterator<string>:
	return Map.iterateKeys(map)

func iterateKeys as Iterator<string>:
	return Map.iterateKeys(map)

func iterateValues as Iterator<dynamic>:
	return Vector.iterate(getValues())

func remove(key as string):
	Map.remove(map, key)

func clear:
	Map.clear(map)

func getCount as int:
	return Map.count(map)

func containsKey(key as string) as bool:
	return Map.containsKey(map, key)

func removeNullValues as this
{
	var keys = getKeys()
	foreach key in keys {
		var value = getDynamic(key)
		if value == null:
			remove(key)
	}
	return this
}
