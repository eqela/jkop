
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2018 Job and Esther Technologies Oy
 * Copyright (c) 2018-2019 Eqela Oy
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class is StringObject:

func forString(str as string, normalizePath as bool = false) static as this
{
	var v = new this()
	v.parse(str, normalizePath)
	return v
}

prop scheme as string
prop username as string
prop password as string
prop host as string
prop port as string
prop path as string
prop fragment as string
prop rawQueryParameters as KeyValueList<string,string>
prop queryParameters as map<string,string>
prop original as string
prop percentOnly = false
prop encodeUnreservedChars = true

func dup as this
{
	var v = new this()
	v.setScheme(scheme)
	v.setUsername(username)
	v.setPassword(password)
	v.setHost(host)
	v.setPort(port)
	v.setPath(path)
	v.setFragment(fragment)
	if rawQueryParameters:
		v.setRawQueryParameters(rawQueryParameters.dup())
	if queryParameters:
		v.setQueryParameters(Map.dup(queryParameters))
	return v
}

func toString as string:
	return toStringDo(true)

func toStringNohost as string:
	return toStringDo(false)

func toStringDo(userhost as bool) private as string
{
	var sb = new StringBuilder()
	if userhost {
		if scheme {
			sb.append(scheme)
			sb.append("://")
		}
		if username {
			sb.append(URLEncoder.encode(username))
			if password {
				sb.append(':')
				sb.append(URLEncoder.encode(password))
			}
			sb.append('@')
		}
		if host {
			sb.append(host)
			if port {
				sb.append(':')
				sb.append(port)
			}
		}
	}
	if path:
		sb.append(String.replace(path, ' ', '+'))
	if rawQueryParameters && rawQueryParameters.count() > 0 {
		var first = true
		var it as Iterator<string> #force = Map.iterateKeys(queryParameters)
		while it {
			var key = it.next() as string
			if key == null:
				break
			if first {
				sb.append('?')
				first = false
			}
			else {
				sb.append('&')
			}
			sb.append(key)
			var val = Map.get(queryParameters, key)
			if val {
				sb.append('=')
				sb.append(URLEncoder.encode(val, percentOnly, encodeUnreservedChars))
			}
		}
	}
	if fragment {
		sb.append('#')
		sb.append(fragment)
	}
	return sb.toString()
}

func clearQueryParameters
{
	rawQueryParameters = null
	queryParameters = null
}

func normalizePath(path as string) private as string
{
	assert path
	var v = new StringBuilder()
	var comps = String.split(path, '/')
	foreach comp in comps {
		if String.isEmpty(comp) {
			;
		}
		else if comp == "." {
			;
		}
		else if comp == ".." {
			var str = v.toString()
			v.clear()
			if str {
				var slash = String.lastIndexOf(str, '/')
				if slash > 0:
					v.append(String.getSubString(str, 0, slash))
			}
		}
		else {
			v.append('/')
			v.append(comp)
		}
	}
	if v.count() < 2:
		return "/"
	if String.endsWith(path, "/"):
		v.append('/')
	return v.toString()
}

func parse(astr as string, doNormalizePath as bool)
{
	setOriginal(astr)
	assert astr
	var fsp = String.split(astr, '#', 2)
	var str = Vector.get(fsp, 0)
	fragment = Vector.get(fsp, 1)
	var qsplit = String.split(str, '?', 2)
	str = Vector.get(qsplit, 0)
	var queryString = Vector.get(qsplit, 1)
	if String.isEmpty(queryString) == false {
		var qss = String.split(queryString, '&')
		foreach qs in qss {
			if not rawQueryParameters:
				rawQueryParameters = new KeyValueList<string,string>()
			if not queryParameters:
				queryParameters = new map<string,string>
			if String.indexOf(qs, '=') < 0 {
				Map.set(queryParameters, qs, null)
				rawQueryParameters.add(qs, null)
				continue
			}
			var qsps = String.split(qs, '=', 2)
			var key = Vector.get(qsps, 0)
			var val = Vector.get(qsps, 1)
			if String.isEmpty(key) == false {
				if not val:
					val = ""
				var udv = URLDecoder.decode(val)
				Map.set(queryParameters, key, udv)
				rawQueryParameters.add(key, udv)
			}
		}
	}
	var css = String.indexOf(str, "://")
	if css >= 0 {
		scheme = String.subString(str, 0, css)
		if String.indexOf(scheme, ':') >= 0 || String.indexOf(scheme, '/') >= 0 {
			scheme = null
		}
		else {
			str = String.subString(str, css+3)
		}
	}
	var pp as string
	if String.charAt(str, 0) == '/' {
		pp = URLDecoder.decode(str)
	}
	else {
		if String.indexOf(str, '/') >= 0 {
			var sssplit = String.split(str, '/', 2)
			str = Vector.get(sssplit, 0)
			pp = Vector.get(sssplit, 1)
			if not pp:
				pp = ""
			if String.charAt(pp, 0) != '/':
				pp = String.append("/", pp)
			pp = URLDecoder.decode(pp)
		}
		if String.indexOf(str, '@') >= 0 {
			var asplit = String.split(str, '@', 2)
			var auth = Vector.get(asplit, 0)
			str = Vector.get(asplit, 1)
			if String.indexOf(auth, ':') >= 0 {
				var acsplit = String.split(auth, ':', 2)
				username = URLDecoder.decode(Vector.get(acsplit, 0))
				password = URLDecoder.decode(Vector.get(acsplit, 1))
			}
			else {
				username = auth
			}
		}
		if String.indexOf(str, ':') >= 0 {
			var hcsplit = String.split(str, ':', 2)
			str = Vector.get(hcsplit, 0)
			port = Vector.get(hcsplit, 1)
		}
		host = str
	}
	if doNormalizePath {
		path = normalizePath(pp)
	}
	else {
		path = pp
	}
	if not port && String.equals("https", scheme) || String.equals("wss", scheme):
		port = "443"
}

func getPortInt as int
{
	if not port:
		return 0
	return String.toInteger(port)
}

func getQueryParameter(key as string) as string
{
	if not queryParameters:
		return null
	return Map.get(queryParameters, key)
}
