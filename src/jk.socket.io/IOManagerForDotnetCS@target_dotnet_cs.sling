
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2024 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.log
import jk.time
import jk.thread
import jk.socket
import jk.socket.ssl

class is IOManager:

class MyTimer is IOManagerTimer is Runnable
{
	pvar delay as long
	pvar parent as parent?
	pvar handler as function<bool>?

	var stopFlag = false
	var targetTime as TimeValue?

	func hasElapsed(now as TimeValue) as bool:
		return TimeValue.diff(targetTime, now) <= 0

	func process(now as TimeValue) as bool
	{
		assert handler
		if hasElapsed(now) == false:
			return true
		if handler() == false:
			return false
		targetTime = null
		return initialize()
	}

	func stop:
		stopFlag = true

	func run
	{
		stopFlag = false
		var now = new TimeValue()
		while stopFlag == false {
			SystemClock.update(now)
			var diff = TimeValue.diff(targetTime, now)
			if diff <= 0:
				break
			if diff > 5000000:
				diff = 5000000
			CurrentThread.sleepMicroSeconds(diff as! int)
		}
		if parent:
			parent.sendSignal()
	}

	func initialize as bool
	{
		if targetTime != null:
			return false
		var orig = SystemClock.asTimeValue()
		targetTime = orig.add(0, delay)
		Thread.start(this)
		return true
	}
}

class MyEntry is IOManagerEntry
{
	pvar socket as ConnectedSocket?
	pvar master as parent?

	var rrl as function<void>?
	var wrl as function<void>?
	var added = false

	func onReadReady
	{
		var rrl = this.rrl
		assert rrl
		rrl()
	}

	func onWriteReady
	{
		var wrl = this.wrl
		assert wrl
		wrl()
	}

	func setListeners(rrl as function<void>?, wrl as function<void>?) as bool
	{
		this.rrl = rrl
		this.wrl = wrl
		return update()
	}

	func setReadListener(rrl as function<void>?) as bool
	{
		this.rrl = rrl
		return update()
	}

	func setWriteListener(wrl as function<void>?) as bool
	{
		this.wrl = wrl
		return update()
	}

	func update as bool
	{
		remove()
		assert socket
		assert master
		if rrl == null && wrl == null:
			return true
		if rrl != null:
			Vector.appendValue(master.readlist, this)
		if wrl != null:
			Vector.appendValue(master.writelist, this)
		added = true
		return true
	}

	func remove as bool
	{
		if added == false || master == null:
			return false
		Vector.removeValue(master.readlist, this)
		Vector.removeValue(master.writelist, this)
		added = false
		return true
	}

	func getNetSocket as [magical:System.Net.Sockets.Socket]
	{
		if socket is TCPSocket {
			var myss = socket as TCPSocketForDotnetCS
			if not myss:
				ERROR "invalidTcpSocket"
			return myss.getNetSocket()
		}
		if socket is SSLSocket {
			var myss = socket as SSLSocketForDotnetCS
			if not myss:
				ERROR "invalidSslSocket"
			return myss.getNetSocket()
		}
		ERROR "invalidSocketType"
	}
}

var exitflag as bool
var running = false
var signalPort = -1
var readlist public = new vector<IOManagerEntry>
var writelist public = new vector<IOManagerEntry>
var timers = new vector<MyTimer>

func getEntryForSocket(socket as [magical:System.Net.Sockets.Socket], list as vector<IOManagerEntry>) as IOManagerEntry?
{
	foreach ee as MyEntry in list {
		if ee.getNetSocket() == socket:
			return ee
	}
	return null
}

func onReadReady(socket as TCPSocket)
{
	var ss = socket.accept()
	assert ss
	ss.close()
}

func doExecute(ctx as LoggingContext) override as bool
{
	exitflag = false
	running = true
	var signalSocket = openSignalSocket()
	if signalSocket != null {
		var ee = add(signalSocket)
		if ee != null:
			ee.setReadListener(func => onReadReady(signalSocket))
	}
	var reads = new vector<object>
	var writes = new vector<object>
	var now = new TimeValue()
	Log.debug(ctx, VALUE "__THIS__" .. " started")
	while exitflag == false {
		if executeSelect(ctx, reads, writes) == false:
			continue
		foreach ele as MyEntry in reads:
			ele.onReadReady()
		foreach ele as MyEntry in writes:
			ele.onWriteReady()
		Vector.clear(reads)
		Vector.clear(writes)
		if Vector.isEmpty(timers) == false {
			SystemClock.update(now)
			// FIXME: The timer removal process is NOT fast.
			var timersToRemove as vector<MyTimer>?
			foreach timer in timers {
				if timer.process(now) == false {
					if timersToRemove == null:
						timersToRemove = new vector<MyTimer>
					timersToRemove += timer
				}
			}
			if timersToRemove: foreach timer in timersToRemove:
				Vector.removeValue(timers, timer)
		}
	}
	if signalSocket != null {
		signalSocket.close()
		signalSocket = null
	}
	foreach timer in timers:
		timer.stop()
	Vector.clear(timers)
	Vector.clear(readlist)
	Vector.clear(writelist)
	signalPort = -1
	running = false
	Log.debug(ctx, VALUE "__THIS__" .. " ended")
	return true
}

func executeSelect(ctx as LoggingContext, reads as vector<object>, writes as vector<object>) private as bool
{
	lang "cs" {{{
		var fdsetr = new System.Collections.Generic.List<System.Net.Sockets.Socket>();
		var fdsetw = new System.Collections.Generic.List<System.Net.Sockets.Socket>();
	}}}
	foreach myo1 as MyEntry in readlist {
		var nsor = myo1.getNetSocket()
		lang "cs" {{{
			fdsetr.Add(nsor);
		}}}
	}
	foreach myo2 as MyEntry in writelist {
		var nsow = myo2.getNetSocket()
		lang "cs" {{{
			fdsetw.Add(nsow);
		}}}
	}
	lang "cs" {{{
		try {
			System.Net.Sockets.Socket.Select(fdsetr, fdsetw, null, -1);
		}
		catch(System.Exception e) {
			jk.log.Log.error(ctx, "Call to Select failed: " + e.ToString());
			return(false);
		}
		foreach(System.Net.Sockets.Socket socket in fdsetr) {
			var e = getEntryForSocket(socket, readlist);
			if(e != null) {
				jk.lang.Vector.appendValue(reads, e);
			}
		}
		foreach(System.Net.Sockets.Socket socket in fdsetw) {
			var e = getEntryForSocket(socket, writelist);
			if(e != null) {
				jk.lang.Vector.appendValue(writes, e);
			}
		}
	}}}
	return true
}

func openSignalSocket as TCPSocket?
{
	var v = TCPSocket.create()
	var n as int
	for(n = 20000 ; n < 60000 ; n++) {
		// FIXME: This should be bound to localhost only
		try {
			v.listen(n)
			signalPort = n
			return v
		}
		catch e {
		}
	}
	return null
}

func stop override
{
	exitflag = true
	sendSignal()
}

func sendSignal
{
	if signalPort > 0 {
		var signalClient = TCPSocket.createAndConnect("127.0.0.1", signalPort)
		signalClient.close()
	}
}

func isRunning as bool:
	return running

func add(o as object) override as IOManagerEntry?
{
	var s = assert o as ConnectedSocket
	return new MyEntry().setMaster(this).setSocket(s)
}

func startTimer(delay as long, handler as function<bool>) override as IOManagerTimer?
{
	var timer = new MyTimer()
	timer.setDelay(delay)
	timer.setParent(this)
	timer.setHandler(handler)
	if timer.initialize() == false:
		return null
	timers += timer
	return timer
}
