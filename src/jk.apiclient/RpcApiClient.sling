
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2023 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.json
import jk.base64

class:

func callRemoteFunction(apiClient as ApiClient, name as string, data as DynamicMap) static async as DynamicMap
{
	var dd = data
	if not dd:
		dd = new DynamicMap()
	var bdata = String.toUTF8Buffer(JSONEncoder.encode(dd))
	var url = "/func/" .. name
	var headers = new KeyValueList<string,string>()
	headers.add("content-type", "application/json")
	var response = await apiClient.request("POST", url, headers, bdata)
	var status = response.statusCode
	var body = response.body
	if status == null {
		var detail = new DynamicMap()
		detail.setString("url", url)
		detail.setString("error", String.forUTF8Buffer(body))
		Error.throw("failedToCallRemoteFunction", JSONEncoder.toString(detail))
	}
	var str = String.forUTF8Buffer(body)
	var data = JSONParser.parseString(str) as DynamicMap
	if not data {
		var detail = new DynamicMap()
		detail.setString("url", url)
		detail.setString("responseBody", str)
		Error.throw("failedToCallRemoteFunction", JSONEncoder.toString(detail))
	}
	data.setString("url", url)
	data.setString("name", name)
	return data
}

func validateResponse(response as DynamicMap) static as object
{
	if not response:
		Error.throw("nullResponse", null)
	if response.getCount() < 1:
		Error.throw("responseWithNoContent", null)
	var statusInBody = response.getString("status")
	if not String.equals(statusInBody, "ok") {
		if String.equals(statusInBody, "error") {
			var error = response.getString("errorCode")
			var detail = response.getString("errorDetail")
			if String.isEmpty(error) {
				error = "errorStatusFromRemoteFunction"
				if String.isEmpty(detail):
					detail = response.getString("name")
			}
			Error.throw(error, detail)
		}
		else {
			Error.throw("unsupportedRpcStatus", statusInBody)
		}
	}
	return response.getObject("data")
}

func responseAsString(response as DynamicMap) static as string
{
	var r = validateResponse(response)
	var v = String.asString(r)
	if not v:
		Error.throw("responseIsNotString", response)
	return v
}

func responseAsBoolean(response as DynamicMap) static as bool
{
	var r = validateResponse(response)
	var v = Boolean.asObject(r)
	if not v:
		Error.throw("responseIsNotBoolean", response)
	return v.toBoolean()
}

func responseAsLongInteger(response as DynamicMap) static as long
{
	var r = validateResponse(response)
	var v = LongInteger.asObject(r)
	if not v:
		Error.throw("responseIsNotLongInteger", response)
	return v.toLong()
}

func responseAsInteger(response as DynamicMap) static as long
{
	var r = validateResponse(response)
	var v = Integer.asObject(r)
	if not v:
		Error.throw("responseIsNotInteger", response)
	return v.toInteger()
}

func responseAsBuffer(response as DynamicMap) static as buffer
{
	var r = validateResponse(response)
	var v = Buffer.asBuffer(r)
	if not v {
		var rs = String.asString(r)
		if rs:
			v = Base64Decoder.decode(rs)
	}
	if not v:
		Error.throw("responseIsNotBuffer", response)
	return v
}

func responseAsDynamicMap(response as DynamicMap) static as DynamicMap
{
	var r = validateResponse(response)
	var v = DynamicMap.asDynamicMap(r)
	if not v:
		Error.throw("responseIsNotDynamicMap", response)
	return v
}
