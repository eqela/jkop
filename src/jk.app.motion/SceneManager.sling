
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2024 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class:

func forScene(scene as Scene, backend as Backend) static as this
{
	var v = new this()
	v.setBackend(backend)
	v.pushScene(scene)
	return v
}

var sceneStack = new Stack<Scene>()
var isInitialized = false
var isStarted = false

pvar backend as Backend?
pvar context as GuiApplicationContext?

func initialize virtual
{
	isInitialized = true
	var scene = sceneStack.peek()
	if scene:
		scene.initialize()
}

func start virtual
{
	isStarted = true
	var scene = sceneStack.peek()
	if scene:
		scene.start()
}

func stop virtual
{
	isStarted = false
	var scene = sceneStack.peek()
	if scene:
		scene.stop()
}

func cleanup virtual
{
	isInitialized = false
	while doPopScene():
		nop
}

func onNewCurrentScene private
{
	var scene = assert sceneStack.peek()
	if isInitialized:
		scene.initialize()
	if isStarted:
		scene.start()
}

func getCurrentScene as Scene?:
	return sceneStack.peek()

func replaceScene(next as Scene?)
{
	// FIXME: Do a timer or something so that the outgoing scene goes out of stack
	doReplaceCurrentScene(next)
}

func doReplaceCurrentScene(next as Scene?) private
{
	popScene()
	pushScene(next)
}

func pushScene(scene as Scene?)
{
	var currentScene = sceneStack.peek()
	if currentScene {
		if isStarted:
			currentScene.stop()
		if isInitialized:
			currentScene.cleanup()
	}
	if scene {
		scene.setContext(context)
		scene.setBackend(backend)
		scene.setManager(this)
		sceneStack.push(scene)
		onNewCurrentScene()
	}
}

func popScene
{
	// FIXME: Do a timer or something so that the outgoing scene goes out of stack
	doPopScene()
}

func doPopScene private as bool
{
	var currentScene = sceneStack.pop()
	if currentScene {
		if isStarted:
			currentScene.stop()
		if isInitialized:
			currentScene.cleanup()
		currentScene.setManager(null)
		currentScene.setBackend(null)
		onNewCurrentScene()
		return true
	}
	return false
}
