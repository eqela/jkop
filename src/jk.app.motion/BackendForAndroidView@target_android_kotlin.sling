
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2023 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class is Backend:

class ViewSprite is Sprite
{
	var view protected as native<android.view.View>?
	pvar widthFactor as double
	pvar heightFactor as double
	var myx protected = -99999.9
	var myy protected = -99999.9
	var mywidth protected as double
	var myheight protected as double
	var myAngle protected = 0.0
	var myAlpha protected = 1.0

	func getView as native<android.view.View>
	{
		return view
	}

	func getViewGroup as native<android.view.ViewGroup>
	{
		return view as native<android.view.ViewGroup>
	}

	func setView(value as native<android.view.View>) as this
	{
		view = value
		return this
	}

	func setBackgroundColor(color as Color)
	{
		if color {
			view.setBackgroundColor(color.toARGBInt32())
		}
		else {
			view.setBackgroundColor(0)
		}
	}

	func move(x as double, y as double)
	{
		if x != myx {
			view.setX(x * widthFactor as float)
			myx = x
		}
		if y != myy {
			view.setY(y * heightFactor as float)
			myy = y
		}
	}

	func updateSizeAsPixels(wpx as int, hpx as int)
	{
		mywidth = wpx / widthFactor
		myheight = hpx / heightFactor
	}

	func setRotation(angle as double)
	{
		if myAngle == angle:
			return
		var a = angle * 180.0 / Math.M_PI
		view.setRotation(a as float)
		myAngle = angle
	}

	func setAlpha(alpha as double)
	{
		var f = alpha
		if f < 0:
			f = 0
		if f > 1:
			f = 1
		if f == myAlpha:
			return
		view.setAlpha(f as float)
		myAlpha = f
	}

	func scale(scalex as double, scaley as double)
	{
		view.setScaleX(scalex as float)
		view.setScaleY(scaley as float)
	}

	func getX as double
	{
		return myx
	}

	func getY as double
	{
		return myy
	}

	func getWidth as double
	{
		return mywidth
	}

	func getHeight as double
	{
		return myheight
	}

	func getRotation as double
	{
		return myAngle
	}

	func getAlpha as double
	{
		return myAlpha
	}

	func getScaleX as double
	{
		return view.getScaleX() as double
	}

	func getScaleY as double
	{
		return view.getScaleY() as double
	}

	func removeFromContainer
	{
		if not view:
			return
		var v = view
		var pp = lang "kotlin" as native<android.view.ViewParent> {{{ v!!.getParent() }}}
		if not pp:
			return
		var parent = pp as native<android.view.ViewGroup>
		if not parent:
			return
		parent.removeView(view)
	}
}

class MyTextureSprite is ViewSprite is TextureSprite
{
	pvar context as native<android.content.Context>?
	pvar layer as MySpriteLayer?

	func resize(w as double, h as double)
	{
		if w == mywidth && h == myheight:
			return
		var wpx = w * getWidthFactor() as int
		var hpx = h * getHeightFactor() as int
		lang "kotlin" {{{
			val x = (view!!.getX()).toInt()
			val y = (view!!.getY()).toInt()
			view!!.layout(x, y, x+wpx, y+hpx)
		}}}
		mywidth = w
		myheight = h
	}

	func setTexture(texture as Texture)
	{
		setTextureWithSize(texture, getWidth(), getHeight())
	}

	func setTextureWithSize(texture as Texture, width as double, height as double)
	{
		if texture == null || (width <= 0.0 && height <= 0.0):
			return
		var w = SpriteUtil.getAspectWidth(texture.getPixelWidth(), texture.getPixelHeight(), width, height)
		var h = SpriteUtil.getAspectHeight(texture.getPixelWidth(), texture.getPixelHeight(), width, height)
		if w <= 0.0:
			w = height
		if h <= 0.0:
			h = width
		if w <= 0.0 || h <= 0.0:
			return
		if w <= 0.0 || h <= 0.0:
			return
		if texture is ColorTexture {
			var view = getView()
			if view == null {
				view = new native<android.view.View>(context)
				setView(view)
			}
			resize(w, h)
			setBackgroundColor((texture as! ColorTexture).getColor())
			return
		}
		if texture is ImageTexture {
			var img = assert (texture as! ImageTexture).getImage() as ImageForAndroidKotlin
			var bm = assert img.getKotlinBitmap()
			var view = getView() as native<android.widget.ImageView>
			if view == null {
				view = new native<android.widget.ImageView>(context)
				setView(view)
			}
			lang "kotlin" {{{
				view.setScaleType(android.widget.ImageView.ScaleType.FIT_XY);
			}}}
			resize(w, h)
			view.setImageBitmap(bm)
			return
		}
		ERROR "unknownTextureType"
	}
}

class TextViewSprite is ViewSprite is TextSprite
{
	var context as KotlinApplicationContext?

	ctor(androidContext as KotlinApplicationContext)
	{
		this.context = androidContext
	}

	func initialize(context as native<android.content.Context>, text as TextProperties) as bool
	{
		var vv = new native<android.widget.TextView>(context)
		setView(vv)
		setText(text)
		return true
	}

	func setText(desc as TextProperties?)
	{
		var view = getView() as native<android.widget.TextView>
		if not view:
			return
		if not desc {
			view.setText("")
			return
		}
		var textColor = desc.getTextColor()
		if textColor:
			view.setTextColor(textColor.toARGBInt32())
		var fontFamily = desc.getFontFamily()
		var fontResource = desc.getFontResource()
		var fontFile = desc.getFontFile()
		var fontStyle as int
		lang "kotlin" {{{
			fontStyle = android.graphics.Typeface.NORMAL
			if(desc.getFontIsItalic() && desc.getFontIsBold()) {
				fontStyle = android.graphics.Typeface.BOLD_ITALIC
			}
			else if(desc.getFontIsItalic()) {
				fontStyle = android.graphics.Typeface.ITALIC
			}
			if(desc.getFontIsBold()) {
				fontStyle = android.graphics.Typeface.BOLD
			}
		}}}
		if String.isEmpty(fontFamily) == false {
			lang "kotlin" {{{
				view.setTypeface(android.graphics.Typeface.create(fontFamily, fontStyle), fontStyle)
			}}}
		}
		else if String.isEmpty(fontResource) == false {
			lang "kotlin" {{{
				val ctx : android.content.Context = view.getContext()
				if(ctx != null) {
					view.setTypeface(android.graphics.Typeface.createFromAsset(ctx.getAssets(), fontResource), fontStyle)
				}
			}}}
		}
		else if fontFile {
			lang "kotlin" {{{
				view.setTypeface(android.graphics.Typeface.createFromFile(fontFile.getPath()), fontStyle)
			}}}
		}
		setBackgroundColor(desc.getBackgroundColor())
		view.setText(desc.getText())
		var msw as int
		var msh as int
		var hsz as double
		var fsDesc = desc.getFontSizeDescription()
		if not String.isEmpty(fsDesc) {
			var density as int
			var gc = context as GuiApplicationContext
			if gc {
				density = gc.getScreenDensity()
			}
			else {
				density = 96
			}
			hsz = Length.asPoints(fsDesc, density)
		}
		else {
			var fsRelative = desc.getFontSizeRelative()
			var fsAbsolute = desc.getFontSizeAbsolute()
			if fsRelative > 0.0 {
				hsz = getHeightFactor() * fsRelative
			}
			else {
				hsz = fsAbsolute * 18
			}
		}
		lang "kotlin" {{{
			view.setTextSize(hsz.toFloat())
			view.measure(android.view.View.MeasureSpec.UNSPECIFIED, android.view.View.MeasureSpec.UNSPECIFIED)
			msw = view.getMeasuredWidth()
			msh = view.getMeasuredHeight()
			view.layout(0, 0, msw, msh)
		}}}
		updateSizeAsPixels(msw, msh)
	}
}

class MyViewGroup is native<android.view.ViewGroup>
{
	ctor(context as native<android.content.Context>)
	{
		base(context)
	}

	func onLayout(changed as bool, left as int, top as int, right as int, bottom as int) override
	{
		lang "kotlin" {{{
			val count = getChildCount()
			for (i in 0..count) {
				val cc = getChildAt(i)
				if(cc != null) {
					val child : android.view.View = cc
					if(child.getVisibility() != GONE) {
						child.layout((child.getX()).toInt(), (child.getY()).toInt(), (child.getWidth()).toInt(), (child.getHeight()).toInt())
					}
				}
			}
		}}}
	}

	func onSizeChanged(w as int, h as int, oldw as int, oldh as int) override
	{
		base.onSizeChanged(w, h, oldw, oldh)
	}
}

class MySpriteLayer is ViewSprite is ContainerSprite is SpriteLayer
{
	pvar parentView as native<android.view.View>?
	var referenceWidth = 1.0
	var referenceHeight = 0.0
	var layerWidthFactor = 0.0
	var layerHeightFactor = 0.0
	var context as KotlinApplicationContext?
	var ppi = -1

	ctor(androidContext as KotlinApplicationContext, context as native<android.content.Context>)
	{
		this.context = androidContext
		view = new MyViewGroup(context)
	}

	func getLayerWidthFactor as double
	{
		if layerWidthFactor == 0.0 && referenceWidth > 0.0 && parentView {
			var rwidth = parentView.getWidth()
			layerWidthFactor = rwidth / referenceWidth
		}
		return layerWidthFactor
	}

	func getLayerHeightFactor as double
	{
		if layerHeightFactor == 0.0 {
			var rh = getReferenceHeight()
			if rh > 0.0 && parentView {
				var rheight = parentView.getHeight()
				layerHeightFactor = rheight / rh
			}
		}
		return layerHeightFactor
	}

	func addTextureSpriteForSize(texture as Texture, width as double, height as double) as TextureSprite
	{
		var v = new MyTextureSprite()
		v.setWidthFactor(getLayerWidthFactor())
		v.setHeightFactor(getLayerHeightFactor())
		v.setContext(view.getContext())
		v.setLayer(this)
		v.setTextureWithSize(texture, width, height)
		var vv = assert v.getView()
		var vg = view as native<android.view.ViewGroup>
		if vg:
			vg.addView(vv)
		return v
	}

	func addTextSprite(text as TextProperties) as TextSprite
	{
		assert view
		var v = new TextViewSprite(context)
		v.setWidthFactor(getLayerWidthFactor())
		v.setHeightFactor(getLayerHeightFactor())
		assert v.initialize(view.getContext(), text)
		var vv = assert v.getView()
		var vg = view as native<android.view.ViewGroup>
		if vg:
			vg.addView(vv)
		return v
	}

	func addContainerSprite(width as double, height as double) as ContainerSprite
	{
		if not view:
			return null
		var v = new this(context, view.getContext() as! native<android.content.Context>)
		v.setParentView(view)
		var vv = v.getView()
		if not vv:
			return null
		var vg = view as native<android.view.ViewGroup>
		if vg:
			vg.addView(vv)
		return v
	}

	func removeSprite(sprite as Sprite)
	{
		var vs = sprite as ViewSprite
		if not vs:
			return
		vs.removeFromContainer()
	}

	func removeAllSprites
	{
		var vg = getViewGroup()
		if vg:
			vg.removeAllViews()
	}

	func setReferenceWidth(rw as double)
	{
		referenceWidth = rw
	}

	func setReferenceHeight(rh as double)
	{
		referenceHeight = rh
	}

	func getReferenceWidth as double
	{
		return referenceWidth
	}

	func getReferenceHeight as double
	{
		if referenceHeight > 0.0:
			return referenceHeight
		return parentView.getHeight() * referenceWidth / parentView.getWidth()
	}

	func getPPI private as int
	{
		if ppi < 0 {
			var gc = context as GuiApplicationContext
			if gc:
				ppi = gc.getScreenDensity()
			if ppi < 0:
				ppi = 96
		}
		return ppi
	}

	func getHeightValue(spec as string) as double
	{
		return Length.asPoints(spec, getPPI()) / getLayerHeightFactor()
	}

	func getWidthValue(spec as string) as double
	{
		return Length.asPoints(spec, getPPI()) / getLayerWidthFactor()
	}
}

class ColorTexture is Texture
{
	pvar color as Color?

	func getPixelWidth as int:
		return 0

	func getPixelHeight as int:
		return 0
}

class ImageTexture is Texture
{
	pvar image as Image?

	func getPixelWidth as int
	{
		assert image
		return image.getPixelWidth()
	}

	func getPixelHeight as int
	{
		assert image
		return image.getPixelHeight()
	}
}

class MyLayeredContainerView is native<android.view.ViewGroup>
{
	pvar backend as parent?
	var pointerEvent = new PointerEvent()
	var keyEvent = new KeyEvent()

	ctor(context as native<android.content.Context>)
	{
		base(context)
	}

	func onLayout(changed as bool, left as int, top as int, right as int, bottom as int) override
	{
		lang "kotlin" {{{
			val count = getChildCount()
			for (i in 0..count) {
				val cc = getChildAt(i)
				if(cc != null) {
					val child : android.view.View = cc
					if(child.getVisibility() != GONE) {
						child.layout((child.getX()).toInt(), (child.getY()).toInt(), (child.getWidth()).toInt(), (child.getHeight()).toInt())
					}
				}
			}
		}}}
	}

	func onSizeChanged(w as int, h as int, oldw as int, oldh as int) override
	{
		base.onSizeChanged(w, h, oldw, oldh)
		backend.onSizeChanged(w, h)
	}

	func onKeyDown(keyCode as int, event as native<android.view.KeyEvent>) override as bool:
		return true

	func onKeyUp(keyCode as int, event as native<android.view.KeyEvent>) override as bool:
		return true

	func onTouchEvent(event as native<android.view.MotionEvent>) override as bool
	{
		lang "kotlin" {{{
			val viewWidth = getWidth()
			val viewHeight = getHeight()
			if(viewWidth < 1 || viewHeight < 1) {
				return(false)
			}
			val androidAction = event!!.getAction()
			var action : kotlin.Int = 0
			val hsz = event.getHistorySize()
			val pcount = event.getPointerCount()
			if(androidAction == android.view.MotionEvent.ACTION_DOWN || androidAction == android.view.MotionEvent.ACTION_POINTER_DOWN) {
				action = jk.app.ui.PointerEvent.DOWN
			}
			else if(androidAction == android.view.MotionEvent.ACTION_UP || androidAction == android.view.MotionEvent.ACTION_POINTER_UP) {
				action = jk.app.ui.PointerEvent.UP
			}
			else if(androidAction == android.view.MotionEvent.ACTION_MOVE) {
				action = jk.app.ui.PointerEvent.MOVE
			}
			else if(androidAction == android.view.MotionEvent.ACTION_CANCEL) {
				action = jk.app.ui.PointerEvent.CANCEL
			}
			else {
				return(false)
			}
			pointerEvent!!.isConsumed = false
			val pe = pointerEvent
			pe!!.action = action
			for (h in 0 until hsz) {
				for (p in 0 until pcount) {
					pe.pointerId = p
					pe.x = (event.getHistoricalX(p, h)).toDouble() / viewWidth.toDouble()
					pe.y = (event.getHistoricalY(p, h)).toDouble() / viewHeight.toDouble()
					backend!!.onPointerEvent(pe)
				}
			}
			for (p in 0 until pcount) {
				pe.pointerId = p
				pe.x = (event.getX(p)).toDouble() / viewWidth.toDouble()
				pe.y = (event.getY(p)).toDouble() / viewHeight.toDouble()
				backend!!.onPointerEvent(pe)
			}
		}}}
		return true
	}
}

func forScene(scene as Scene, context as KotlinApplicationContext) static as this
{
	var v = new this(context)
	if scene {
		v.setScene(scene)
		scene.setBackend(v)
	}
	return v
}

var androidView as native<android.view.ViewGroup>?
var isInitialized = false
var isStarted = false
var animator as native<android.animation.TimeAnimator>?
var gameTime as TimeValue?
var sceneManager as SceneManager?
var androidContext as KotlinApplicationContext?

ctor(context as KotlinApplicationContext)
{
	this.androidContext = context
	sceneManager = new SceneManager()
	sceneManager.setContext(context as GuiApplicationContext)
	sceneManager.setBackend(this)
	var vv = new MyLayeredContainerView(context.getAndroidActivityContext())
	vv.setBackend(this)
	androidView = vv
}

func setScene(scene as Scene?):
	sceneManager.pushScene(scene)

func onKeyEvent(event as KeyEvent)
{
	var scene = sceneManager.getCurrentScene()
	if scene:
		scene.onKeyEvent(event)
}

func onPointerEvent(event as PointerEvent)
{
	var scene = sceneManager.getCurrentScene()
	if scene:
		scene.onPointerEvent(event)
}

func getAndroidView as native<android.view.ViewGroup>
{
	return androidView
}

func onSizeChanged(w as int, h as int)
{
	if isInitialized == false && w > 0 && h > 0 {
		isInitialized = true
		sceneManager.initialize()
		if isStarted {
			doStart()
		}
	}
}

func createSpriteLayer override as SpriteLayer
{
	assert androidView
	var v = new MySpriteLayer(androidContext, androidView.getContext() as! native<android.content.Context>)
	v.setParentView(androidView)
	var vv = assert v.getView()
	androidView.addView(vv)
	vv.layout(0, 0, androidView.getWidth(), androidView.getHeight())
	return v
}

func createImageFromResource(name as string) override as Image
{
	assert androidView
	return ImageForAndroidKotlin.forResource(name, androidView.getContext())
}

func createTextureForImage(image as Image) override as Texture:
	return new ImageTexture().setImage(image)

func createTextureForColor(color as Color) override as Texture:
	return new ColorTexture().setColor(color)

func deleteSpriteLayer(layer as SpriteLayer) override
{
	assert androidView
	var ll = assert layer as MySpriteLayer
	androidView.removeView(ll.getView())
}

func deleteTexture(texture as Texture) override
{
}

class MyAnimatorListener implements native<android.animation.TimeAnimator.TimeListener>
{
	pvar listener as parent?

	func onTimeUpdate(animation as native<android.animation.TimeAnimator>, totalTime as long, deltaTime as long) override
	{
		listener.onTick(totalTime, deltaTime)
	}
}

func onTick(totalTime as long, deltaTime as long)
{
	if not gameTime:
		gameTime = new TimeValue()
	gameTime.setMilliSeconds(totalTime)
	// FIXME: Too slow
	var scene = sceneManager.getCurrentScene()
	if scene:
		scene.tick(gameTime, deltaTime / 1000.0)
}

func doStart private
{
	sceneManager.start()
	if not animator {
		animator = new native<android.animation.TimeAnimator>()
		animator.setTimeListener(new MyAnimatorListener().setListener(this))
	}
	if animator.isStarted() == false {
		animator.start()
	}
	else if animator.isPaused() == true {
		try {
			animator.resume()
		}
		catch e as native<java.lang.NoSuchMethodError> {
			animator.start()
		}
	}
}

func onStart
{
	isStarted = true
	if isInitialized:
		doStart()
}

func onStop
{
	if animator {
		try {
			animator.pause()
		}
		catch e as native<java.lang.NoSuchMethodError> {
			animator.end()
		}
	}
	sceneManager.stop()
	isStarted = false
}

func onDestroy
{
	if animator {
		animator.end()
		animator = null
	}
	sceneManager.cleanup()
	isInitialized = false
}
