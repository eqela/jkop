
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2024 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class is StringObject:

func forKeyValue(key as string, value as string) static as this
{
	var v = new this()
	v.setKey(key)
	v.setValue(value)
	return v
}

func forDeleteKey(key as string) static as this
{
	var v = new this()
	v.setKey(key)
	v.setValue("deleted")
	v.setExpires("Thu, 01 Jan 1970 00:00:00 GMT")
	return v
}

ctor:
	nop

ctor(key as string, value as string)
{
	this.key = key
	this.value = value
}

pvar key as string?
pvar value as string?
pvar maxAge = -1
pvar expires as string?
pvar path as string?
pvar domain as string?
pvar secure as bool = false
pvar httpOnly as bool = false
pvar sameSite as string?

func toString as string
{
	var sb = new StringBuilder()
	if key:
		sb.appendString(key)
	sb.appendCharacter('=')
	if value:
		sb.appendString(value)
	if maxAge >= 0 {
		sb.appendString("; Max-Age=")
		sb.appendString(String.forInteger(maxAge))
	}
	if String.isNotEmpty(expires) {
		sb.appendString("; Expires=")
		sb.appendString(expires)
	}
	if String.isNotEmpty(path) {
		sb.appendString("; Path=")
		sb.appendString(path)
	}
	if String.isNotEmpty(domain) {
		sb.appendString("; Domain=")
		sb.appendString(domain)
	}
	if secure:
		sb.appendString("; Secure")
	if httpOnly:
		sb.appendString("; HttpOnly")
	if String.isNotEmpty(sameSite) {
		sb.appendString("; SameSite=")
		sb.appendString(sameSite)
	}
	return sb.toString()
}
