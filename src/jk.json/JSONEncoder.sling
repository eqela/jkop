
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2018 Job and Esther Technologies Oy
 * Copyright (c) 2018-2019 Eqela Oy
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.base64

class:

prop niceFormatting = true
prop stringFormattedBoolean = false
prop stringFormattedNumber = false
prop newJSONFormat = true
var isNewLine = true
var mysb as StringBuilder

func print(line as string, indent as int, startline as bool, endline as bool, sb as StringBuilder) private
{
	if startline && isNewLine == false {
		if niceFormatting:
			sb.appendCharacter('\n')
		isNewLine = true
	}
	if isNewLine && niceFormatting {
		for(var n = 0; n<indent; n++):
			sb.appendCharacter('\t')
	}
	sb.appendString(line)
	if endline {
		if niceFormatting:
			sb.appendCharacter('\n')
		isNewLine = true
	}
	else {
		isNewLine = false
	}
}

macro PRINT_COLON
{
	if niceFormatting {
		sb.appendString(" : ")
	}
	else {
		sb.appendCharacter(':')
	}
}

func encodeArray(cc as array<object>, indent as int, sb as StringBuilder) private
{
	print("[", indent, false, true, sb)
	var first = true
	foreach o in cc {
		if first == false:
			print(",", indent, false, true, sb)
		encodeObject(o, indent+1, sb)
		first = false
	}
	print("]", indent, true, false, sb)
}

func encodeDynamicVector(cc as DynamicVector, indent as int, sb as StringBuilder) private
{
	print("[", indent, false, true, sb)
	var first = true
	foreach o in cc.toVector() {
		if first == false:
			print(",", indent, false, true, sb)
		encodeObject(o, indent+1, sb)
		first = false
	}
	print("]", indent, true, false, sb)
}

func encodeVector(cc as vector<object>, indent as int, sb as StringBuilder) private
{
	print("[", indent, false, true, sb)
	var first = true
	foreach o in cc {
		if first == false:
			print(",", indent, false, true, sb)
		encodeObject(o, indent+1, sb)
		first = false
	}
	print("]", indent, true, false, sb)
}

func encodeMap(map as map, indent as int, sb as StringBuilder) private
{
	print("{", indent, false, true, sb)
	var first = true
	var it as Iterator<object> #force = Map.iterateKeys(map)
	while it {
		var keyo = it.next()
		if not keyo:
			break
		var key = String.asString(keyo)
		if not key:
			continue
		if not first:
			print(",", indent, false, true, sb)
		encodeString(key, indent+1, sb)
		PRINT_COLON()
		encodeObject(map[keyo], indent+1, sb)
		first = false
	}
	print("}", indent, true, false, sb)
}

func encodeDynamicMap(map as DynamicMap, indent as int, sb as StringBuilder) private
{
	print("{", indent, false, true, sb)
	var first = true
	var it = map.iterateKeys()
	while it {
		var key = it.next()
		if not key:
			break
		if first == false:
			print(",", indent, false, true, sb)
		encodeString(key, indent+1, sb)
		PRINT_COLON()
		encodeObject(map.get(key), indent+1, sb)
		first = false
	}
	print("}", indent, true, false, sb)
}

func encodeKeyValueList(list as KeyValueListForStrings, indent as int, sb as StringBuilder) private
{
	print("{", indent, false, true, sb)
	var first = true
	var it as Iterator<KeyValuePair<string,string>> #force = list.iterate()
	while it {
		var pair = it.next()
		if pair == null:
			break
		if first == false:
			print(",", indent, false, true, sb)
		encodeString(pair.key, indent+1, sb)
		PRINT_COLON()
		encodeString(pair.value, indent+1, sb)
		first = false
	}
	print("}", indent, true, false, sb)
}

func encodeString(s as string, indent as int, sb as StringBuilder) private
{
	if mysb == null {
		mysb = new StringBuilder()
	}
	else {
		mysb.clear()
	}
	mysb.appendCharacter('"')
	var it = String.iterate(s)
	loop {
		var c = it.getNextChar()
		if Character.isEOF(c):
			break
		if newJSONFormat {
			if c == '\t' {
				mysb.appendString("\\t")
				continue
			}
			if c == '\n' {
				mysb.appendString("\\n")
				continue
			}
			if c == '\r' {
				mysb.appendString("\\r")
				continue
			}
			if c == '\b' {
				mysb.appendString("\\b")
				continue
			}
			if c == '\f' {
				mysb.appendString("\\f")
				continue
			}
		}
		if c == '"' {
			mysb.appendCharacter('\\')
		}
		else if c == '\\' {
			mysb.appendCharacter('\\')
		}
		mysb.appendCharacter(c)
	}
	mysb.appendCharacter('"')
	print(mysb.toString(), indent, false, false, sb)
}

func encodeBoolean(b as bool, indent as int, sb as StringBuilder) private
{
	if mysb == null {
		mysb = new StringBuilder()
	}
	else {
		mysb.clear()
	}
	if stringFormattedBoolean {
		mysb.appendCharacter('"')
		mysb.appendString(String.forBoolean(b))
		mysb.appendCharacter('"')
	}
	else {
		mysb.appendString(String.forBoolean(b))
	}
	print(mysb.toString(), indent, false, false, sb)
}

func encodeNumber(o as object, indent as int, sb as StringBuilder) private
{
	if mysb == null {
		mysb = new StringBuilder()
	}
	else {
		mysb.clear()
	}
	if stringFormattedNumber {
		mysb.appendCharacter('"')
		mysb.appendString(String.asString(o))
		mysb.appendCharacter('"')
	}
	else {
		if o is IntegerObject {
			mysb.appendInteger(Integer.asInteger(o))
		}
		else if o as LongIntegerObject {
			mysb.appendLong(LongInteger.asLong(o))
		}
		else if o as DoubleObject {
			mysb.appendDouble(Double.asDouble(o))
		}
		else {
			mysb.appendString("")
		}
	}
	print(mysb.toString(), indent, false, false, sb)
}

func encodeObject(o as object, indent as int, sb as StringBuilder) virtual
{
	if not o {
		encodeString("", indent, sb)
	}
	else if o is JSONObject {
		encodeObject((o as! JSONObject).toJsonObject(), indent, sb)
	}
	else if o is array {
		encodeArray(o as! array, indent, sb)
	}
	else if o is vector {
		encodeVector(o as! vector, indent, sb)
	}
	else if o is map {
		encodeMap(o as! map, indent, sb)
	}
	else if o is buffer {
		encodeString(Base64Encoder.encode(o as! buffer), indent, sb)
	}
	else if o is string {
		encodeString(o as! string, indent, sb)
	}
	else if o is DynamicMap {
		encodeDynamicMap(o as! DynamicMap, indent, sb)
	}
	else if o is DynamicVector {
		encodeDynamicVector(o as! DynamicVector, indent, sb)
	}
	else if o is KeyValueListForStrings {
		encodeKeyValueList(o as! KeyValueListForStrings, indent, sb)
	}
	else if o is StringObject {
		encodeString((o as! StringObject).toString(), indent, sb)
	}
	else if o is BufferObject {
		encodeString(Base64Encoder.encode((o as! BufferObject).toBuffer()), indent, sb)
	}
	else if o is ArrayObject<object> {
		var aa as array #force = (o as! ArrayObject<object>).toArray()
		encodeArray(aa, indent, sb)
	}
	else if o is VectorObject<object> {
		var vv as vector #force = (o as! VectorObject<object>).toVector()
		encodeVector(vv, indent, sb)
	}
	else if o is IntegerObject || o is LongIntegerObject || o is DoubleObject{
		encodeNumber(o, indent, sb)
	}
	else if o is CharacterObject {
		encodeString(String.asString(o), indent, sb)
	}
	else if o is BooleanObject {
		encodeBoolean(Boolean.asBoolean(o), indent, sb)
	}
	else {
		encodeString("", indent, sb)
	}
}

func encode(o as object, niceFormatting as bool = true, stringFormattedNumber = false) static as string #deprecated
{
	var v = new this()
	v.setStringFormattedBoolean(true)
	v.setStringFormattedNumber(stringFormattedNumber)
	v.setNiceFormatting(niceFormatting)
	return v.execute(o)
}

func execute(o as object) as string
{
	var sb = new StringBuilder()
	encodeObject(o, 0, sb)
	return sb.toString()
}

func encodeStringToBuilder(s as string, sb as StringBuilder) static
{
	sb.appendCharacter('"')
	if String.getIndexOfCharacter(s, '"') >= 0 || String.getIndexOfCharacter(s, '\\') >= 0 {
		var it = String.iterate(s)
		loop {
			var c = it.getNextChar()
			if c < 1:
				break
			if c == '"' {
				sb.appendCharacter('\\')
			}
			else if c == '\\' {
				sb.appendCharacter('\\')
			}
			sb.appendCharacter(c)
		}
	}
	else {
		sb.appendString(s)
	}
	sb.appendCharacter('"')
}
