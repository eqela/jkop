
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2018 Job and Esther Technologies Oy
 * Copyright (c) 2018-2020 Eqela Oy
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class:

**
** Creates an instance of this class with the
** specified table name.
**

func forName(name as string) static as this:
	return new this().setName(name)

func forDetails(name as string, columns as array<SQLTableColumnInfo> = null, indexes as array<string> = null, uniqueIndexes as array<string> = null) static as this
{
	var v = new this()
	v.setName(name)
	foreach column in columns:
		v.addColumn(column)
	foreach index in indexes:
		v.addIndex(index)
	foreach uniqueIndex in uniqueIndexes:
		v.addUniqueIndex(uniqueIndex)
	return v
}

pvar name as string
pvar columns as vector<SQLTableColumnInfo>
pvar indexes as vector<SQLTableColumnIndexInfo>

**
** Adds the supplied instance of SQLTableColumnInfo class
** which defines the column info to the list of columns of
** this class instance and returns itself.
**

func addColumn(info as SQLTableColumnInfo) as this
{
	if info == null:
		return this
	if columns == null:
		columns = new vector<SQLTableColumnInfo>
	columns += info
	return this
}

**
** Creates an instance of SQLTableColumnInfo class for
** column of type integer with the supplied column name
** and adds it to the list of columns of this class
** instance and returns itself.
**

func addIntegerColumn(name as string) as this:
	return addColumn(SQLTableColumnInfo.forInteger(name))

**
** Creates an instance of SQLTableColumnInfo class for
** column of type long integer with the supplied column
** name and adds it to the list of columns of this class
** instance and returns itself.
**

func addLongColumn(name as string) as this:
	return addColumn(SQLTableColumnInfo.forLongInteger(name))

**
** Creates an instance of SQLTableColumnInfo class for
** column of type string with the supplied column name
** and adds it to the list of columns of this class
** instance and returns itself.
**

func addStringColumn(name as string) as this:
	return addColumn(SQLTableColumnInfo.forString(name))

**
** Creates an instance of SQLTableColumnInfo class for
** key column of type string with the supplied column
** name and adds it to the list of columns of this class
** instance and returns itself.
**

func addStringKeyColumn(name as string) as this:
	return addColumn(SQLTableColumnInfo.forStringKey(name))

**
** Creates an instance of SQLTableColumnInfo class for
** column of type text with the supplied column name
** and adds it to the list of columns of this class
** instance and returns itself.
**

func addTextColumn(name as string) as this:
	return addColumn(SQLTableColumnInfo.forText(name))

**
** Creates an instance of SQLTableColumnInfo class for
** key column of type integer with the supplied column
** name and adds it to the list of columns of this class
** instance and returns itself.
**

func addIntegerKeyColumn(name as string) as this:
	return addColumn(SQLTableColumnInfo.forIntegerKey(name))

**
** Creates an instance of SQLTableColumnInfo class for
** key column of type long integer with the supplied
** column name and adds it to the list of columns of
** this class instance and returns itself.
**

func addLongKeyColumn(name as string) as this:
	return addColumn(SQLTableColumnInfo.forLongIntegerKey(name))

**
** Creates an instance of SQLTableColumnInfo class for
** column of type double with the supplied column name
** and adds it to the list of columns of this class
** instance and returns itself.
**

func addDoubleColumn(name as string) as this:
	return addColumn(SQLTableColumnInfo.forDouble(name))

**
** Creates an instance of SQLTableColumnInfo class for
** column of type blob with the supplied column name
** and adds it to the list of columns of this class
** instance and returns itself.
**

func addBlobColumn(name as string) as this:
	return addColumn(SQLTableColumnInfo.forBlob(name))

**
** Creates an instance of SQLTableColumnIndexInfo class
** from the supplied column name and adds it to the list
** of indexes of this class and instance and returns itself.
** The index constraint is not a unique index constraint.
**

func addIndex(column as string) as this
{
	if String.isEmpty(column) == false {
		if indexes == null:
			indexes = new vector<SQLTableColumnIndexInfo>
		indexes += new SQLTableColumnIndexInfo().setColumn(column).setUnique(false)
	}
	return this
}

**
** Creates an instance of SQLTableColumnIndexInfo class
** from the supplied column name and adds it to the list
** of indexes of this class and instance and returns itself.
** The index constraint is a unique index constraint.
**

func addUniqueIndex(column as string) as this
{
	if String.isEmpty(column) == false {
		if indexes == null:
			indexes = new vector<SQLTableColumnIndexInfo>
		indexes += new SQLTableColumnIndexInfo().setColumn(column).setUnique(true)
	}
	return this
}
