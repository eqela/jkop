
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2023 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.lang
import jk.json

class is Iterator<DynamicMap> abstract:

meta
{
	description [[
		A generic iterator for SQL result sets.
	]]
}

func next abstract as DynamicMap
func hasNext abstract as bool
func nextValues(values as vector<object>) abstract as bool
func step abstract as bool
func getColumnCount abstract as int
func getColumnName(n as int) abstract as string
func getColumnNames abstract as vector<string>
func getColumnObject(n as int) abstract as object
func getColumnInt(n as int) abstract as int
func getColumnLong(n as int) abstract as long
func getColumnDouble(n as int) abstract as double
func getColumnBuffer(n as int) abstract as buffer
func close abstract

func toVectorOfMaps as DynamicVector
{
	meta {
		description [[
			Converts the data into an instance of DynamicVector
			of records. Each record is an instance of DynamicMap.
		]]
	}
	var v = new DynamicVector()
	loop {
		var o = next()
		if o == null:
			break
		v.appendObject(o)
	}
	return v
}

func toVectorList as vector
{
	meta {
		description [[
			Converts the data into a vector of records.
			Each record is a vector of column objects.
			The first record is a vector of column names.
		]]
	}
	var data = new vector
	var cc = getColumnCount()
	var cols = new vector
	Vector.setCapacity(cols, cc)
	for(var n = 0 ; n < cc ; n++):
		cols += getColumnName(n)
	data += cols
	loop {
		if not step():
			break
		var record = new vector
		Vector.setCapacity(record, cc)
		for(var n = 0 ; n < cc ; n++) {
			var co = getColumnObject(n)
			if co == null:
				co = ""
			record += co
		}
		data += record
	}
	return data
}

func headerJSON(sb as StringBuilder)
{
	meta {
		description [[
			Encodes the column name(s) into a vector as a string and
			appends it to the supplied instance of StringBuilder.
		]]
	}
	sb.appendCharacter('[')
	var cc = getColumnCount()
	for(var n = 0 ; n < cc ; n++) {
		if n > 0:
			sb.appendCharacter(',')
		JSONEncoder.encodeToBuilder(getColumnName(n), sb)
	}
	sb.appendCharacter(']')
}

func nextJSON(sb as StringBuilder) as bool
{
	meta {
		description [[
			Encodes the next record into a vector of objects as a string and
			appends it to the supplied instance of StringBuilder.
			Returns false if there is no record left to encode.
		]]
	}
	if not step():
		return false
	var cc = getColumnCount()
	sb.appendString(",[")
	for(var n = 0 ; n < cc ; n++) {
		if n > 0:
			sb.appendCharacter(',')
		JSONEncoder.encodeToBuilder(getColumnObject(n), sb)
	}
	sb.appendCharacter(']')
	return true
}

func toVectorListJSON as string
{
	meta {
		description [[
			Encodes the data into a string with a vector list JSON format.
		]]
	}
	var sb = new StringBuilder()
	sb.appendCharacter('[')
	headerJSON(sb)
	loop {
		if not nextJSON(sb):
			break
	}
	sb.appendCharacter(']')
	return sb.toString()
}
