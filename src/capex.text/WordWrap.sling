
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2018 Job and Esther Technologies Oy
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class:

func wrapToLines(text as string, charactersPerLine as int) static as vector<string>
{
	if(text == null) {
		return null
	}
	var v = new vector<string>
	var it = String.iterate(text)
	var lineBuilder as StringBuilder
	var wordBuilder as StringBuilder
	while(it != null) {
		var c = it.getNextChar()
		if(c == ' ' || c == '\t' || c == '\n' || c < 1) {
			if(wordBuilder != null) {
				var word = wordBuilder.toString()
				wordBuilder = null
				if(lineBuilder == null) {
					lineBuilder = new StringBuilder()
				}
				var cc = lineBuilder.count()
				if(cc > 0) {
					cc ++
				}
				cc += String.getLength(word)
				if(cc > charactersPerLine) {
					v += lineBuilder.toString()
					lineBuilder = new StringBuilder()
				}
				if(lineBuilder.count() > 0) {
					lineBuilder.append(' ')
				}
				lineBuilder.append(word)
			}
			if(c < 1) {
				break
			}
			continue
		}
		if(wordBuilder == null) {
			wordBuilder = new StringBuilder()
		}
		wordBuilder.append(c)
	}
	if(lineBuilder != null) {
		v += lineBuilder.toString()
	}
	return v
}
