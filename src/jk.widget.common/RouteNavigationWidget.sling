
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2022 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class is NavigationFrameWidget is RouterListener #widget:

class Route public
{
	pvar name as string
	pvar handler as function<Widget>
}

var routes private as map<string, function<Widget>>

ctor(ctx as GuiApplicationContext)
{
	base(ctx)
	routes = new map<string, function<Widget>>
}

func initializeWidget override
{
	base.initializeWidget()
	Router.goToDefault()
}

func onRouteChanged(route as string)
{
	var w = assert getWidget(route)
	Widget.removeChildrenOf(contentArea)
	contentArea.addWidget(w)
	if w is DisplayAwareWidget:
		(w as! DisplayAwareWidget).onWidgetDisplayed()
}

func initializeRoutes(rs as array) as bool
{
	assert rs
	foreach m as Route in rs {
		var route = m.getName()
		var widget = m.getHandler()
		addRoute(route, widget)
	}
	return true
}

func addRoute(route as string, callback as function<Widget>)
{
	if not routes:
		routes = new map<string, function<Widget>>
	routes[route] = callback
}

func getWidget(route as string) as Widget
{
	var w = Map.getValue(routes, route)
	if not w:
		w = Map.getValue(routes, "*")
	assert w
	return w()
}

