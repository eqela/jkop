
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2022 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.log
import jk.jwt
import jk.test
import jk.time
import jk.slon
import jk.random
import jk.base64

class is UnitTest:

func generateRandom64ByteKey as string
{
	var v = malloc 64
	for(var n=0; n<64; n++):
		v[n] = Random.getSharedInstance().nextIntegerWithMaximum(256) as uint8
	return Base64Encoder.encode(v)
}

func testHs256
{
	var key = generateRandom64ByteKey()
	println "[HS256] Using key: " .. key
	var jwt = new JWT()
	jwt.setAudience("http://test.audience")
	jwt.setExpirationTime(SystemClock.asUTCSeconds() + 60)
	var odata = SLONEncoder.toString(jwt.getClaims())
	ASSERT(String.isNotEmpty(odata))
	println "[HS256] Original claims: " .. odata
	var token = jwt.generateTokenWithHS256(String.toUTF8Buffer(key))
	ASSERT(String.isNotEmpty(token))
	println "[HS256] Token: " .. token
	var njwt = JWT.decode(token, new map { hs256 String.toUTF8Buffer(key) })
	ASSERT(njwt)
	var ndata = SLONEncoder.toString(njwt.getClaims())
	ASSERT(String.isNotEmpty(ndata))
	println "[HS256] Decoded claims: " .. ndata
	var wrongKey = generateRandom64ByteKey()
	ASSERT(String.equals(key, wrongKey) == false)
	println "[HS256] Using wrong key: " .. wrongKey
	var fjwt = JWT.decode(token, new map { hs256 String.toUTF8Buffer(wrongKey) })
	ASSERT(fjwt == null)
}

func execute override as bool
{
	START_TESTS()
	TEST("Test HS256 functionality", testHs256())
	END_TESTS()
}
