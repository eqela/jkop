
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2018 Job and Esther Technologies Oy
 * Copyright (c) 2018-2019 Eqela Oy
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class is StringObject:

func forString(initial as string) static as this
{
	var v = new this()
	v.appendString(initial)
	return v
}

func dup(initial as this) static as this
{
	var v = new this()
	if initial:
		v.appendString(initial.toString())
	return v
}

IFDEF "target_objc" {
	var data as !"NSMutableString*"
}
ELSE IFDEF "target_cs" {
	var builder as !"System.Text.StringBuilder"
}
ELSE IFDEF "target_java" {
	var builder as !"java.lang.StringBuilder"
}
ELSE IFDEF "target_js" {
	var builder as !"String" #used
}
ELSE IFDEF "target_stdlua" {
	var builder as string
}
ELSE IFDEF "target_kotlin" {
	var builder as !"kotlin.text.StringBuilder" #used
}
ELSE IFDEF "target_swift" {
	var builder as !"NSMutableString" #used
}
ELSE {
	var buffer as buffer
	var position = 0

	func resizeTo(size as long)
	{
		var osz = sizeof buffer
		if osz < size {
			while osz < size:
				osz += 1024
			buffer = Buffer.resize(buffer, osz)
		}
	}
}

ctor:
	initialize()

func initialize
{
	IFDEF "target_objc" {
		lang "objc" {{{
			data = [[NSMutableString alloc] init];
		}}}
	}
	ELSE IFDEF "target_cs" {
		lang "cs" {{{
			builder = new System.Text.StringBuilder();
		}}}
	}
	ELSE IFDEF "target_java" {
		lang "java" {{{
			builder = new java.lang.StringBuilder();
		}}}
	}
	ELSE IFDEF "target_js" {
		lang "js" {{{
			this.builder = new String();
		}}}
	}
	ELSE IFDEF "target_stdlua" {
		this.builder = ""
	}
	ELSE IFDEF "target_kotlin" {
		lang "kotlin" {{{
			builder = kotlin.text.StringBuilder()
		}}}
	}
	ELSE IFDEF "target_swift" {
		lang "swift" {{{
			builder = ""
		}}}
	}
	ELSE {
		buffer = malloc 1024
	}
}

func clear
{
	IFDEF "target_objc" {
		initialize()
	}
	ELSE IFDEF "target_cs" {
		lang "cs" {{{
			builder.Clear();
		}}}
	}
	ELSE IFDEF "target_java" {
		lang "java" {{{
			builder.setLength(0);
		}}}
	}
	ELSE IFDEF "target_js" {
		initialize()
	}
	ELSE IFDEF "target_stdlua" {
		this.builder = ""
	}
	ELSE IFDEF "target_kotlin" {
		lang "kotlin" {{{
			builder!!.clear()
		}}}
	}
	ELSE IFDEF "target_swift" {
		builder = ""
	}
	ELSE {
		buffer = malloc 1024
		position = 0
	}
}

func count as int
{
	IFDEF "target_objc" {
		return lang "objc" int {{{ [data length] }}}
	}
	ELSE IFDEF "target_cs" {
		return lang "cs" int {{{ builder.Length }}}
	}
	ELSE IFDEF "target_java" {
		return lang "java" int {{{ builder.length() }}}
	}
	ELSE IFDEF "target_js" {
		return lang "js" int {{{ this.builder.length }}}
	}
	ELSE IFDEF "target_stdlua" {
		return lang "lua" int {{{ string.len(self.builder) }}}
	}
	ELSE IFDEF "target_kotlin" {
		return lang "kotlin" int {{{ builder!!.length }}}
	}
	ELSE IFDEF "target_swift" {
		return lang "swift" int {{{ (builder! as NSString).length }}}
	}
	ELSE {
		return position
	}
}

func appendLong(c as long) as this:
	return appendString(String.forLongInteger(c))

func appendInteger(c as int) as this:
	return appendString(String.forInteger(c))

func appendDouble(c as double) as this:
	return appendString(String.forDouble(c))

func appendFloat(c as float) as this:
	return appendString(String.forFloat(c))

func appendCharacter(c as char) as this
{
	if c == 0 || c == -1:
		return this
	IFDEF "target_objc" {
		return appendString(String.forCharacter(c))
	}
	ELSE IFDEF "target_cs" {
		return appendString(String.forCharacter(c))
	}
	ELSE IFDEF "target_java" {
		return appendString(String.forCharacter(c))
	}
	ELSE IFDEF "target_js" {
		return appendString(String.forCharacter(c))
	}
	ELSE IFDEF "target_stdlua" {
		return appendString(String.forCharacter(c))
	}
	ELSE IFDEF "target_kotlin" {
		return appendString(String.forCharacter(c))
	}
	ELSE IFDEF "target_swift" {
		return appendString(String.forCharacter(c))
	}
	ELSE {
		if c <= 0x7F {
			resizeTo(position + 1)
			buffer[position] = c as uint8
			position++
		}
		else {
			appendString(String.forCharacter(c))
		}
		return this
	}
}

func appendString(str as string #used) as this
{
	if str == null:
		return this
	IFDEF "target_objc" {
		lang "objc" {{{
			[data appendString:str];
		}}}
		return this
	}
	ELSE IFDEF "target_cs" {
		lang "cs" {{{
			builder.Append(str);
		}}}
		return this
	}
	ELSE IFDEF "target_java" {
		lang "java" {{{
			builder.append(str);
		}}}
		return this
	}
	ELSE IFDEF "target_js" {
		lang "js" {{{
			this.builder += str;
		}}}
		return this
	}
	ELSE IFDEF "target_stdlua" {
		builder = builder .. str
	}
	ELSE IFDEF "target_kotlin" {
		lang "kotlin" {{{
			builder!!.append(str)
		}}}
		return this
	}
	ELSE IFDEF "target_swift" {
		lang "swift" {{{
			builder.append(str!)
		}}}
		return this
	}
	ELSE {
		var nbuf = String.toUTF8Buffer(str)
		if nbuf {
			var nsz = sizeof nbuf
			resizeTo(position + nsz)
			Buffer.copyFrom(buffer, nbuf, 0, position, nsz)
			position += nsz
		}
		return this
	}
}

func insertInteger(index as int, c as int) as this:
	return insertString(index, String.forInteger(c))

func insertCharacter(index as int, c as char) as this
{
	if c == 0:
		return this
	return insertString(index, String.forCharacter(c))
}

func insertDouble(index as int, c as double) as this:
	return insertString(index, String.forDouble(c))

func insertFloat(index as int, c as float) as this:
	return insertString(index, String.forFloat(c))

func insertString(index as int, str as string) as this
{
	if str == null:
		return this
	IFDEF "target_objc" {
		lang "objc" {{{
			[data insertString:str atIndex:index];
		}}}
	}
	ELSE IFDEF "target_cs" {
		lang "cs" {{{
			builder.Insert(index, str);
		}}}
	}
	ELSE IFDEF "target_java" {
		lang "java" {{{
			builder.insert(index, str);
		}}}
	}
	ELSE IFDEF "target_js" {
		var fp #used
		var lp #used
		lang "js" {{{
			fp = this.builder.substring(0, index);
			lp = this.builder.substring(index, this.builder.length);
			this.builder = fp + str + lp;
		}}}
	}
	ELSE IFDEF "target_kotlin" {
		lang "kotlin" {{{
			builder!!.insert(index, str)
		}}}
	}
	ELSE IFDEF "target_swift" {
		lang "swift" {{{
			builder.insert(str!, at: index)
		}}}
	}
	ELSE {
		var nbuf = String.toUTF8Buffer(str)
		if nbuf {
			var nsz = sizeof nbuf
			resizeTo(position + nsz)
			Buffer.copyFrom(buffer, buffer, index, index+nsz, position-index)
			Buffer.copyFrom(buffer, nbuf, 0, index, nsz)
			position += nsz
		}
	}
	return this
}

func remove(index as int, length as int) as this
{
	if length < 1:
		return this
	IFDEF "target_cs" {
		lang "cs" {{{
			if(index >= 0 && index < builder.Length) {
				builder.Remove(index, length);
			}
		}}}
	}
	ELSE IFDEF "target_js" {
		var fp #used
		var lp #used
		lang "js" {{{
			fp = this.builder.substring(0, index);
			lp = this.builder.substring(index+length, this.builder.length);
			this.builder = fp + lp;
		}}}
	}
	ELSE IFDEF "target_java" {
		lang "java" {{{
			builder.delete(index, index+length);
		}}}
	}
	ELSE IFDEF "target_objc" {
		lang "objc" {{{
			[data deleteCharactersInRange:NSMakeRange(index,length)];
		}}}
	}
	ELSE IFDEF "target_kotlin" {
		var ss as string
		lang "kotlin" {{{
			var cs = builder!!.removeRange(index, index+length)
			ss = cs.toString()
		}}}
		clear()
		append(ss)
	}
	ELSE IFDEF "target_swift" {
		lang "swift" {{{
			builder.deleteCharacters(in: NSMakeRange(index, length))
		}}}
	}
	ELSE {
		if index < position {
			var ll = length
			var limit = position - index
			if ll > limit:
				ll = limit
			Buffer.copyFrom(buffer, buffer, index+ll, index, position-index-ll)
			position -= ll
		}
	}
	return this
}

func removeLastCharacter as this
{
	var c = count()
	if c > 0:
		remove(c-1, 1)
	return this
}

func toString as string
{
	IFDEF "target_objc" {
		return lang "objc" string {{{ [NSString stringWithString:data] }}}
	}
	ELSE IFDEF "target_cs" {
		return lang "cs" string {{{ builder.ToString() }}}
	}
	ELSE IFDEF "target_java" {
		return lang "java" string {{{ builder.toString() }}}
	}
	ELSE IFDEF "target_js" {
		return lang "js" string {{{ this.builder.toString() }}}
	}
	ELSE IFDEF "target_stdlua" {
		return builder
	}
	ELSE IFDEF "target_kotlin" {
		return lang "kotlin" string {{{ builder!!.toString() }}}
	}
	ELSE IFDEF "target_swift" {
		return lang "swift" string {{{ builder as Swift.String? }}}
	}
	ELSE {
		if not buffer || position < 1:
			return ""
		var bb = buffer
		if position < sizeof bb:
			bb = Buffer.getSubBuffer(bb, 0, position)
		return String.forUTF8Buffer(bb)
	}
}
