
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2018 Job and Esther Technologies Oy
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class is TCPSocket:

var socket as !"java.net.Socket"
var serverSocket as !"java.net.ServerSocket"
var blocking private = true

func getJavaClientSocket as !"java.net.Socket":
	return socket

func getJavaServerSocket as !"java.net.ServerSocket":
	return serverSocket

func getRemoteAddress override as string
{
	var a as !"java.net.InetAddress"
	assert socket
	lang "kotlin" {{{
		a = socket?.getInetAddress()
		if(a != null) {
			return (a.getHostAddress()).toString()
		}
	}}}
	return null
}

func getRemotePort override as int
{
	if socket == null:
		return(0)
	lang "kotlin" {{{
		return (socket!!.getPort()).toInt()
	}}}
	return 0
}

func getLocalAddress override as string
{
	var a as !"java.net.InetAddress"
	lang "kotlin" {{{
		if(socket != null) {
			a = socket?.getLocalAddress()
		}
		if(serverSocket != null) {
			a = serverSocket?.getInetAddress()
		}
		if(a != null) {
			return (a.getHostAddress()).toString()
		}
	}}}
	return null
}

func getLocalPort override as int
{
	lang "kotlin" {{{
		if(socket != null) {
			return (socket!!.getLocalPort()).toInt()
		}
		if(serverSocket != null) {
			return (serverSocket!!.getLocalPort()).toInt()
		}
	}}}
	return -1
}

func connect(address as string, port as int) override as bool
{
	var v = true
	lang "kotlin" {{{
		try {
			var serverAddress = java.net.InetAddress.getByName(address)
			socket = java.net.Socket(serverAddress, port)
			socket?.setKeepAlive(true)
		}
		catch (e : kotlin.Exception){
			e.printStackTrace()
			v = false
		}
	}}}
	return v
}

func listen(port as int) override as bool
{
	var v = true
	lang "kotlin" {{{
		try {
			serverSocket = java.net.ServerSocket(port)
		}
		catch(e : kotlin.Exception) {
			e.printStackTrace()
			v = false
		}
	}}}
	return v
}

func accept override as TCPSocket
{
	assert serverSocket
	var nsocket as !"java.net.Socket"
	lang "kotlin" {{{
		try {
			nsocket = serverSocket?.accept()
		}
		catch(e : kotlin.Exception) {
			e.printStackTrace()
			nsocket = null
		}
	}}}
	if nsocket {
		var v = new this()
		v.socket = nsocket
		return v
	}
	return null
}

func setBlocking(blocking as bool) override as bool
{
	this.blocking = blocking
	return true
}

func getBlocking override as bool:
	return blocking

func close override
{
	lang "kotlin" {{{
		try {
			if(socket != null) {
				socket?.close()
				socket = null
			}
			if(serverSocket != null) {
				serverSocket?.close()
				serverSocket = null
			}
		}
		catch(e : kotlin.Exception) {
			e.printStackTrace()
		}
	}}}
}

func read(buffer as buffer) override as int
{
	if buffer == null:
		return -1
	var v = 0
	lang "kotlin" {{{
		try {
			var ins = socket!!.getInputStream()
			if(blocking == false && ins.available() < 1) {
				return(0)
			}
			else {
				v = ins.read(buffer)
			}
		}
		catch(e : kotlin.Exception) {
			e.printStackTrace()
			v = -1
		}
	}}}
	if v < 1 {
		close()
		v = -1
	}
	return v
}

func write(buffer as buffer, size as int) override as int
{
	if buffer == null:
		return -1
	var v = 0
	lang "kotlin" {{{
		try {
			var os = socket?.getOutputStream()
			os!!.write(buffer)
			os!!.flush()
			v = size
		}
		catch(e : kotlin.Exception)
		{
			e.printStackTrace()
			v = -1
		}
	}}}
	if v < 1 {
		close()
		v = -1
	}
	return v
}
