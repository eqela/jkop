
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2022 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.data.sql
import jk.log

class is MSSQLDatabase:

IFDEF "target_mono" {
	depend "dll:System.Data.dll"
}

IFDEF "target_netcore" {
	depend "nuget:System.Data.SqlClient@4.5.1"
}

class Statement is SQLStatement
{
	func forSql(sql as string) static as this
	{
		assert String.isNotEmpty(sql)
		var v = new this()
		v.sql = SQLAdoNet.convertSQLString(sql)
		return v
	}

	var pcounter = 0
	var sql public as string
	var params as vector<!"System.Data.SqlClient.SqlParameter">
	var isStoredProcedure = false

	func setIsStoredProcedure(v as bool) as SQLStatement
	{
		isStoredProcedure = v
		return this
	}

	func getIsStoredProcedure as bool:
		return isStoredProcedure

	func getParamArray as array<!"System.Data.SqlClient.SqlParameter">
	{
		assert params
		var arr = new !"System.Data.SqlClient.SqlParameter"[Vector.getSize(params)]
		var i = 0
		var iter as Iterator<!"System.Data.SqlClient.SqlParameter"> #force = Vector.iterate(params)
		loop {
			var n = iter.next() as !"System.Data.SqlClient.SqlParameter"
			if not n:
				break
			arr[i] = n
			i++
		}
		return arr
	}

	func addToParams(param as !"System.Data.SqlClient.SqlParameter")
	{
		if not params:
			params = new vector<!"System.Data.SqlClient.SqlParameter">
		params += param
	}

	func getNextParamName as string:
		return "@p" .. String.forInteger(++ pcounter)

	func addParamString(val as string) as SQLStatement:
		return setParamString(null, val)

	func addParamInteger(val as int) as SQLStatement:
		return setParamInteger(null, val)

	func addParamLongInteger(val as long) as SQLStatement:
		return setParamLongInteger(null, val)

	func addParamDouble(val as double) as SQLStatement:
		return setParamDouble(null, val)

	func addParamBlob(val as buffer) as SQLStatement:
		return setParamBlob(null, val)

	func setParamString(name as string, val as string) as SQLStatement
	{
		var n = name
		if String.isEmpty(n):
			n = getNextParamName()
		var vv = val
		if not vv:
			vv = ""
		lang "cs" {{{
			var param = new System.Data.SqlClient.SqlParameter(n, System.Data.SqlDbType.VarChar);
			param.Value = vv;
			addToParams(param);
		}}}
		return this
	}

	func setParamInteger(name as string, val as int) as SQLStatement
	{
		var n = name
		if String.isEmpty(n):
			n = getNextParamName()
		lang "cs" {{{
			var param = new System.Data.SqlClient.SqlParameter(n, System.Data.SqlDbType.Int);
			param.Value = val;
			addToParams(param);
		}}}
		return this
	}

	func setParamLongInteger(name as string, val as long) as SQLStatement
	{
		var n = name
		if String.isEmpty(n):
			n = getNextParamName()
		lang "cs" {{{
			var param = new System.Data.SqlClient.SqlParameter(n, System.Data.SqlDbType.BigInt);
			param.Value = val;
			addToParams(param);
		}}}
		return this
	}

	func setParamDouble(name as string, val as double) as SQLStatement
	{
		var n = name
		if String.isEmpty(n):
			n = getNextParamName()
		lang "cs" {{{
			var param = new System.Data.SqlClient.SqlParameter(n, System.Data.SqlDbType.Float);
			param.Value = val;
			addToParams(param);
		}}}
		return this
	}

	func setParamBlob(name as string, val as buffer) as SQLStatement
	{
		var n = name
		if String.isEmpty(n):
			n = getNextParamName()
		if not val:
			return this
		lang "cs" {{{
			var param = new System.Data.SqlClient.SqlParameter(n, System.Data.SqlDbType.VarBinary);
			param.Value = val;
			addToParams(param);
		}}}
		return this
	}

	func resetStatement
	{
		params = null
		pcounter = 0
	}

	func getError as string:
		return null

	func close:
		nop
}

class ResultSet is SQLResultSetIterator
{
	var ctx public as LoggingContext
	var reader public as !"System.Data.SqlClient.SqlDataReader"
	var connection public as !"System.Data.SqlClient.SqlConnection"
	var columnCount = -1
	var columnNames as vector<string>
	var hasNextRecord as bool

	func close override
	{
		if reader {
			reader.Close()
			reader = null
		}
		if connection {
			connection.Close()
			connection = null
		}
	}

	func nextValues(values as vector<object>) override as bool
	{
		assert values
		assert step()
		Vector.clear(values)
		var cols = getColumnCount()
		for(var n = 0; n < cols; n++) {
			var o = getColumnObject(n)
			if not o:
				o = ""
			values += o
		}
		return true
	}

	func step override as bool
	{
		assert reader
		if hasNextRecord {
			hasNextRecord = false
			return true
		}
		var v = false
		var error as string = null
		lang "cs" {{{
			try {
				v = reader.Read();
			}
			catch(System.Exception e) {
				v = false;
				error = e.ToString();
			}
			if(v == false) {
				close();
			}
		}}}
		if String.isNotEmpty(error):
			Log.error(ctx, error)
		return v
	}

	func getColumnCount override as int
	{
		if columnCount >= 0:
			return columnCount
		var v = -1
		lang "cs" {{{
			if(reader != null) {
				v = reader.FieldCount;
			}
		}}}
		columnCount = v
		return v
	}

	func getColumnNames override as vector<string>
	{
		if columnNames == null {
			columnNames = new vector<string>
			var c = getColumnCount()
			for(var n = 0; n < c; n++) {
				var nn = getColumnName(n)
				if not nn:
					nn = ""
				columnNames += nn
			}
		}
		return columnNames
	}

	func getColumnName(n as int) override as string
	{
		var v as string = null
		lang "cs" {{{
			if(reader != null) {
				v = reader.GetName(n);
			}
			if(v == null) {
				v = "";
			}
		}}}
		return v
	}

	func getColumnObject(n as int) override as object
	{
		assert reader
		var v as string = null
		lang "cs" {{{
			if(reader.IsDBNull(n)) {
				return(null);
			}
			if(reader.GetFieldType(n) == typeof(byte[])) {
				var b = (byte[])reader.GetValue(n);
				return(jk.lang.Buffer.asObject(b));
			}
			var o = reader.GetValue(n);
			if(o == null) {
				return(null);
			}
			if(o is System.DateTime) {
				return(((System.DateTime)o).ToString("yyyy-MM-dd HH:mm:ss"));
			}
			v = o.ToString();
		}}}
		return v
	}

	func getColumnInt(n as int) override as int
	{
		var v = 0
		lang "cs" {{{
			if(reader != null) {
				v = reader.GetInt32(n);
			}
		}}}
		return v
	}

	func getColumnLong(n as int) override as long
	{
		var v as long = 0
		lang "cs" {{{
			if(reader != null) {
				v = reader.GetInt64(n);
			}
		}}}
		return v
	}

	func getColumnDouble(n as int) override as double
	{
		var v as double = 0
		lang "cs" {{{
			if(reader != null) {
				v = reader.GetDouble(n);
			}
		}}}
		return v
	}

	func getColumnBuffer(n as int) override as buffer
	{
		var v as buffer = null
		lang "cs" {{{
			if(reader != null) {
				v = (byte[])reader.GetValue(n);
			}
		}}}
		return v
	}

	func next override as DynamicMap
	{
		assert step()
		var names = getColumnNames()
		if not names || sizeof names < 1:
			return null
		var v = new DynamicMap()
		var sz = sizeof names
		for(var n = 0; n < sz; n++):
			v.setObject(names[n], getColumnObject(n))
		return v
	}

	func hasNext override as bool
	{
		if hasNextRecord:
			return true
		var v = step()
		if v {
			hasNextRecord = true
			return true
		}
		return false
	}
}

var connectionString as string

func setConnectionDetails(serverAddress as string, username as string, password as string, database as string) override
{
	databaseName = database
	connectionString = "Asynchronous Processing=true;Data Source=tcp:" .. serverAddress .. ";Initial Catalog=" .. database .. ";User Id=" .. username .. ";Password=" .. password
	Log.debug(ctx, "MS SQL connection string: '" .. connectionString .. "'")
}

func closeConnection async override:
	connectionString = null

func prepareStatement(sql as string) async override as SQLStatement:
	return Statement.forSql(sql)

func executeStatement(stmt as SQLStatement) override async as bool
{
	var ss = assert stmt as Statement
	var v = true
	var error as string = null
	lang "cs" {{{
		using(System.Data.SqlClient.SqlConnection conn = new System.Data.SqlClient.SqlConnection(connectionString)) {
			try {
				System.Data.SqlClient.SqlCommand cmd = new System.Data.SqlClient.SqlCommand(ss.sql, conn);
				cmd.CommandTimeout = 0;
				if(ss.getIsStoredProcedure()) {
					cmd.CommandType = System.Data.CommandType.StoredProcedure;
				}
				System.Data.SqlClient.SqlParameter[] pa = ss.getParamArray();
				if(pa != null) {
					cmd.Parameters.AddRange(ss.getParamArray());
				}
				conn.Open();
				cmd.ExecuteNonQuery();
			}
			catch(System.Exception e) {
				error = e.ToString();
				v = false;
			}
		}
	}}}
	if error:
		Log.error(ctx, error)
	return v
}

func executeUpdateDeleteStatement(stmt as SQLStatement) override async as int
{
	var ss = stmt as Statement
	if not ss:
		return 0
	var v = 0
	var error as string = null
	lang "cs" {{{
		using(System.Data.SqlClient.SqlConnection conn = new System.Data.SqlClient.SqlConnection(connectionString)) {
			try {
				System.Data.SqlClient.SqlCommand cmd = new System.Data.SqlClient.SqlCommand(ss.sql, conn);
				cmd.CommandTimeout = 0;
				if(ss.getIsStoredProcedure()) {
					cmd.CommandType = System.Data.CommandType.StoredProcedure;
				}
				System.Data.SqlClient.SqlParameter[] pa = ss.getParamArray();
				if(pa != null) {
					cmd.Parameters.AddRange(ss.getParamArray());
				}
				conn.Open();
				v = cmd.ExecuteNonQuery();
			}
			catch(System.Exception e) {
				error = e.ToString();
				v = 0;
			}
		}
	}}}
	if error:
		Log.error(ctx, error)
	return v
}

func executeQueryStatement(stmt as SQLStatement) override async as SQLResultSetIterator
{
	var ss = assert stmt as Statement
	var v as ResultSet = null
	var error as string = null
	var reader as !"System.Data.SqlClient.SqlDataReader"
	var connection as !"System.Data.SqlClient.SqlConnection"
	lang "cs" {{{
		connection = new System.Data.SqlClient.SqlConnection(connectionString);
		try {
			System.Data.SqlClient.SqlCommand cmd = new System.Data.SqlClient.SqlCommand(ss.sql, connection);
			cmd.CommandTimeout = 0;
			if(ss.getIsStoredProcedure()) {
				cmd.CommandType = System.Data.CommandType.StoredProcedure;
			}
			System.Data.SqlClient.SqlParameter[] pa = ss.getParamArray();
			if(pa != null) {
				cmd.Parameters.AddRange(pa);
			}
			connection.Open();
			reader = cmd.ExecuteReader();
		}
		catch(System.Exception e) {
			error = e.ToString();
			if(reader != null) {
				reader.Close();
				reader.Dispose();
				reader = null;
			}
			if(connection != null) {
				connection.Close();
				connection.Dispose();
				connection = null;
			}
		}
	}}}
	if reader {
		v = new ResultSet()
		v.reader = reader
		v.ctx = ctx
		v.connection = connection
	}
	if error:
		Log.error(ctx, error)
	return v
}
