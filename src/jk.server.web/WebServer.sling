
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2018 Job and Esther Technologies Oy
 * Copyright (c) 2018-2019 Eqela Oy
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.fs
import jk.env
import jk.http.server

class is CommonServer:

class MyHTTPServer is HTTPServer
{
	prop myParent as parent

	func onMaintenance override
	{
		base.onMaintenance()
		myParent.onMaintenance()
	}
}

prop enableHttpsForwarder = false
prop enableRequestLogging = true
prop enableRateLimit as string
prop timeoutDelay = 30
prop debugErrors = false
prop handler as HTTPServerRequestHandler

ctor
{
	envConfigure("enableHttpsForwarder", EnvironmentVariable.get("SYMPATHY_ENABLE_HTTPS_FORWARDER"))
	envConfigure("enableRequestLogging", EnvironmentVariable.get("SYMPATHY_ENABLE_REQUEST_LOGGING"))
	envConfigure("enableRateLimit", EnvironmentVariable.get("SYMPATHY_ENABLE_RATE_LIMIT"))
	envConfigure("timeoutDelay", EnvironmentVariable.get("SYMPATHY_TIMEOUT_DELAY"))
	envConfigure("debugErrors", EnvironmentVariable.get("SYMPATHY_DEBUG_ERRORS"))
}

func configure(key as string, value as string, relativeTo as File, error as Error) override as bool
{
	if key == "enableHttpsForwarder" {
		enableHttpsForwarder = Boolean.asBoolean(value)
		return true
	}
	if key == "enableRequestLogging" {
		enableRequestLogging = Boolean.asBoolean(value)
		return true
	}
	if key == "enableRateLimit" {
		enableRateLimit = value
		return true
	}
	if key == "timeoutDelay" {
		timeoutDelay = Integer.asInteger(value)
		if timeoutDelay < 30:
			timeoutDelay = 30
		return true
	}
	if key == "debugErrors" {
		debugErrors = Boolean.asBoolean(value)
		return true
	}
	return base.configure(key, value, relativeTo, error)
}

func addHttpsForwarderHandler(server as HTTPServer)
{
	server.pushRequestHandler(func(req as HTTPServerRequest, next as function) {
		var hdr = req.getHeader("x-forwarded-proto")
		if not String.equals(hdr, "http") {
			next()
			return
		}
		var host = req.getHeader("host")
		var path = req.getUrlString()
		if String.isEmpty(host) || String.isEmpty(path) {
			next()
			return
		}
		req.sendResponse(HTTPServerResponse.forHTTPTemporaryRedirect("https://" .. host .. path))
	})
}

func initializeServer(server as HTTPServer) virtual as bool
{
	server.setDebugErrors(debugErrors)
	if enableRateLimit {
		var limit = new HTTPServerRateLimitHandler()
		if enableRateLimit == "true" || enableRateLimit == "yes" {
			nop // all good, use default settings
		}
		else {
			var comps = String.split(enableRateLimit, ':', 3)
			limit.setCountLimit(Integer.asInteger(Vector.get(comps, 0)))
			limit.setCountDuration(Integer.asInteger(Vector.get(comps, 1)))
			limit.setIgnoreDuration(Integer.asInteger(Vector.get(comps, 2)))
		}
		server.pushRequestHandler(limit)
	}
	if enableHttpsForwarder:
		addHttpsForwarderHandler(server)
	if handler:
		server.pushRequestHandler(handler)
	return true
}

func initializeNetworkServer(server as NetworkServer) override as bool
{
	assert base.initializeNetworkServer(server)
	if server is HTTPServer {
		assert initializeServer(server as! HTTPServer)
		if enableRequestLogging:
			(server as! HTTPServer).addRequestHandlerListener(new HTTPServerRequestLogger().setLogContext(ctx).setLogdir(getLoggingDirectory()))
	}
	return true
}

func createServer override as NetworkServer
{
	var server = new MyHTTPServer()
	server.setTimeoutDelay(timeoutDelay)
	server.setMaintenanceTimerDelay(getMaintenanceTimerDelay())
	server.setMyParent(this)
	server.setPort(getListenPort())
	return server
}

func executeForHandler(handler as HTTPServerRequestHandler, args as array<string>) as int
{
	setHandler(handler)
	return executeMain(args)
}
